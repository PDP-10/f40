       TITLE RAN	V.022
       SUBTTLE   15-DECEMBER-1969     BRUNO G. DUERR/KK


	;FROM VERSION DATED 3/27/69
       ;PSEUDO RANDOM NUMBER GENERATOR AND INITIALIZING ROUTINE
       ;METHOD SUGGESTED BY D. H. LEHMER


       ;CALLING SEQUENCE FOR FUNCTION RAN:

       ;JSA Q,RAN
       ;EXP   ARG     (DUMMY ARGUMENT)
       ;ANSWER IS RETURNED IN ACCUMULATOR A AS A SINGLE
       ;PRECISION FLOATING POINT NUMBER IN THE RANGE
       ;0<X<1


       ;CALLING SEQUENCE FOR INITIALIZING SUBROUTINE SETRAN:

       ;JSA Q,SETRAN
       ;EXP   ARG (ARG IS THE ADDRESS OF A POSITIVE INTEGER <2**31)
       ;NOTE: IF ARG CONTAINS 0 A STANDARD STARTING VALUE IS TAKEN INSTEAD


       ;CALLING SEQUENCE FOR SAVRAN
 
       ;JSA Q,SAVRAN
       ;EXP ARG   (WHERE ARG IS THE ADDRESS FOR THE LAST RANDOM
       ;           NUMBER TO BE STORED IN INTEGER MODE)


       ENTRY RAN,SETRAN,SAVRAN

       A=0
       B=1
       Q=16


	SIXBIT/RAN/
RAN:   Z
       MOVE A,K           ;GET K [14**29(MOD2**31 -1)]
       MUL A,XN           ;MULTIPLY WITH LAST RANDOM NUMBER
       ASHC A,4           ;SEPARATE RESULT IN TWO 31 BIT WORDS
       LSH B,-4
       ADD A,B            ;ADD THEM TOGETHER
       TLZE A,760000      ;SKIP IF RESULT < 31 BITS
       ADDI A,1
       MOVEM A,XN         ;STORE NEW RN IN INTEGER MODE
       HLRZ B,A           ;CONVERT TO FP IN TWO STEPS IN
       FSC B,216          ;ORDER TO LOOSE NO LOW ORDER
       HRLI A,A           ;BITS
       FSC A,174
       FAD A,B
       JRA Q,1(Q)         ;RETURN TO CALLING PROGRAM
K:     ^D630360016        ;14**29(MOD 2**31 -1)
XN:    ^D524287           ;STARTING VALUE



	SIXBIT/SETRAN/
SETRAN:Z
       MOVE A,@0(Q)       ;GET ARGUMENT FROM CALLING PROGRAM
       TLZ A,760000       ;MASK 5 BITS FOR SAFETY
       CAIN A,            ;SKIP IF ARGUMENT NONZERO
       MOVE A,X0          ;GET STANDARD STARTING VALUE
       MOVEM A,XN
       JRA Q,1(Q)         ;RETURN
X0:    ^D524287           ;STANDARD STARTING VALUE FOR RAN


	SIXBIT/SAVRAN/
SAVRAN:Z
       MOVE A,XN          ;LAST RANDOM NUMBER TO AC0
       MOVEM A,@0(Q)      ;STORE IN INTEGER MODE
       JRA Q,1(Q)         ;RETURN
       END

