TITLE DFDV V.027J	1-OCT-1970
SUBTTL DOUBLE PRECISION FLOATING DIVIDE- TOM EGGERS/TWE

Q=16		;POINTER TO MEMORY ARGUMENT
M=Q		;PNT TO MEMORY ARG (MORE MNEMINIC)
P=17		;PUSH DOWN POINTER

DEFINE FLDIV(X,%OV)
<	FDVL X,(M)		;GET QUOTIENT AND REMAINDER
	JOV %OV		;XFER FOR UNDER/OVERFLOW(NOT QUITE RIGHT FOR OVERFLOW)
	PUSH P,X+2		;GET X+2 OUT OF WAY
	PUSH P,(M)		;GET HIGH PART OF MEM ARG ON PDL
	MOVN X+2,X		;NEGATE HIGH ORDER DIVIDEND
	FMPR X+2,1(M)		;MUL THE LOW ORDER ANSWER
	JFCL (2)			;IN CASE OF UNDERFLOW, UN-NORMALIZE
	UFA X+1,X+2		;ADD LOW PROD. TO -DIVIDEND
	FDVR X+2,(P)		;DIV THE LOW BITS BY DIVISOR
	JFCL			;IN CASE OF UNDERFLOW
	FADL X,X+2		;ADD FINAL RESULT INTO X,X+1
	POP P,X+2		;REMOVE ITEM FROM LIST
	POP P,X+2		;RESTORE X+2
%OV: >

DEFINE DFD(X)<

ENTRY DFD.'X
	SIXBIT /DFD.'X/
DFD.'X:	FLDIV(X)
	POPJ P,
>

DEFINE DFDM(X)<

ENTRY DFDM.'X
	SIXBIT /DFDM.'X/
DFDM.'X:	PUSHJ P,DFD.'X
	MOVEM X,(M)		;STORE HIGH PART
	MOVEM X+1,1(M)		;STORE LOW PART
	POPJ P,
>

DEFINE EXPAND(X)<
IRP X,<
DFD X
DFDM X
>>

EXPAND <0,2,4,6>

END

