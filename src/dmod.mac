TITLE DMOD  V.027       PDP-10 DOUBLE PRECISION MOD FUNCTION
SUBTTL	17-JUL-70	/KK/DMN
;FROM	V.023	12-JAN-70

;FROM V.022	13-DEC-69	/TWE
;FROM V.020,	28 APRIL 1969	/TWE
;FROM V.005, 15 MAY 1967
;THIS ROUTINE CALCULATES
;	MOD(A,B) = A-[A/B]*B
;FOR DOUBLE PRECISION ARGUMENTS A AND B, WHERE [A/B] IS THE
;GREATEST INTEGER IN THE MAGNITUDE OF A/B.


;THE CALLING SEQUENCE FOR THE ROUTINE IS AS FOLLOWS:
;	JSA	Q, DMOD
;	EXP	ARG1
;	EXP	ARG2
;ARG1 AND ARG2 ARE THE ADDRESSES OF THE HIGH ORDER PORTIONS OF
;THE DOUBLE PRECISION ARGUMENTS. THE DOUBLE PRECISION ANSWER IS
;RETURNED IN ACCUMULATORS A AND B.

	ENTRY	DMOD
	EXTERN	TYPER.


P=17	;PUSH DOWN POINTER
Q=16	;JSA-JRA ACCUMULATOR

	SIXBIT /DMOD/
DMOD:	0			;ENTRY TO DMOD ROUTINE.
	MOVEI	1,@1(Q)		;GET ADDRESS OF ARG B.
	MOVE	0,(1)		;GET HIGH OF ARG B.
	MOVE	1,1(1)		;GET LOW OF ARG B.
	SKIPGE	0		;GET
	DFN	0,1		;/ARG B/.
	DMOVEM	0,ARGB		;SAVE ARG B IN ARGB.
	MOVEI	1,@(16)		;GET ADDRESS OF ARG A.
	MOVE	0,(1)		;GET HIGH OF ARG A.
	MOVE	1,1(1)		;GET LOW OF ARG A.
	SKIPGE	0		;GET
	DFN	0,1		;/ARG A/.
	DMOVEM	0,ARGA		;SAVE ARG A IN ARGA.
	MOVEM	2,SAV2		;SAVE AC 2.

	FDVL	0,ARGB		;DIVIDE A BY B
	JFCL	OVUNFL		;IF
	MOVN	2,0		;OVER
	FMPR	2,ARGB+1	;OR
	JFCL			;UNDER
	UFA	1,2		;FLOW
	FDVR	2,ARGB		;OCCURS,
	JFCL			;GO
	FADL	0,2		;TO
	JFCL	OVUNFL		;OVUNFL.

	MOVEM	0,SAVNUM	;SAVE NUMBER
	MOVEM	1,SAVNUM+1	;IN SAVNUM.
	LDB	2,[POINT 8,0,8]	;GET EXPONENT
	TRC	2,777777	;GET 1'S COMPLEMENT OF EXPONENT
	MOVSI	0,777000	;INITIALIZE MASK
	MOVEI	1,0		;IN 0 AND 1.
	CAIL	2,777577	;IS THE NUMBER ALL FRACTION BITS?
	TDZA	0,0		;YES, A FRACTION WILL TRUNCATE TO 0
	ASHC	0,201(2)	;CREATE MASK(SHIFT TO RIGHT ONLY)
				;REMEMBER- EXPONENT IS 1'S COMP NEGATIVE
	ASH	1,-8		;PROTECT LOW ORDER EXPONENT
	AND	0,SAVNUM	;MASK HI WORD
	AND	1,SAVNUM+1	;AND LOW WORD
	FADL	0,1		;AND STRAIGHTEN 0'S OUT.
	FLMUL	0,ARGB		;CALCULATE [A/B]*B.
	DFN	0,1		;GET -[A/B]*B
	FLADD	0,ARGA		;CALC. A-[A/B]*B.
	MOVE	2,SAV2		;RESTORE AC 2.
	SKIPGE	@(16)		;GIVE THE ANSWER
	DFN	0,1		;THE CORRECT SIGN.
	JRA	Q,2(Q)		;EXIT.

OVUNFL:	MOVE	2,SAV2		;RESTORE AC 2.
	JUMPE	0,ANSISA	;IF UNDERFLOW, GO TO ANSISA.
	MOVEI	0,3		;OVERFLOW.  RETURN AN
	PUSHJ	17,TYPER.	;ERROR MESSAGE AND
	SETZB	0,1		;AN ANSWER OF ZERO AND
	JRA	Q,2(Q)		;EXIT.
ANSISA:	MOVEI	1,@(16)		;UNDERFLOW.  ANS = A
	MOVE	0,(1)
	MOVE	1,1(1)
	JRA	Q,2(Q)

ARGA:	BLOCK 2
ARGB:	BLOCK 2
SAVNUM:	BLOCK 2
SAV2:	BLOCK 1

END

