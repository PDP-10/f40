TITLE INTO.  V.030.0.63   DECIMAL INTEGER OUTPUT FORTRAN IV
SUBTTL	2-APR-71	/DMN
;FROM	V.022 25-NOV-69 FROM V.21 31-JULY-1969    /KK /VAA

;REENT==1 GIVES RE-ENTRANT FORTRAN OP SYSTEM
IFNDEF REENT,<REENT==0>
IFN REENT,<	HISEG	>
IFNDEF ASTFL,<ASTFL==1	;GIVES ASTERISK FILL IF FORMAT OVERFLOWS>

;"INTO." IS A ROUTINE WHICH OUTPUTS DECIMAL ASCII
;CHARACTERS TO AN EXTERNAL ROUTINE, "DEPOT." THE INTEGER IS
;CONTAINED IN (ACO) ON ENTRY, AND THE OUTPUT ITEM IS RETURNED
;IN (ACO).

;CALLING SEQUENCE:
;	PUSHJ P,INTO.
;PUSH DOWN LIST CONTAINS:
;	1.	FIELD WIDTH W, INTEGER IN LEFT HALF
;		W=0 MEANS VARIABLE FIELD
;	2.	PROGRAM COUNTER (RETURN ADDRESS)
;	3. THRU 19. FREE STORAGE (ACS AND CHARACTERS)

	ENTRY	INTO.
	EXTERN	DEPOT.
IFN ASTFL,<EXTERN	ASTRK.>

INTO.:	PUSH	PDP,ADO		;SAVE
	PUSH	PDP,ACW		;THE
	PUSH	PDP,ACCH	;ACS IN THE
	PUSH	PDP,A		;PUSH DOWN LIST
	LDB	ACW,FIELD	;PICK UP FIELD WIDTH
	JUMPN	ACW,.+2		;VARIABLE FIELD?
	MOVEI	ACW,17		;YES, SET WIDTH=15
	MOVEI	ACCH,0		;SET CHAR. COUNT=0
	JUMPGE	ACO,MAKDEC	;IT INTEGER + OR - ?
	TLO	ADO,400000	;SET UP NEG. FLAG
	MOVNS	ACO		;NEG. - MAKE POSITIVE.
	JFCL	LRGNEG		;IF - 2**35, GO TO LRGNEG.
MAKDEC:	IDIVI	ACO,12		;FORM AN
	ADDI	A,60		;INTEGER
	ADDI	ACCH,1		;INCREMENT CHAR COUNT
	PUSH	PDP,A		;STORE INTEGER
	JUMPG	ACO,MAKDEC	;ENTIRE NO. YET?
	MOVEI	ACO,40		;YES, SET UP LEADING BLKS
	MOVE	A,ACCH		;SAVE COUNT
	SUBM	ACW,ACCH	;WIDTH/NO. OF CHARS.
	JUMPG	ACCH,NOSW	;IS FIELD OK?
	JUMPL	ACCH,.+2	;NO, NO. OF CHAR >,=W
	JUMPGE	ADO,CHARO	;NO. OF CHAR=W FIELD OK
				;IF NO.> 0.
IFN ASTFL,<SUBM	ACW,ACCH	;RESTORE ORIGINAL FIELD WIDTH>
IFE ASTFL,<MOVMS ACCH		;MAKE CTR POSITIVE>
	HRLS	ACCH
	SUB	PDP,ACCH	;ADJUST PNTR,SKIP
				;EXTRA CHARS >W
IFN ASTFL,<PUSHJ PDP,ASTRK.	;OUTPUT *'S IN WHOLE FIELD
	JRST	RTNIO		;AND RETURN>
IFE ASTFL,<MOVE	A,ACW		;SAVE FIELD WIDTH
	JRST	CHARO		;OUTPUT CHARACTERS>
NOSW:	SOJE	ACCH,SIGN	;ANY LEADING BLANKS
OUTBLK:	PUSHJ	PDP,DEPOT.	;YES
	SOJG	ACCH,OUTBLK
SIGN:	JUMPGE	ADO,.+2		;IS NO. + OR - ?
	MOVEI	ACO,55		;OUTPUT A MINUS
	PUSHJ	PDP,DEPOT.	;OUTPUT A BLANK
CHARO:	JUMPLE	A,RTNIO		;IF W<,=0, RETURN
OUTGET:	POP	PDP,ACO		;BRING UP A CHAR.
	PUSHJ	PDP,DEPOT.	;OUTPUT IT
	SOJG	A,OUTGET	;ANY MORE CHARS?
RTNIO:	POP	PDP,A		;NO, RESTORE
	POP	PDP,ACCH	;ACS
	POP	PDP,ACW		;TO ORIGINAL
	POP	PDP,ADO		;VALUES
	POPJ	PDP,		;RETURN


LRGNEG:	HRLOI	ACO,377777	;SET UP NUMBER - 1.
	IDIVI	ACO,12		;GET A CHARACTER.
	ADDI	A,61		;FIX CHAR TO CHAR + 1.
	JRST	MAKDEC+2	;RETURN TO ROUTINE.

FIELD:	POINT	7,-5(PDP),17;FIELD WIDTH IN FORMAT WORD

	ACO=0			;RETURNS OUTPUT ITEM
	A=1			;STORAGE
	ADO=2			;FLAG
	ACCH=3			;CHAR.COUNT
	ACW=4			;FIELD WIDTH.
	PDP=17				;PUSHDOWN POINTER.


	END





