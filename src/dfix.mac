TITLE DFIX	V.30A	DOUBLE PRECISION TO INTEGER CONVERSION
SUBTTL	7-OCT-1970	TOM EGGERS

Q=16

ENTRY DFIX,IDFIX
	SIXBIT /IDFIX/
IDFIX:
DFIX:	0
	MOVEM	2,SAV2		;SAVE A TEMP
	MOVEI	1,@(Q)		;GET ADDRESS OF D.P. ARGUMENT
	DMOVE	0,0(1)		;PICK UP D.P. ARG
	SKIPGE	0		;TAKE ABSOLUTE VALUE
	DFN	0,1		;  ...
	HLRZ	2,0		;GET EXPONENT INTO
	ASH	2,-9		;   RIGHT 8 BITS OF AC 2
	TLZ	0,777000	;WIPE OUT EXPONENT IN ARGUMENT
	LSH	1,8		;WIPE OUT LOW EXPONENT IN ARGUMENT
	ASHC	0,-201-^D26(2)	;CHANGE FRACTION BITS TO INTEGER
	MOVE	2,SAV2		;RESTORE AC 2
	SKIPGE	@(Q)		;NEGATE INTEGER RESULT IF
	MOVNS	0		;  ORIGINAL ARGUMENT WAS NEGATIVE
	JRA	Q,1(Q)		;RETURN

SAV2:	BLOCK	1		;TEMP FOR AC 2

	END
