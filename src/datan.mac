TITLE DATAN  V.020    PDP-10 DOUBLE PRECISION ARCTANGENT FUNCTION

SUBTTL	APRIL 28,1969	/TWE
		;FROM V.005, 12-APRIL-1967

;THIS ROUTINE CALCULATES THE ACTANGENT OF A DOUBLE PRECISION
;ARGUMENT ACCORDING TO THE ALGORITHM

;DATAN(X) = LAMBDA*X/(Z+B0+A1/(Z+B1+A2/(Z+B2+A3/(Z+B3))))

;FOR X>1.0, THE IDENTITY
;			ATAN(X) = PI/2 - ATAN(1/X)
;IS USED. FOR 0.5<X<1.0, THE IDENTITY
;			ATAN(X) = ATAN(1/2) + ATAN(2X-1/X+2)
;IS USED.
;FOR X<SQRT(3)*2**-27, ATAN(X)=X IS USED

;THE ROUTINE HAS THE FOLLOWING CALLING SEQUENCE
;	JSA	Q, DATAN
;	EXP	ARG
;WHERE ARG IS THE ADDRESS OF THE HIGH ORDER PART OF THE DOUBLE
;PRECISION WORD. THE ANSWER IS RETURNED IN ACCUMULATORS A AND B.

	ENTRY	DATAN

A=	0
B=	1
C=	2
D=	3
E=	4
F=	5
G=	6	;FLAG REGISTER
	;BIT35=1, ADD ATAN(1/2) TO ANSWER
	;BIT34=1,ADD -PI/2 TO ANSWER
	;BIT0=1, NEGATE FINAL ANSWER

H=	7		;LOOP POINTER

Q=	16
P=	17

X=	H		;NUMBER OF ACCUMULATORS TO SAVE

	SIXBIT /DATAN/

DATAN:	0			;ENTRY TO DATAN ROUTINE
	MOVE	0,XBLT		;SAVE ACCUMULATORS
	BLT	0,ACSAVE+X-C	;...

	MOVEI	B,@(Q)		;GET POINTER TO ARG
	MOVE	A,(B)		;GET HI PART
	MOVE	B,1(B)		;GET LO PART
	JUMPE	A,DATAN6	;ARG .E. 0?
	HLLZ	G, A		;LH(G)=SGN(A), RH(G) = 0
	SKIPGE	A		;IS THE ARGUMENT POSITIVE?
	DFN	A, B		;NO,NEGATE IT
	MOVSI	D, (1.0)	;GET DOUBLE PRECISION 1.0
	MOVEI	E,0		;0 LO PART
	CAMN	A,D		;IS HIGH ORDER EQUAL TO 1.0?
	SKIPE	B		;YES, IS LOW ORDER ZERO?
	CAMGE	A,D		;NO, IS ARG>1.0?
	JRST	DATAN0		;NO
	TLC	G,(1B0)		;COMPLEMENT FINAL SIGN BIT, GET 1/X
	TRO	G,2		;ADD -PI/2 TO FINAL ANSWER
	FLDIV	D,A
	DMOVEM	D,A

DATAN0:	DMOVEM	A,DX
	CAMGE	A,HALF		;IS ARG .GE. 0.5?
	JRST	DATAN1		;NO, PROCEED WITH ALGORITHM
				;CALCULATE X+2
	FLADD	A,TWO
	EXCH	A,DX		;GET X, SAVE X+2
	EXCH	B,DX+1		;...
	FSC	A,1		;CALCULATE 2X
	FSC	B,1		;...
	FADL	A,B		;...
				;CALCULATE 2X-1
	FLADD	A,MONE
				;(2X-1)/(X+2) WITH RESULTS IN A,B
	FLDIV	A,DX
	DMOVEM	A,DX
	TRO	G,1		;SET FLAG TO LATER ADD ATAN(1/2)

DATAN1:	CAMGE	A,SMALL		;CAN ATAN(X)=X?
	JRST	DATAN3		;YES
			;CALCULATE X**2
FLMUL A,DX
DMOVEM A,X2
				;INIT CONTINUED FRACTION COMP. WITH B3
DMOVE A,B3

	MOVEI	H,B3		;INIT POINTER TO NUMBER TABLE
	JRST	DAT2		;DIVE INTO LOOP

DAT1:				;ADD B1
FLADD A,0(H)

DAT2:				;ADD X**2
FLADD A,X2
				;GET A3 (OR A1)
DMOVE D,-2(H)
FLDIV D,A
				;ADD B2 (OR B0)
FLADD D,-4(H)
				;ADD X**2
FLADD D,X2
				;GET A2 (OR LAMBDA)
DMOVE A,-6(H)
FLDIV A,D

	SUBI	H,8		;DECREMENT TABLE POINTER
	CAILE	H,LAMBDA	;FINISHED?
	JRST	DAT1		;NO, DO IT LAST TIME
				;MULTIPLY BY X
FLMUL A,DX

DATAN3:	TRNN	G,1		;ADD ATAN(1/2)?
	JRST	DATAN4		;NO
FLADD A,ATANH

DATAN4:	TRNN	G,2		;ADD -PI/2?
	JRST	DATAN5		;NO
FLADD A,MPIOT

DATAN5:	SKIPGE	G		;NEGATE RESULT?
	DFN	A,B		;YES
DATAN6:	MOVS	X,XBLT		;RESTORE ACCUMULATORS
	BLT	X,X		;...
	JRA	Q,1(Q)		;EXIT


LAMBDA:	204613772770		;12.37469 38775 51020 40816
	151036057513
B0:	205644272446		;26.27277 52490 26980 67155
	152242672522
A1:	570276502107		;-80.34270 56102 16599 70467
	154076375544
B1:	203627237361		;6.36424 16870 04411 34492
	150353030306
A2:	576316772502		;-1.19144 72238 50426 48905
	146225160256
B2:	202415301602		;2.10451 89515 40978 95180
	147032562064
A3:	602277106546		;-0.07833 54278 56532 11777
	142636357263
B3:	201502125320		;1.25846 41124 27629 031727
	146760417550

ATANH:	177732614701		;ATAN(1/2)
	144247502334

HALF:	0.5
MONE:	EXP -1.0,0
TWO:	EXP +2.0,0
MPIOT:	EXP 576155700452,146735722717	;-PI/2
SMALL:	XWD 146673,317272		;SQRT(3)*2**-27

XBLT:	XWD	C, ACSAVE
ACSAVE:	BLOCK	X-C+1
DX:	BLOCK 2		;HOLDS X MOST OF THE TIME
X2:	BLOCK 2		;HOLDS X**2

	END

