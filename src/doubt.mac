TITLE DOUBT.  V.030.0.140    DOUBLE PRECISION OUTPUT PDP-10
SUBTTL	28-APR-71	/DMN
		;FROM	V.027U	9-NOV-70	/DMN
		;FROM	V.022- 1-DEC-69	/TWE
		;FROM V.020-APRIL 23,1969	/TWE
		;V.005  10-MAR-67

;REENT==1 GIVES RE-ENTRANT FORTRAN OP SYSTEM
IFNDEF REENT,<REENT==0>
IFN REENT,<	HISEG	>
IFNDEF ASTFL,<ASTFL==1	;GIVES ASTERISK FILL ON FORMAT OVERFLOW>

IFNDEF KI10,<PRINTX MUST BE ASSEMBLED WITH DEF40>		; *EJG* 06/26/76
								; *EJG* 06/26/76
;	DOUBT. OUTPUTS ONE DOUBLE PRECISION WORD.  IT IS
;	CALLED BY 

;	PUSH	17,FMTWRD
;	MOVE	0,A
;	MOVE	1,A+1
;	PUSHJ	P,DOUBT.

;	WHERE A IS THE ADDRESS OF THE DOUBLE PRECISION WORD
;	AND FMTWRD IS A FORMAT WORD AS DESCRIBED IN FLOUT.

	ENTRY	DOUBT.
	EXTERN	DEPOT.
IFN ASTFL,<EXTERN	ASTRK.>
	EXTERN	TAB.P,TAB.P1,TAB.P2				; *EJG* 06/26/76
IFE KI10,<							; *EJG* 06/26/76
	EXTERN	DFM..						; *EJG* 06/26/76
>								; *EJG* 06/26/76
	EXTERN	TAB.M,TAB.M1,TAB.M2

AC=0		;AC+1 IS ALSO USED
MUL=AC+1	;MUL+1 IS ALSO USED
BITS=3
IN=5
XP=4
D=5
W=1
FR=10		;FR+1 IS ALSO USED
IGN=12
S=13
DIG=14
R=15
Q==15
P=17

NAC==15

IFE KI10,<							; *EJG* 06/26/76
MAXDIG==^D16
>								; *EJG* 06/26/76
IFN KI10,<				;KI10 CAN USE HW FORMAT	; *EJG* 06/26/76
MAXDIG==^D18							; *EJG* 06/26/76
>								; *EJG* 06/26/76
MINDIG==^D9	;NO. OF DIGITS IF LS. WORD NOT SIGNIFICANT

BITNEG==40000
BITZ==100000
LZEROB==200000

ABLANK==40
APLUS==53
AMINUS==55
APOINT==56
AZERO==60
AD==104
AO==117
DOUBT.:	PUSH	P,NAC		;PRESERVE ACCUMULATORS
	MOVEI	NAC,1(P)	;...
	ADD	P,[XWD NAC,NAC]	;...
	BLT	NAC,(P)		;...
	MOVE	BITS,-2-NAC(P)	;GET FORMAT WORD
	TLZ	BITS,BITNEG+BITZ+LZEROB	;SET INDICATORS OFF
	JUMPE	AC,ZERO		;THE TRIVIAL CASE
	JUMPG	AC,POS		;IS NUMBER NEGATIVE?
	DFN	AC,AC+1		;YES, MAKE IT POSITIVE
	TLO	BITS,BITNEG	;REMEMBER THE MINUS
POS:	JUMPN	AC+1,POS1	;IF LS. WORD = 0
	LDB	S,[POINT 9,AC,8] ;AND EXPONENT OF MS. WORD GT. 33
	CAIG	S,33
	TLO	BITS,LZEROB	;SET LZEROB
POS1:	MOVEI	IN,13		;TOP OF COMPARE TABLE
	MOVEI	XP,0		;INITIAL DECIMAL EXPONENT
	CAML	AC,TAB.P2	;IS NUMBER >= 1.0?
	JRST	LUPP		;YES, BRING IT DOWN
	CAMN	AC,TAB.M1	;...
	CAML	AC+1,TAB.M2	;...
	CAMGE	AC,TAB.M1	;...
	JRST	LUPM		;YES, BRING IT UP
REND:	LDB	S,[POINT 9,AC,8];GET BINARY EXP
	TLZ	AC,777000	;CLEAR EXP OUT OF FRACTION
IFE KI10,<							; *EJG* 06/26/76
	LSH	AC+1,10		;REMOVE EXP. FROM LOW ORDER WORD
>								; *EJG* 06/26/76
	ASHC	AC,8-200(S)	;MOVE FRACTION TO BINAL POINT
	MOVE	FR,AC		;PLACE IN FRACTION REGISTER
	MOVE	FR+1,AC+1	;...
ZEROH:	LDB	D,[POINT 7,BITS,10];DIGITS OF SIGNIFICANCE
	LDB	W,[POINT 7,BITS,17];WIDTH OF FIELD
	HRRE	S,BITS		;SCALE FACTOR
	SUB	XP,S		;SCALE DECIMAL EXPONENT
	JUMPN	W,DIGITS	;IS THIS UNSPECIFIED FORMAT?
	MOVEI	D,MAXDIG	;YES, SET TO D<MAXDIG>+8.<MAXDIG>
	MOVEI	W,MAXDIG+^D8	;...
DIGITS:	JUMPLE	S,.+2		;+ SCALE IMPLIES D+1 DIGITS SIGNIFICANCE
	ADDI	D,1
	MOVM	DIG,S		;DIGITS OF SCALING
	CAILE	D,MAXDIG	;MAXIMUM SIGNIFICANCE EXCEEDED?
	MOVEI	D,MAXDIG	;NO IT ISN'T.
	TLZN	BITS,LZEROB	;IF LZEROB SET
	JRST	.+3
	CAILE	D,MINDIG	;CHECK NO MORE THAN
	MOVEI	D,MINDIG	;ONE WORD OF DIGITS
	MOVE	R,D		;SET ROUNDING POINT
	CAMGE	DIG,D		;MAXF (S,D)
	MOVE	DIG,D		;...
	HRREI	IGN,-6(W)	;W-6-DIGITS
	SUBB	IGN,DIG		;...
	JUMPG	S,SCAL		;IF PLUS SCALE, THEN LEFT ROUTINE
SCAR:	JUMPLE	IGN,RNBLK	;JUMP IF NO LEADING BLANKS
	TLO	BITS,LZEROB	;SPACE FOR LEADING ZERO
	SOJE	DIG,RNBLK	;JUMP IF LEADING ZERO WAS ONLY SPACE
	MOVEI	AC,ABLANK	;ASCII BLANK
	PUSHJ	P,OUTCH		;OUTPUT IT
	SOJG	DIG,.-2		;JUMP IF MORE BLANKS
RNBLK:	ADD	R,S		;ONLY ROUND ON SIGNIFICANT DIGITS
	PUSHJ	P,SGNRND	;OUTPUT SIGN AND ROUND
	MOVEI	AC,AZERO	;LEADING ZERO
	TLNE	BITS,LZEROB	;WAS THERE SPACE FOR IT?
	PUSHJ	P,OUTCH		;YES, OUTPUT
	MOVEI	AC,APOINT	;MAY THERE ALWAYS BE A DECIMAL POINT
	PUSHJ	P,OUTCH		;...
	ADD	D,S		;SCALE FACTOR IMPLIES EXTRA ZEROS
	JUMPGE	S,OGDIG		;NOT FOR THIS NUMBER
	MOVEI	AC,AZERO	;ASCII 0
	PUSHJ	P,OUTCH		;OUTPUT
	AOJL	S,.-2		;ANY MORE ZEROS?
	JRST	OGDIG		;NO.

SGNRND:	MOVEI	AC,ABLANK	;ASCII BLANK
	TLNE	BITS,BITNEG	;WAS NUMBER -?
	MOVEI	AC,100000+AMINUS	;ASCII MINUS
	PUSHJ	P,OUTCH		;OUTPUT SIGN
	JUMPL	R,NORND		;NOT ENUF TO ROUND
	CAILE	R,MAXDIG	;PERHAPS NO ROUND REQUIRED
	JRST	NORND		;ALL DIGITS OUTPUT
				;V.005 TO V.020 ON NEXT INSTRUCTION
	TLO	FR+1,400000	;SET SIGN BIT TO AVOID OVERFLOW
			;ON NEXT ADD. RNDL ENTRIES ARE ALWAYS .GE.0
	ADD	FR+1,RNDL(R)	;LO ORDER ROUND
				;V.005 TO V.020 ON NEXT 2 INSTRCTNS.
	TLO	FR,(1B0)	;TAKE CARE OF OVERFLOW TO HIGH WORD
	TLZN	FR+1,400000	;CLEAR BIT. WAS THERE CRY1?
	ADDI	FR,1		;YES. PROPOGATE CARRY
RND1:	ADD	FR,RNDH(R)	;HI ORDER ROUND
	TLC	FR,(1B0)	;BACK AS IT WAS UNLESS OVERFLOW
	JUMPGE	FR,NORND	;DID ROUND OVERFLO?
	MOVE	FR,[31463146314];YES,SET TO 0.1
	MOVE	FR+1,[314631463147]
	AOJA	XP,NORND	;INDICATE TO EXP
SCAL:	JUMPLE	IGN,LNBLK	;IS THERE SPACE FOR BLANKS?
	MOVEI	AC,ABLANK	;YES, OUTPUT SOME
	PUSHJ	P,OUTCH		;...
	SOJG	DIG,.-2		;OUTPUT SOME MORE
LNBLK:	SUB	D,S		;SCALE DIGITS OF SIGNIF.
	HRREI	DIG,-MAXDIG(S)	;S-MAXIMUM SIGNIF
	JUMPLE	DIG,.+2		;IS S>MAXDIG?
	MOVEI	S,MAXDIG	;PROHIBIT PRINTOUT OF GIGO
	ADD	R,S		;ROUND AFTER SCALING DIGITS
	PUSHJ	P,SGNRND	;OUTPUT SIGN AND ROUND
	PUSHJ	P,OUTDIG	;DIGITS BEFORE DECIMAL POINT
	SOJG	S,.-1		;OUTPUT MOST OF SCALE DIGITS
	JUMPLE	DIG,DECPNT	;MORE SCALE DIGITS?
	MOVEI	AC,AO		;MAKE SURE THEY'RE INSIGNIFICANT
	PUSHJ	P,OUTCH		;...
	SOJG	DIG,.-2		;UNTIL SCALE FULFILLED
DECPNT:	MOVEI	AC,APOINT	;DECIMAL POINT
	PUSHJ	P,OUTCH		;...
OGDIG:	TLNE	BITS,BITZ		;IS THE NUMBER 0?
	JRST	ZEROP		;YES, PRINT 0
	JUMPLE	D,EXPP		;ANY DIGITS REMAINING?
	PUSHJ	P,OUTDIG	;OUTPUT NEXT DIGIT
	SOJG	D,.-1		;LOOP IF MORE DIGITS
EXPP:	MOVEI	AC,AD		;EXPONENT PRINT, ASCII D
	PUSHJ	P,OUTCH		;...
	MOVEI	AC,APLUS	;ASCII +
	JUMPGE	XP,XTEN		;IS EXP +?
	MOVEI	AC,AMINUS	;NO, ASCII -
	MOVNS	XP		;MAKE EXP +
XTEN:	PUSHJ	P,OUTCH		;OUTPUT SIGN OF EXP
	IDIVI	XP,^D100	;EXP MODULO 100.
	MOVE	XP,XP+1		;...
	IDIVI	XP,^D10		;SPLIT INTO TWO DIGITS
	MOVEI	AC,60(XP)	;OUTPUT TENS DIGIT
	PUSHJ	P,OUTCH		;TENS POSITION
	MOVEI	AC,60(XP+1)	;OUTPUT UNITS DIGIT
	PUSHJ	P,OUTCH		;...
OVT:	MOVEM	7,7+1-NAC(P)	;LEAVE AC 7 ALONE FOR BILL
	SUB	P,[XWD NAC,NAC]	;RESTORE ACC'S (EXCEPT 7)
	HRLZI	NAC,1(P)	;...
	BLT	NAC,NAC-1	;...
	POP	P,NAC		;...
NORND:	POPJ	P,
LUPP:	ASH	XP,1		;DECIAML EXPONENT *02
	CAMN	AC,TAB.P(IN)	;IS NUMBER NOW >= TABLE
	CAML	AC+1,TAB.P1(IN)	;...
	CAMGE	AC,TAB.P(IN)	;...
	JRST	PNO		;NO, DON'T MULTIPLY
	MOVEI	Q,TAB.M(IN)	;MULTIPLY BY SELECTED NEGATIVE
	CAIE	IN,13		;E-32?
	JRST	NOTE32		;NO
	SUBI	Q,2		;USE E-16
	PUSHJ	P,DFM..		;TWICE
NOTE32:	PUSHJ	P,DFM..		;...
	ADDI	XP,1		;INDICATE MULTIPLICATION
PNO:	SOJL	IN,REND		;END OF TABLE?
	SOJG	IN,LUPP		;MOVE TABLE POINTER AND LOOP
	JRST	LUPP+1		;EXP IN RIGHT PLACE

LUPM:	ASH	XP,1		;DECIMAL EXPONENT * 2
	CAMN	AC,TAB.M(IN)	;IS NUMBER < TABLE?
	CAMGE	AC+1,TAB.M1(IN)	;...
	CAMLE	AC,TAB.M(IN)	;...
	JRST	MNO		;NO, DON'T MULTIPLY
	MOVEI	Q,TAB.P(IN)	;MULTIPLY BY SELECTED POSITIVE
	PUSHJ	P,DFM..		;...
	SUBI	XP,1		;INDICATE MULTIPLICATION
MNO:	SOJLE	IN,REND		;END OF TABLE?
	SOJA	IN,LUPM		;NO, MOVE TABLE POINTER AND LOOP
								; *EJG* 06/26/76
IFN KI10,<							; *EJG* 06/26/76
DFM..:	DFMP	AC,(Q)						; *EJG* 06/26/76
	JOV	.+1						; *EJG* 06/26/76
	POPJ	P,						; *EJG* 06/26/76
>								; *EJG* 06/26/76
ZERO:	TLO	BITS,BITZ	;SET INDICATOR
	CLEARB	FR,FR+1		;MAKE FRACTION PART 0
	JRST	ZEROH		;SCALING,IF NEEDED

ZEROP:	MOVEI	AC,"0"		;OUTPUT A ZERO
	PUSHJ	P,OUTCH		;...
	MOVEI	IN,3(D)		;OUTPUT ENUF BLANKS
	MOVEI	AC,ABLANK	;ASCII BLANK
	PUSHJ	P,OUTCH		;...
	SOJG	IN,.-2		;...
	JRST	OVT		;GO RETURN

			;NEXT 11 INSTRUCTIONS CHANGE V.005 TO V.020
OUTDIG:	MOVE	MUL,FR+1	;MULTIPLY FRACTION BY 10.
	MULI	MUL,^D10	;*LOW HALF BY 10.
	MOVE	FR+1,MUL+1	;STORE NEW LOW HALF IN FR+1
	MOVE	MUL+1,MUL	;SAVE LOW HALF CARRIES
	MOVE	AC,FR		;GET HIGH HALF OF FRACTION
	MULI	AC,^D10		;* HIGH HALF BY 10.
	TLO	MUL,400000	;SET SIGN TO STOP OVERFLOW
	ADD	MUL,MUL+1	;ADD LOW HALF CARRIES TO HIGH HALF
	TLZN	MUL,400000	;CLEAR SIGN. WAS THERE CRY1?
	ADDI	AC,1		;YES, PROPOGATE CARRY
	MOVE	FR,MUL		;PUT HIGH PART OF NEW FRACTION BACK
	ADDI	AC,AZERO	;ASCII NUMBERS START AT 0

OUTCH:	AOJG	IGN,DEPOT.	;OUTPUT OR IGNORE?
IFN ASTFL,<CAIN	AC,ABLANK	;DON'T CARE ABOUT BLANKS>
	POPJ	P,		;RETURN
IFN ASTFL,<TRZE	AC,100000	;IF ON SIGN
	POP	P,AC		;GET RID OF TWO PUSHJ 'S
	POP	P,AC
	LDB	XP,[POINT 7,BITS,17]	;ORIGINAL FIELD WIDTH
	PUSHJ	P,ASTRK.	;OUTPUT *'S IN FIELD
	JRST	OVT		;RETURN>
	SUBTTL	ROUNDING TABLES
RNDH:	OCT	200000000000
	OCT	14631463146
	OCT	1217270243
	OCT	101422335
	OCT	6433342
	OCT	517426
	OCT	41433
	OCT	3265
	OCT	253
	OCT	21
	OCT	1
	OCT	0,0,0,0,0,0,0,0,0
RNDL:	OCT	0
	OCT	146314631464
	OCT	327024365604
	OCT	57065176763
	OCT	353070414545
	OCT	261070664360
	OCT	336405536661
	OCT	374515274536
	OCT	314356106043
	OCT	56027640466
	OCT	267633766353
	OCT	53765777027
	OCT	4313631402
	OCT	341134115
	OCT	26411156
	OCT	2200727
	OCT	163225
	OCT	13416
	OCT	1116
	OCT	73

	END

