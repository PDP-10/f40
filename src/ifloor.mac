	TITLE	IFLOOR	AND  IROUND
	SUBTTL	R.E. GORIN STANFORD UNVERSITY, JUNE 1971
COMMENT /
	REAL TO INTEGER CONVERSION
	IFLOOR	THE FLOOR FUNCTION
	IROUND	ROUNDING REAL TO INTEGER


DEFINITIONS:

IFLOOR(X) = LARGEST INTEGER I SUCH THAT I	$X

IFLOOR(X) = IF Y	¥0 THEN IFIX(X) ELSE IF FLOAT(IFIX(X))=X THEN IFIX(X) 
						ELSE IFIX(X)-1;

IROUND(X) = SIGN(X) * IFIX(ABS(X)+0.5)


/
	ENTRY 	IFLOOR,IROUND

COMMENT/

CALLING SEQUENCE IS:

	JSA	16,IFLOOR
	JUMP	ARG

WHERE ARG IS THE ADDRESS OF THE ARGUMENT.
RESULT IS RETURNED IN REGISTER 0.
REGISTERS 0 AND 1 ONLY ARE USED.
SIMILAR CALL IS USED FOR IROUND

/

	A=	0
	B=	1
	Q=	16

	SIXBIT	/IFLOOR/
IFLOOR:	0
	MOVM	A, @(Q)		;PICK UP THE ARGUMENT
	MULI	A, 400		;SEPARATE FRACTION AND EXPONENT
	EXCH	A, B		;PUT PARTIAL RESULTS IN A
	ASH	A, -243(B)	;USE EXPONENT AS INDEX REGISTER
	SKIPL	@(Q)		;SKIP IF ARGUMENT WAS NEGATIVE
	JRA	Q, 1(Q)		;EXIT
	MOVNM	A,TEMP		;SAVE RESULTS FOR A WHILE
	IDIVI	A, 400000	;PUT HIGH ORDER IN RH(A),...
	SKIPE	A		;IMPLIES INTEGER LESS THAN 18 BITS
	TLC	A, 254000	;SET EXP TO 254 (27+17 DECIMAL)
	TLC	B, 233000	;SET EXP OF B TO 233 (27 DECIMAL)
	FAD	A, B		;NORMALIZE AND ADD
	MOVN	A,A		;RESET THE SIGN
COMMENT/
NOW TEMP HAS THE FIXED VALUE AND A CONTAINS THE EQUIVALENT
FLOATING VALUE.  IF THE FLOATING VALUE IN A IS LARGER THAN
THE ORIGINAL ARGUMENT THEN WE DECREMENT TEMP, OTHERWISE
TEMP IS THE ANSWER
/

	CAMLE	A,@(Q)
	SOS	TEMP		;GOD FORBID YOU SHOULD TRY SOSA A,TEMP
	MOVE	A,TEMP
	JRA	Q,1(Q)
TEMP:	BLOCK	1
     

	SUBTTL	ROUNDING FUNCTION
	SIXBIT	/IROUND/
IROUND:	0
	MOVM	A,@(Q)
	FADRI	A,(<0.5>)
	MULI	A, 400		;SEPARATE FRACTION AND EXPONENT
	EXCH	A, B		;PUT PARTIAL RESULTS IN A
	ASH	A, -243(B)	;USE EXPONENT AS INDEX REGISTER
	SKIPGE	@(Q)		;TEST SIGN 
	MOVN	A,A		;SET NEGATIVE SIGN
	JRA	Q,1(Q)
	END
