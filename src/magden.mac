TITLE	MAGDEN	V31(214)
SUBTTL	24-JUN-71	/DMN
;FROM	31-JAN-68	V.001


ENTRY	MAGDEN
EXTERN	DEVTB.,MTABF.,UUOL.
EXTERN	UNSV.,ILLMG.,INIER.,SAVCK.
EXTERN	TABP1.,DEVIC.,INTVR.


	;ARGUMENT PARAMETER DEFINITIONS
LOW== ^D200
MED== ^D556
HIGH==^D800
ODD== 0
EVEN==1



	;AC'S
A=1		;DEVICE NUMBER,POINTER
B=2
C=3
D=4		;DENSITY AND PARITY MASK
E=5
F=6		;FREE CHANNELL
Q=16
P=17


	;MODE MASKS FOR DENSITIES AND PARITIES
LMASK==2
MMASK==4
HMASK==6
EVNMSK==10
;SAVE AC'S, INITIALIZE, CHECK ARGS, SAVE INFORMATION

MAGDEN:	0
	PUSHJ	P,SAVCK.	;SAVE 0-6,INIT D,E;CHECK DEV NUM
	MOVE	B,@1(Q)		;DEV NUM IN A, GET DENSITY.
	SETZ	D,		;START WITH STANDARD ZERO
	CAIN	B,LOW		;LOW DENSITY?
	MOVEI	D,LMASK		;YES
	CAIN	B,HIGH		;HIGH DENSITY?
	MOVEI	D,HMASK		;YES
	CAIN	B,MED		;MEDIUM DENSITY?
	MOVEI	D,MMASK		;YES
	JUMPE	D,DIEM		;FAIL IF NOT ONE OF ABOVE
	MOVE	B,@2(Q)		;GET PARITY ARG
	CAIN	B,ODD		;ODD PARITY?
	JRST	AFTRCK		;YES, GO ON, NO BITS SET
	CAIE	B,EVEN		;EVEN PARITY?
	JSP	F,DIEM		;NO,FAIL!
	IORI	D,EVNMSK	;YES, SET BIT

;DEV NUM IN "A", MASK IN "D", ARGS LEGAL
AFTRCK:	PUSHJ	P,INTVR.	;PICK UP DEV NAME
				;MAKE SURE DEV NOT YET INIT

;MODE IN D, DEV NAM IN E



;FIND A SLOT, INSERT DATA, RESTORE AC'S , RETURN

GOTNAM:	MOVEI	A,TABP1.	;INIT TABLE POINTER
	SETZB	B,F		;INIT POINTER TO FREE SLOT
SLTLOP:	SUBI	A,1		;UPDATE TABLE POINTER
	CAIGE	A,MTABF.	;DONE WITH TABLE?
	JRST	PREPUT		;YES, PUT DATA IN SLOT
	MOVE	C,(A)		;GET NAME FROM SLOT
	TRZ	C,77		;CLEAR MODE BITS
	CAMN	C,E		;THE ONE WE WANT?
	JRST	SETPUT		;YES,DONE LOOKING
	JUMPN	C,SLTLOP	;NO,LOOP BACK UNLESS 0
	MOVE	B,A		;IF ZERO,SAVE POINTER AND
	JRST	SLTLOP		;THEN LOOP BACK
SETPUT:	MOVE	B,A		;SET POINTER, THIS DEV PREVIOUSLY
				;INIT IN THIS SLOT
PREPUT:	JUMPN	B,PUT		;WAS THERE A FREE SLOT?
	JSP	F,DIE		;NO,FAIL!
PUT:	MOVEM	E,(B)		;YES, PUT NAME IN SLOT
	IORM	D,(B)		;GET MODE BITS IN TOO
	PUSHJ	P,UNSV.		;RESTORE
	JRA	Q,3(Q)		;RETURN


DIE:	XCT	UUOL.		;SET UP AC 16 FIRST
	MOVE	7,[PUSHJ P,INIER.]	;SO IT CAN FIND DEV NUM
	JRST	7

DIEM:	XCT	UUOL.
	MOVE	0,DEVTB.(A)	;SET UP
	MOVEM	0,DEVIC.	;DEV NAM
	PUSHJ	P,ILLMG.	;FOR ERROR



	END

