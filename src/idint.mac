	TITLE	IDINT	V.022
	SUBTTL	12-DECEMBER-1969	/KK

;FROM V.020
;DOUBLE PRECISION TO INTEGER TRUNCATION
;THIS ROUTINE TRUNCATES A DOUBLE PRECISION FLOATING POINT
;NUMBER TO AN INTEGER

;THE ROUTINE IS CALLED IN THE FOLLOWING MANNER:
;	JSA	16,IDINT
;	EXP	ARG
;WHERE ARG IS THE ADDRESS OF THE HIGH ORDER PORTION OF THE
;DOUBLE PRECISION ARGUMENT.  THE ANSWER IS RETURNED IN
;ACCUMULATOR 0.


	ENTRY	IDINT

	SIXBIT/IDINT/
IDINT:	0			;ENTRY TO IDINT ROUTINE.
	MOVEM	2,SAVE2		;SAVE THE CONTENTS OF AC 2.
	MOVEI	1,@(16)		;GET ADDRESS OF ARGUMENT.
	MOVE	0,(1)		;HIGH ORDER OF ARG TO AC 0.
	MOVE	1,1(1)		;LOW ORDER OF ARG TO AC 1.
	JUMPGE	0,.+2		;SET THE
	DFN	0,1		;ARG POSITIVE.
	LDB	2,IDINTP	;GET EXPONENT FROM AC 0.
	TLZ	0,777000	;ZERO OUT EXP AND SIGN.
	LSH	1,10		;CLEAR AWAY LOW ORDER EXP.
	ASHC	0,-233(2)	;CONVERT TO INTEGER.
	MOVE	2,SAVE2		;RESTORE THE CONTENTS OF AC 2.
	SKIPGE	@(16)		;GIVE THE ANSWER
	MOVNS	0,0		;THE CORRECT SIGN.
	JRA	16,1(16)	;RETURN.

IDINTP:	POINT	9,0,8
SAVE2:	0
	END

