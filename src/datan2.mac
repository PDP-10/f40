TITLE DATAN2 V.027     PDP-10 DOUBLE PRECISION ARCTANGENT OF TWO ARGUMENTS

SUBTTL	17-JUL-70	/KK/DMN	
;FROM	V.022	7-JAN-70

;FROM V.020	28 APRIL 1969	/TWE
;FROM V.005,  12-APR-67
;THIS ROUTINE CALCULATES THE ARCTANGENT OF A/B
;IF ARGUMENT IS IN 2ND QUADRANT, DATAN2(A,B)=DATAN(A/B) + PI
;IF ARGUMENT IS IN 3RD QUADRANT, DATAN2(A,B)=DATAN(A/B)-PI
;IF ARGUMENT IS IN 1ST OR 4TH QUADRANT, DATAN2(A,B)=DATAN(A/B)

;IF QUOTIENT A/B OVER OR UNDERFLOWS, RETURN AN ANGLE
;ON A CO-ORDINATE AXIS

;THE ROUTINE IS CALLED IN THE FOLLOWING MANNER:
;	JSA	Q, DATAN2
;	EXP	A
;	EXP	B
;DATAN(A/B) IS RETURNED AS A DOUBLE PRECISION ANSWER IN ACCUMULATORS
;A AND B.

	ENTRY	DATAN2
	EXTERN	DATAN,TYPER.

	A=	0
	B=	1
	C=	2
	D=	3
	Q=	16
	P=	17

	SIXBIT /DATAN2/
DATAN2:	0			;ENTRY TO DATAN2 ROUTINE
	MOVEI	B, @(Q)		;GET ADDRESS OF FIRST ARGUMENT
	MOVE	A, (B)		;GET HIGH ORDER PART OF ARGUMENT
	MOVE	B, 1(B)		;GET LOW ORDER PART OF ARGUMENT
	MOVEM	C,CSAVE		;SAVE AC C
	MOVEM	B,ARG		;SAVE LOW PART OF ARG.
	MOVEI	B,@1(Q)		;PICK UP
	MOVE	C,(B)		;THE
	MOVE	B,1(B)		;NEXT ARG.
	EXCH	B,ARG		;RESTORE LOW OF ONE ARG AND
	MOVEM	C,ARG+1		;COPY THE OTHER ARG.

	FDVL	A,C		;CALCULATE A/B
	JFCL	AXIS		;UNDER/OVER FLOW
	MOVN	C,A		;...
	FMPR	C,ARG		;...
	JFCL
	UFA	B,C
	FDVR	C,ARG+1
	JFCL
	FADL	A,C
	JFCL	AXIS		;UNDER/OVER FLOW

	MOVEM	A,ARG		;STORE ARG FOR DATAN
	MOVEM	B,ARG+1		;...
	JSA	Q, DATAN	;CALCULATE DATAN(A/B)
	EXP	ARG		;ADDRESS OF ARGUMENT FOR DATAN
	MOVE	C, CSAVE	;RESTORE ACCUMULATOR C
	SKIPL	@1(Q)		;WAS 2ND ARGUMENT POSITIVE?
	JRA	Q,2(Q)		;YES, 1ST OR 4TH QUAD, EXIT
				;NO, 2ND OR 3RD
	SKIPGE	@(Q)		;2ND QUADRANT?
	DFN	A,B		;NO, 3RD. SUBTRACT PI

FLADD A,PI.
	MOVE	C,CSAVE		;RESTORE AC C.

	SKIPGE	@(Q)		;3RD OR 4TH QUADRANTS?
	DFN	A,B		;YES, NEGATE FINAL ANSWER
	JRA	Q, 2(Q)		;EXIT

AXIS:	MOVE	C,CSAVE		;RESTORE AC C
	JUMPN	A,OVER		;GO TO OVER IF OVERFLOW.
	SKIPL	@1(Q)		;ANS UNDERFLOWS IF Y/X UNDERFLOWS
	JRST	UNDMSG		;AND IF X >= 0.
	DMOVE	A,PI.		;O'E, ANS = +-PI, SO
	JRST	SETSGN		;GO TO SET SIGN.
OVER:	SKIPN	@(Q)		;WAS Y =0 ?
	FDVR	0,@(Q)		;YES, THIS IS 0/0 , FORCE A DIV CHK MSG.
	DMOVE	A,PIOT		;ANS = +-PI/2.
SETSGN:	SKIPGE	@(Q)		;ANS > 0 IF Y > 0.
	DFN	A,B		;ANS < 0 IF Y > 0.
	JRA	Q,2(Q)		;EXIT.

UNDMSG:	MOVEI	0,4		;RETURN UNDERFLOW
	PUSHJ	17,TYPER.	;MESSAGE
	SETZB	0,1		;AND ANS = 0
	JRA	Q,2(Q)		;EXIT.



ARG:	BLOCK 2			;ARGUMENT FOR DATAN
CSAVE:	BLOCK 1


PIOT:	201622077325	;+PI/2
	146042055061

PI.:	202622077325	;DEC=3.14159265358979323846
	147042055061		


	END

