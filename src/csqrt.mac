	TITLE	CSQRT	V.021
	SUBTTL	22-AUGUST-1969	IAN OLIVER  UNIV.OF QLD./KK

;COMPLEX SQUARE ROOT FUNCTION

;THE ALGORITHM IS AS FOLLOWS:

;IF X >= 0 AND Y >= 0,
;	A=SQRT((/X/+/X+IY/)/2)
;	B=Y/(2*A)

;IF X >= 0 AND Y < 0,
;	A=-SQRT((/X/+/X+IY/)/2)
;	B=Y/(2*A)

;IF X < 0 AND Y >= 0,
;	A=Y/(2*B)
;	B=SQRT((/X/+/X+IY/)/2)

;IF X < 0 AND Y < 0,
;	A=Y/(2*B)
;	B=SQRT((/X/+/X+IY/)/2)

;WHERE /Z/ MEANS THE ABSOLUTE VALUE OF Z.

;BECAUSE OF OVER/UNDERFLOW PROBLEMS, THE SQUARE
;ROOT TERM IS CALCULATED IN SEVERAL DIFFERENT WAYS.

;THE CALLING SEQUENCE IS:
;	JSA	16,CSQRT
;	EXP Z
;WHERE Z HAS BEEN DECLARED COMPLEX.

;THE REAL PART OF THE ANSWER IS RETURNED IN AC 0,
;AND THE IMAGINARY PART OF THE ANSWER IS RETURNED IN AC 1.

	ENTRY CSQRT
	EXTERNAL SQRT




	SIXBIT/CSQRT/
CSQRT:	0			;ENTRY TO COMPLEX SQUARE ROOT ROUTINE.
	MOVE	1,(16)		;PICK UP THE ADDRESS OF Z.
	MOVE	0,(1)		;PICK UP X IN AC 0.
	MOVE	1,1(1)		;PICK UP Y IN AC 1.
	MOVEM	2,SAVE2		;SAVE AC 2.
	MOVM	2,0		;/X/ TO AC 2.
	SKIPE	1		;IF Y IS NOT ZERO,
	JRST	NORMAL		;GO TO THE STANDARD ROUTINE.
	SKIPL	0		;IF Y=0 AND X>=0, Z IS REAL,
	JRST	REAL		;SO TRANSFER TO SIMPLE CALC.
NORMAL:	MOVEM	3,SAVE3		;SAVE AC 3.
	MOVEM	0,XSAVE		;SAVE X.
	MOVEM	1,YSAVE		;SAVE Y.
	MOVM	3,1		;/Y/ TO AC 3.
	SETZM	FLAGWD		;FLAGWD CONTAINS 0 IF /X/
	CAMLE	2,3		;<= /Y/, O'E IT CONTAINS 1.  PUT
	AOSA	FLAGWD		;THE LARGER OF /X/,/Y/ IN AC 2
	EXCH	2,3		;AND THE SMALLER IN AC 3.
	FDVR	3,2		;CALC S/L.
	JFCL			;NO UNDERFLOW ERROR MESSAGE ALLOWED.
	FMPR	3,3		;CALC (S/L)**2.
	JFCL			;NO UNDERFLOW ERROR MESSAGE ALLOWED.
	FADRI	3,201400	;HAVE (1+(S/L)**2) IN AC 3.
	JSA	16,SQRT		;CALC. THE SQRT OF
	EXP	3		;(1+(S/L)**2)=1+EPS.
	SKIPN	FLAGWD		;GO TO YGTX IF
	JRST	YGTX		;/Y/>/X/.
XGTY:	FADRI	0,201400	;CALC. 2 + EPS.
	FDVRI	0,202400	;CALC. (2+EPS)/2.
	MOVM	3,0		;PUT IT IN AC NE 0 OR 1 FOR
	JSA	16,SQRT		;CALL TO SQRT
	EXP	3		;ROUTINE.
	MOVEM	0,3		;SAVE SQRT(1+EPS/2) IN AC 3.
	JSA	16,SQRT		;CALC.
	EXP	2		;SQRT(/X/).
	FMPR	0,3		;CALC. SQRT(/X/*(1+EPS/2)).
	JRST	OUT1		;GO TO REST OF CALC.
YGTX:	CAMGE	2,[1.0]		;IF /Y/>1, GO TO POSSIBLE OVERFLOW CASE.
	JRST	YXSMAL		;O'E, GO TO POSSIBLE UNDERFLOW.
	FDVRI	0,203400	;CALC. (1+EPS)/4.
	FMPR	2,0		;CALC. /Y/*(1+EPS)/4.
	MOVM	3,XSAVE		;/X/ TO AC 3.
	FDVRI	3,203400	;CALC. /X//4.
	JFCL			;SUPPRESS UNDERFLOW ERROR MESSAGE.
	FADR	3,2		;CALC.(/X//4)+(/Y/*(1+EPS)/4).
	JSA	16,SQRT		;CALC.
	EXP	3		;SQRT OF THIS.
	FMPR	0,SQ2		;MULTIPLY IT BY SQRT(2).




OUT1:	MOVM	1,YSAVE		;/Y/ TO AC 1.
	FDVR	1,0		;CALC. /Y//2 TIMES THE
	FDVRI	1,202400	;SQRT TERM.
	JRST	SIGNS		;GO TO REST OF CALC.
YXSMAL:	FMPR	0,2		;/Y/*(1+EPS).
	MOVM	3,XSAVE		;/X/ TO AC 3.
	FADR	3,0		;CALC. /X/+/Y/*(1+EPS).
	JSA	16,SQRT		;CALC. SQRT OF 
	EXP	3		;THIS.
	MOVEM	0,3		;SAVE IT IN AC 3.
	FDVR	0,SQ2		;DIVIDE IT BY THE SQRT(2).
	FMPR	3,SQ2		;OR MULTIPLY IT BY SQRT(2).
	FDVR	2,3		;THEN CALC /Y//THIS.
	MOVE	1,2		;PUT A TEMP ANSWER IN AC 1.
SIGNS:	SKIPGE	XSAVE		;SET UP THE REAL AND
	EXCH	1,0		;THE IMAGINARY ANSWERS WITH
	SKIPGE	YSAVE		;THE APPROPRIATE
	MOVNS	0,0		;SIGNS.
	MOVE	2,SAVE2		;RESTORE AC 2.
	MOVE	3,SAVE3		;RESTORE AC 3.
	JRA	16,1(16)	;RETURN.

REAL:	JSA	16,SQRT		;CALC. THE SQRT(X)
	EXP	2		;BECAUSE IT IS THE REAL ANSWER.
	SETZM	1		;IMAG. ANSWER = 0.
	MOVE	2,SAVE2		;RESTORE AC 2.
	JRA	16,1(16)	;RETURN.

SQ2:	201552023632		;SQRT(2).
XSAVE:	0
YSAVE:	0
SAVE2:	0
SAVE3:	0
FLAGWD:	0
	END



