	TITLE OCTO.  V.030.0.66   OCTAL INTEGER OUTPUT FORTRAN IV
	SUBTTL	2-APR-71	/DMN

;EDITED (BY TWE) 11 DEC. 68 TO REMOVE GLICH CHARACTERS

;REENT==1 FOR RE-ENTRANT OPERATING SYSTEM
IFNDEF REENT,<REENT==0>
IFN REENT,<	HISEG	>
IFNDEF ASTFL,<ASTFL==1	;GIVES ASTERISK FILL IF FORMAT OVERFLOWS>

	;"OCTO." IS A ROUTINE WHICH OUTPUTS OCTAL ASCII
	;CHARACTERS TO AN EXTERNAL ROUTINE "DEPOT." . THE INTEGER
	;IS CONTAINED IN (ACO) ON ENTRY, AND THE OUTPUT ITEM IS
	;RETURNED IN (ACO).

	;CALLING SEQUENCE:
	;	PUSHJ	P,OCTO.
	;PUSHDOWN LIST CONTAINS:
	;	1.	FIELD WIDTH W, INTEGER IN LEFT HALF
	;		W=0  MEANS VARIABLE FIELD
	;	2.	PROGRAM COUNTER(RETURN ADDRESS)
	;	3.THRU	19.  FREE STORAGE(ACS AND CHARACTERS)

		ACO=0			;CONTAINS INTEGER TO BE PUT OUT
		B=1			;STORAGE
		A=2			;STORAGE
		ACCH=3			;CHARACTER COUNT
		ACW=4			;FIELD WIDTH
		PDP=17			;PUSHDOWN POINTER
	ENTRY	OCTO.
	EXTERN	DEPOT.
IFN ASTFL,<EXTERN	ASTRK.>

OCTO.:	PUSH	PDP,ACW		;SAVE THE
	PUSH	PDP,ACCH	;ACS
	PUSH	PDP,A		;IN THE
	PUSH	PDP,B		;PUSHDOWN LIST
	LDB	ACW,FIELD	;PICK UP FIELD WIDTH
	JUMPN	ACW,.+2		;VARIABLE FIELD?
	MOVEI	ACW,17		;YES,SET WIDTH = 15
	MOVEI	ACCH,14		;SET MAX. CHAR. =12
	MOVE	A,ACO		;MOVE INTEGER TO A
	MOVE	B,[POINT 3,A]	;MOVE BYTE PTR TO B
	SUB	ACW,ACCH	;DETERMINE NO OF CHAR TO OUTPUT
	JUMPLE	ACW,LODCHR	;JUMP IF FIELD L.E. 12
	MOVEI	ACO,40		;SET UP LEADING BLANKS
	PUSHJ	PDP,DEPOT.	;OUTPUT BLANK
	SOJN	ACW,.-1		;CHECK IF =0
LODCHR:	ILDB	ACO,B		;DEPOSIT CHAR IN ACO
IFN ASTFL,<JUMPGE ACW,.+3		;OUTPUTTING CHARS. NOW
	JUMPN	ACO,OUTAST	;CAN ONLY THROW AWAY ZEROS
	AOJA	ACW,LODCHR	;GET NEXT CHAR>
IFE ASTFL,<AOJLE ACW,LODCHR	;JUMP IF LESS THAN OR = 0>
	ADDI	ACO,60		;CONVERT TO ASCII
	PUSHJ	PDP,DEPOT.	;OUTPUT CHAR.
	TLNE	B,770000	;CK IF ALL CHAR HAVE BEEN OUTPTD
	JRST	LODCHR		;GET ANOTHER CHAR.
OCTRTN:	POP	PDP,B		;RESTORE
	POP	PDP,A		;THE
	POP	PDP,ACCH	;ACS
	POP	PDP,ACW		;TO THEIR ORIGINAL VALUE
	POPJ	PDP,		;RETURN

IFN ASTFL,<
OUTAST:	LDB	ACW,FIELD	;GET ORIGINAL WIDTH
	PUSHJ	PDP,ASTRK.	;FILL FIELD WITH *'S
	JRST	OCTRTN		;RETURN>

FIELD:	POINT	7,-5(PDP),17	;FIELD WIDTH IN FORMAT WORD
	END

