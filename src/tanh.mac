	TITLE	TANH  V.021
	SUBTTL	22-SEPTEMBER-1969	ED YOURDON/KK

;FLOATING POINT SINGLE PRECISION HYPERBOLIC TANGENT ROUTINE

;THIS ROUTINE CALCULATES THE TANH BY THE FOLLOWING ALGORITHM:
;IF ABSF(X) <.00034, THEN TANH(X) = X
;IF ABSF(X) >12.0, THEN TANH(X) = 1.0*SIGN(X)
;IF 0.17 <= X < 12.0, THEN TANH IS CALCULATED AS
;	TANH(X) = 1.0 - 2(1.0 + EXP(2*X))**-1
;IF .00034 <= X < 0.17, THEN TANH IS CALCULATED AS
;TANH(X) = F(A+F^2(B+C(D+F^2)**-1))**-1
;WHERE X = 4*LOG(E)  (BASE 2)

;THE ROUTINE IS CALLED IN THE FOLLOWING MANNER:
;	JSA	Q, TANH
;	EXP	ARG
;THE ANSWER IS RETURNED IN ACCUMULATOR A

	ENTRY	TANH
	EXTERN	EXP

	A=	0
	B=	1
	Q=	16


	SIXBIT/TANH/
TANH:	0			;ENTRY TO TANH ROUTINE
	MOVE	A, @(Q)		;PICK UP THE ARGUMENT
	MOVM	B, A		;GET ABSF(ARGUMENT)
	CAMGE	B, T1		;RETURN TANH(X)=X IF 
	JRA	Q, 1(Q)		;ABSF(X) .LE. .00034
	CAMLE	B, T2		;RETURN TANH(X) = 1.0 IF
	JRST	TH5		;ARGUMENT GREATER THAN 12.0
	CAMGE	B, T3		;USE RATIONAL APPROXIMATION IF
	JRST	TH3		;ARGUMENT IS LESS THAN 0.17
	MOVEM	A,SAVEA		;SAVE ARG. 
	FMPRI	B,202400	;GET 2*ARG.
	MOVEM	B,TM1		;ARG TO NE 0 OR 1.
	JSA	Q, EXP		;CALCULATE EXP(2X)
	EXP	TM1		;ADDRESS FOR EXP ROUTINE
	MOVSI	B, 201400	;FORM 1.0
	FAD	A, B		;1 + EXP(2X)
	FDVM	B, A		;(1 + EXP(2X))**-1
	FMPRI	A,202400	;2*(1 + EXP(2X))**-1
	FSBRM	B, A		;1 - 2*(1 + EXP(2X))**-1
	SKIPGE	SAVEA		;SKIP AHEAD IF ARG WAS >=0.
	MOVNS	A		;OTHERWISE,NEGATE THE ANSWER.
	JRA	Q, 1(Q)		;EXIT

TH3:	FMP	A, T7		;FORM 4*X*LOG(E) BASE 2
	MOVEM	A, TM1		;SAVE IT IN TM1
	FMP	A, A		;SQUARE IT
	MOVEM	A, TM2		;SAVE IT
	FAD	A, T4		;FORM F^2 + T4
	MOVE	B, T5		;GET T5 IN ACCUMULATOR B
	FDV	B, A		;T5/(F^2 + T4)
	FAD	B, T6		;T6 + T5/(F^2 + T4)
	FMP	B, TM2		;MULTIPLY BY F^2
	FAD	B, T7		;ADD T7 (4*LOG(E) BASE 2)
	MOVE	A, TM1		;GET F IN ACCUMULATOR A
TH5:	FDV	A, B		;DIVIDE F BY PARTIAL SUM
	JRA	Q, 1(Q)		;EXIT

T1:	165544410070		;0.00034
T2:	204600000000		;12.0
T3:	176534121727		;0.17
T4:	211535527022		;349.6699888
T5:	204704333567		;14.1384514018
T6:	173433723376		;0.01732867951
T7:	203561250731		;5.7707801636

TM1:	0
TM2:	0
SAVEA:	0
	END

