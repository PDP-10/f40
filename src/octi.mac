TITLE OCTI.  V30.0.143   OCTAL INTEGER INPUT FORTRAN IV
SUBTTL V30.0.44 16-DEC-70 FROM V30 /VAA
;V30  16-NOV-70	/DMN

;REENT==1 FOR RE-ENTRANT OPERATING SYSTEM
IFNDEF REENT,<REENT==0>
IFN REENT,<	HISEG	>

;"OCTI." IS A ROUTINE WHICH GETS OCTAL ASCII
;CHARACTERS FROM "CHINN," (IN ACO), AND INPUTS THEM
;AS OCTAL INTEGERS (IN ACO), "IIB." INCREMENTS THE
;BYTE POINTER,

;CALLING SEQUENCE:
;	PUSHJ P, OCTI.
; 2 RETURNS:
;	ILLEGAL CHARACTER
;	NORMAL
;PUSHDOWN LIST CONTAINS:
;	1. W: FIELD WIDTH OF INPUT STRING;
;		INTEGER IN LEFT HALF,
;		W=0 MEANS VARIABLE FIELD
;	2. PROGRAM COUNTER (RETURN ADDRESS)
;	3. THRU 6. FREE STORAGE

	ENTRY	OCTI.
	EXTERN	IIB.,CHINN.,OVFLS.,TTYFL.

OCTI.:	PUSH	PDP,ACT		;SAVE
	PUSH	PDP,D		;THE 3
	PUSH	PDP,ACTH	;ACS
	HLRZ	ACTH,-N(PDP)	;PICK UP FIELD WIDTH
	SETZB	D,ACT		;SET TO ZEROS
	JUMPN	ACTH,GET+1	;A VARIABLE FIELD?
	SETO	ACTH,		;YES, SET W TO-1
GET:	JUMPE	ACTH,INTIN	;IF W=0, (NO MORE CHARS)
				;RETURN
	PUSHJ	PDP,CHINN.	;GET A CHAR. (ACO)
	SUBI	ACTH,1		;DECREMENT FIELD WIDTH
	CAIG	ACO,67		;CHECK FOR AN OCTAL
	CAIGE	ACO,60		;DIGIT,
	JRST	ACH		;CHAR. NOT A DIGIT
	TLO	ACT,400000	;DIGIT, SET FLAG
	SUBI	ACO,60		;GET ACTUAL NUMBER
SHIFT:	LSH	D,3		;SHIFT 1 PLACE TO LEFT
	ADD	D,ACO		;FORM AN INTEGER
IIB:	SKIPN	OVFLS.(LOW)
	XCT	IIB.		;ADVANCE POINTER
	JRST	GET		;GET ANOTHER CHAR,

ACH:	CAIN	ACO,15		;CARRIAGE RETURN
	JRST	CRIN		;YES
	CAIN	ACO,40		;BLANK?
	JRST	BLNK		;CHAR. IS BLANK
	CAIE	ACO,55		;TEST FOR -
	CAIN	ACO,53		;TEST FOR +
	JRST	PLSMNS		;= TO + OR -
	JUMPGE	ACTH,IIL	;ILLEGAL IF NOT A 
				;VRIABLE FIELD
CKCR:	CAIE	ACO,15		;SKIP IF CARRIAGE RETURN
	XCT	IIB.		;VARIABLE FIELD,ADVANCE
				;POINTER
	JRST	INTIN		;NORMAL RETURN
CRIN:	TLNE	16,TTYFL.	;INPUT FROM TTY
	SETO	ACTH,		;YES, AS IF DELIMITER MODE
	JRST	.+2		;SKIP TEST FOR BLANK ONLY
BLNK:	JUMPGE	ACT,.+2		;IGNORE BLANKS IF NO DIGITS YET
	JUMPL	ACTH,CKCR	;IF VAR. FLD.,NORMAL RETURN
				;RETURN
	MOVEI	ACO,0		;IGNORE BLKS-NO DIGITS
				;OR W NOT =0
	JRST	SHIFT		;OUTPUT THE BLK,

PLSMNS:	JUMPL	ACT,VARYT	;IS THE FIRST DIGIT IN
	TRNE	ACO,4		;TEST FOR -
	TLO	ACT,200000	;NEG, SET FLAG
	JRST	IIB		;GET NEXT CHARACTER
VARYT:	JUMPGE	ACTH,IIL	;VARIABLE FIELD ?
	AOS	2(7)		;INCREMENT THE ITEM COUNT
INTIN:	TLNE	ACT,200000	;IS NUMBER NEG. ?
	MOVNS	D		;YES, NEGATE CHAR
	MOVE	ACO,D		;SET NORMAL RETURN
	AOS	-3(PDP)
IIL:	POP	PDP,ACTH	;RESTORE
	POP	PDP,D		;ACS TO
	POP	PDP,ACT		;ORIGINAL VALUES,
	POPJ	PDP,		;RETURN

	ACO=0			;RECEIVES CHAR FROM CHINN.
				;AND RETURNS THE INPUT ITEM
	ACTH=1			;FIELD WIDTH
	ACT=2			;FLAG
	D=3			;STORAGE
	N=4			;NO. OF ACS SAVED +1
IFN REENT,<LOW=16		;LOW SEGMENT OFFSET>
IFE REENT,<LOW=0>
	PDP=17			;PUSHDOWN POINTER

	END

