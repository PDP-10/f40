	TITLE	MAX	V.022
	SUBTTL	5-DECEMBER-1969	ED YOURDON/TWE/KK

;FROM V.005.
;AMAX1, MAX1, AMAX0, AND MAX0 CALCULATE THE MAXIMUM OF A
;STRING OF ARGUMENTS, THE FIRST ADDRESS OF WHICH IS IN
;ACCUMULATOR Q.

;THE ROUTINES ARE CALLED IN THE FOLLOWING MANNER:
;	JSA	Q,AMAX1 OR MAX1 OR AMAX0 OR MAX0
;	EXP	A
;	EXP	B
;	ETC
;FOR AMAX1 AND MAX1, A,B,... ARE REAL.  THE ANSWER
;RETURNED IS REAL FOR AMAX1 AND INTEGER FOR MAX1.
;FOR AMAX0 AND MAX0, A,B,... ARE INTEGER.  THE ANSWER 
;RETURNED IS REAL FOR AMAX0 AND INTEGER FOR MAX0.

;THE ANSWER IS RETURNED IN ACCUMULATOR A.

	ENTRY	AMAX1,MAX1,AMAX0,MAX0
	EXTERNAL	IFIX,FLOAT

A=0
B=1
Q=16
P=17

	SIXBIT/AMAX1/
AMAX1:	0			;ENTRY TO AMAX1 ROUTINE.
	PUSHJ	P,MAX.		;FIND THE MAXIMUM AND
	JRA	Q,(Q)		;RETURN.

	SIXBIT/MAX0/
MAX0:	0			;ENTRY TO MAX0 ROUTINE.
	PUSHJ	P,MAX.		;FIND THE MAXIMUM AND
	JRA	Q,(Q)		;RETURN.

	SIXBIT/MAX1/
MAX1:	0			;ENTRY TO MAX1 ROUTINE.
	PUSHJ	P,MAX.		;FIND THE MAXIMUM AND
	JSA	Q,IFIX		;FIX THE
	EXP	SAVARG		;RESULT AND
	JRA	Q,(Q)		;RETURN.

	SIXBIT/AMAX0/
AMAX0:	0			;ENTRY TO AMAX0 ROUTINE.
	PUSHJ	P,MAX.		;FIND THE MAXIMUM AND
	JSA	Q,FLOAT		;FLOAT THE
	EXP	SAVARG		;RESULT AND
	JRA	Q,(Q)		;RETURN.

MAX.:	MOVE	A,@(Q)		;PICK UP FIRST ARG.
	AOJA	Q,MAX.2		;INCREMENT POINTER TO NEXT ADDRESS.

MAX.1:	CAMGE	A,@B		;IS CURRENT ARG > NEXT ARG?
	MOVE	A,@B		;NO, PUT ARG IN A.

MAX.2:	MOVE	B,(Q)		;NEXT ARG ADDR TO B.
	TLC	B,(<JUMP>)	;IS THE OPCODE OF THE
	TLNN	B,777000	;NEXT ARG A JUMP?
	AOJA	Q,MAX.1		;YES, LOOP.
	MOVEM	A,SAVARG	;ANSWER IN A AND SAVARG.
	POPJ	P,		;EXIT.

SAVARG:	0
	END


