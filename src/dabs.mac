TITLE DABS  V.022       PDP-10
SUBTTL  29-DECEMBER-1969	/KK

;FROM V.005.
;DOUBLE PRECISION ABSOLUTE VALUE FUNCTION
;THIS ROUTINE RETURNS THE ABSOLUTE VALUE OF A DOUBLE PRECISION
;ARGUMENT

;THE CALLING SEQUENCE FOR THE ROUTINE IS
;	JSA	Q, DABS
;	EXP	ARG
;WHERE ARG IS THE ADDRESS OF THE HIGH ORDER PART OF A DOUBLE
;PRECISION ARGUMENT, THE LOW ORDER PART BEING IN ARG+1. THE
;DOUBLE PRECISION ANSWER IS RETURNED IN ACCUMULATORS A AND B.

	ENTRY	DABS

	A=	0
	B=	1
	Q=	16
	P=	17

	SIXBIT/DABS/
DABS:	0			;ENTRY TO DABS ROUTINE
	MOVEI	B, @(Q)		;GET ADDRESS OF ARGUMENT
	MOVE	A, (B)		;PICK UP HIGH ORDER PORTION OF ARG
	MOVE	B, 1(B)		;PICK UP LOW ORDER PORTION
	SKIPGE	A		;IS ARGUMENT POSITIVE?
	DFN	A, B		;NO, NEGATE IT
	JRA	Q, 1(Q)		;EXIT

	END

