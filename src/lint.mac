TITLE LINT.  V.27    LOGICAL INPUT	FORTRAN IV
SUBTTL	15-JUNE-70	/DMN

;FROM 	 	/VAA	JULY-11-69	FROM: OCT. 28,1965

;REENT==1 FOR RE-ENTRANT OPERATING SYSTEM
IFNDEF REENT,<REENT==0>
IFN REENT,<	HISEG	>

;FOR LOGICAL INPUT THE INPUT STRING CONTAINS W CHARACTERS.
;THE FIRST (LEFTMOST) NON-BLANK CHARACTER MUST BE A T OR AN F,
;DENOTING A VALUE OF TRUE (NEGATIVE), OR FALSE (0 OR POSITIVE)
;FOR THE INPUT ITEM.
;THE CHAR IS RECEIVED FROM "CHINN." IN AC0; THE INPUT ITEM IS ALSO
;RETURNED IN THIS AC.  "IIB." INCREMENTS THE BYTE POINTER.

;CALLING SEQUENCE:
;	PUSHJ PDP,LINT.
;3 RETURNS:
;	END OF FILE
;	ILLEGAL CHARACTER
;	NORMAL
;PUSHDOWN LIST CONTAINS:
;	1.  FIELD WIDTH W, INTEGER IN LEFT HALF
;	2.  PROGRAM COUNTER
;	3.-5.  FREE STORAGE

	AC0=0		;RECEIVES CHAR FRON CHINN. AND RETURNS
			;THE INPUT ITEM
	ACA=1		;FIELD WIDTH
	PDP=17		;PUSHDOWN POINTER
	B=2		;STORAGE
IFE REENT,<LOW=0	;NO INDEXING>
IFN REENT,<LOW=16	;OFFSET OF LOW SEGMENT DATA>

	EXTERN CHINN., IIB.,OVFLS.
	ENTRY	LINT.

LINT.:	PUSH PDP,ACA	;SAVE ACS
	PUSH PDP,B
	MOVEI B,0		;CLEAR STORAGE
	HLRZ ACA,-3(PDP)	;PICK UP FIELD WIDTH
	JUMPLE ACA,LIAR	;IF W <,= 0, RETURN
LIG:	PUSHJ PDP,CHINN.	;GET A CHARACTER (AC0)
	SKIPN	OVFLS.(LOW)	;DONT BUMP PTR AT END OF LINE
	XCT	IIB.		;INCREMENT POINTER
	CAIE AC0," "		;A BLANK?
	JRST NBLK		;NO
	SOJG ACA,LIG	;YES, GET NEXT CHARACTER
	JRST LIAR		;ALL BLANKS - SET NORMAL RETURN
NBLK:	CAIN AC0,"F"
	JRST FALSE 	;F
	CAIE AC0,"T"
	JRST	NOTTF		;NOT F OR T
	SETO B,		;T, SET CHAR NEGATIVE
FALSE:	SOJN ACA,SKIP	;DECREMENT FIELD WIDTH
LIAR:	MOVE AC0,B	;NORMAL RETURN
	AOS -2(PDP)
IL:	POP PDP,B	;RESTORE AC'S
	POP PDP,ACA
	POPJ PDP,		;RETURN
NOTTF:	CAIE	AC0,15		;CARRIAGE RETURN?
	JRST	IL		;NO,ILLEGAL CHAR
SKIP:	PUSHJ PDP,CHINN.	;GET A CHAR
	SKIPN	OVFLS.(LOW)	;DONT BUMP PTR IF END OF LINE
	XCT IIB.		;ADVANCE POINTER
	SOJG ACA,SKIP	;DECREMENT WIDTH, - CHAR IS SKIPPED
	JRST LIAR		;NORMAL RETURN

	END

