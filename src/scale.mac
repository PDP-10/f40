TITLE SCALE - A PLOTTER SUBPROGRAM
SUBTTL R CLEMENTS/DMN	30-APR-71	V30.0.145

	P=17	;SYSTEM PDL
	Q=16	;FOR JSA
;	0	FOR TEMPS AND ARGS
;	1	ALSO
	A=2
	X=3
	XMAX=4
	XMIN=5

EXTERN INT,EXP2.0,ALOG10,FLOAT
ENTRY SCALE
SCALE:	0
	MOVEM 5,ACS+5	;SAVE ACS
	MOVEI 5,ACS
	BLT 5,ACS+4
	HRRZ X,0(Q)	;ADR OF ARRAY
	HRLI X,A	;FOR INDEXING INTO X
	MOVEI A,0
	MOVE XMAX,@X
	MOVE XMIN,@X	;INITIAL VALUES=X(1)
L1:	CAMGE XMAX,@X
	MOVE XMAX,@X
	CAMLE XMIN,@X
	MOVE XMIN,@X
	ADDI A,1
	CAMGE A,@1(Q)	;ARRAY LENGTH
	JRST L1

	MOVE A,XMAX
	FSBR A,XMIN
	FMPR A,[10.0]
	FDVR A,@2(Q)	;LENGTH OF LINE
	FADR A,XMIN
	MOVEM A,XMAX
	FSBR A,XMIN
	MOVMS A
	MOVEM A,D
	JSA Q,ALOG10
	JUMP 2,D
	MULI	0,400		;USE OLD NON-SYMETRIC FIX ROUTINE
	TSC	0,0		;CODED IN LINE TO SAVE TIME
	EXCH	0,1
	ASH	0,-243(1)
	MOVEM 0,IE
	MOVSI 0,(10.0)
	MOVE 1,IE
	PUSHJ P,EXP2.0
	MOVE 1,D
	FDVR 1,0
	CAMLE 1,[8.0]
	JRST P2
	CAMG 1,[8.0]
	MOVSI 0,(8.0)
	CAMG 1,[5.0]
	MOVSI 0,(5.0)
	CAMG 1,[4.0]
	MOVSI 0,(4.0)
	CAMG 1,[2.0]
	MOVSI 0,(2.0)
	CAMG 1,[1.0]
	MOVSI 0,(1.0)
	JRST P1
P2:	MOVSI 0,(1.0)
	AOS IE
P1:	MOVEM 0,S
	MOVE 1,IE
	SUBI 1,1
	MOVSI 0,(10.0)
	PUSHJ P,EXP2.0
	MOVEM 0,E1
	FMPR 0,S
	MOVEM 0,D
	MOVEM 0,@4(Q)	;RETURN DX TO CALLER
	MOVE A,XMIN
	FDVR A,D
	JSA Q,INT
	JUMP 2,A
	MOVEM 0,A
	JSA Q,FLOAT
	JUMP 0,A
	FMPR 0,D
	MOVEM 0,XMIN
	MOVEI A,0
L2:	MOVE 0,@X
	FSBR 0,XMIN
	FDVR 0,D
	MOVEM 0,@X
	ADDI A,1
	CAMGE A,@1(Q)
	JRST L2

	MOVEM XMIN,@3(Q)	;RETURN XMIN
	MOVSI 5,ACS
	BLT 5,5
	JRA Q,5(Q)

ACS:	BLOCK 6

IE:	0
E1:	0
S:	0
D:	0

	LIT
	END

