	TITLE	CLOG	V.027
	SUBTTL	15-JUL-70	PW/KK/DMN
;FROM	V.021	22-SEP-69

;COMPLEX LOGARITHM FUNCTION.
;THIS ROUTINE CALCULATES THE LOGARITHM OF A COMPLEX
;ARGUMENT, Z = X + I*Y, WITH THE FOLLOWING ALGORITHM:

;	CLOG(Z) = LOG(ABS(Z)) + I*ATAN2(Y,X)

;WHERE ABS(Z) IS CALCULATED AS L*SQRT(1.0 + (S/L)**2)
;BECAUSE OF OVERFLOW/UNDERFLOW (L IS THE LARGER 
;OF X AND Y, AND S IS THE SMALLER.)


;A SPECIAL CASE CHECK IS MADE FOR X=0 AND Y=0
;AND AN ANSWER WITH REAL = - INFINITY AND IMAG = 0
;IS RETURNED.

;THE ROUTINE IS CALLED IN THE FOLLOWING MANNER:
;	JSA	Q,CLOG
;	EXP	ARG
;THE REAL PART OF THE ANSWER IS RETURNED IN ACCUMULATOR A
;AND THE IMAGINARY PART IS RETURNED IN ACCUMULATOR B.

	ENTRY	CLOG
	EXTERN	ALOG,ATAN2,SQRT,TYPER.

A=0
B=1
C=10
D=11
Q=16
P=17

	SIXBIT/CLOG/
CLOG:	0			;ENTRY TO COMPLEX LOG ROUTINE.
	MOVEI	1,@(Q)		;GET ADDRESS OF COMPLEX ARG.
	MOVE	0,(1)		;PICK UP REAL PART OF ARG.
	MOVE	1,1(1)		;PICK UP IMAG. PART OF ARG.
	JUMPN	0,NORMAL	;SPECIAL CASE CHECK
	JUMPN	1,NORMAL	;FOR X=0 AND Y=0.
	MOVEI	A,3		;SET UP ARG FOR ERROR MESSAGE.
	PUSHJ	P,TYPER.	;GO TO ERROR MESSAGE ROUTINE.
	MOVE	A,[400000000001]	;THE ANSWER RETURNED IS
	SETZ	B,		;REAL=-INFINITY, IMAG. = 0.
	JRA	Q,1(Q)		;RETURN.

NORMAL:	MOVEM	C,SAVEC		;SAVE AC C.
	MOVEM	D,SAVED		;SAVE AC D.
	MOVE	C,0		;REAL OF ARG. TO AC C.
	MOVE	D,1		;IMAG OF ARG. TO AC D.
	JSA	Q,ATAN2		;CALCULATE IMAG.
	EXP	D		;PART OF
	EXP	C		;ANSWER.
	JUMPGE	A,.+2		;ANGLE IS OK IF >= 0,
	FADR	A,TWOPI		;ADD 2*PI IF ANGLE WAS < 0.
	MOVEM	A,IMAG		;STORE ANSWER IN IMAG.
	MOVMS	C,C		;GET ABS(X).
	MOVMS	D,D		;GET ABS(Y).
	CAMGE	D,C		;PUT LARGER OF X AND Y
	EXCH	D,C		;IN AC D AND SMALLER IN AC C.
	FDVR	C,D		;CALC. S/L.
	JFCL			;NO UNDERFLOW ERROR MESSAGE ALLOWED.
	FMPR	C,C		;CALC. (S/L)**2.
	JFCL			;NO UNDERFLOW ERROR MESSAGE ALLOWED.
	FADRI	C,201400	;CALC. 1.0 + (S/L)**2.
	JSA	Q,SQRT		;CALC. THE SQRT OF THIS
	EXP	C		;ARG.
	MOVE	C,A		;THE ANSWER IS IN AC A AND AC C.
	FMPR	C,D		;CALC. L*SQRT.
	JFCL	OVER		;JUMP IF IT OVERFLOWS.
	JSA	Q,ALOG		;CALC. LOG(ABS(Z)).
	EXP	C		;ARG FOR ALOG ROUTINE IS IN AC C.
OUT:	MOVE	B,IMAG		;SET UP IMAG. PART OF ANSWER.
	MOVE	C,SAVEC		;RESTORE AC C.
	MOVE	D,SAVED		;RESTORE AC D.
	JRA	Q,1(Q)		;RETURN.

OVER:	MOVEM	A,SAVEHF	;ARG. TO SAVEHF.
	JSA	Q,ALOG		;CALC. ALOG(SQRT).
	EXP	SAVEHF		;ARG. FOR ALOG IS IN SAVEHF.
	MOVEM	A,SAVEHF	;STORE ANSWER IN SAVEHF.
	JSA	Q,ALOG		;CALC. ALOG(L).
	EXP	D		;L IS IN AC D.
	FADR	A,SAVEHF	;SUM THE TWO LOGS.
	JRST	OUT		;GO TO RETURN.



TWOPI:	203622077325		;2*PI.
SAVEHF:	0
IMAG:	0
SAVEC:	0
SAVED:	0
	END




