TITLE	DEXP.2 	PDP-10 DOUBLE PRECISION EXP2 FUNCTION
SUBTTL	V.027V	7-NOV-1970	/KK/TWE

;FROM V.020	2 MAY 1969	/TWE
;FROM V.005, 26 FEBRUARY 1966	ED YOURDON
;THIS ROUTINE CALCULATES A DOUBLE PRECISION NUMBER RAISED
;TO A FIXED POINT POWER. THE CALCULATION IS A**N, WHERE N
;IS AN INTEGER OF THE FORM
;	N= Q(0) + Q(1)*2 + Q(2)*4 + ... WHERE Q(I) = 0 OR 1
;THE ONLY RESTRICTION ON THE BASE OR EXPONENT IS THAT
;AN EXPONENT OF 400000000000 IS NOT HANDLED CORRECTLY.

;THE ROUTINE IS CALLED BY
;	MOVEI	Q, POWER
;	PUSHJ	P, DEXP2
;WHERE POWER IS THE ADDRESS OF THE FIXED POINT POWER, AND
;THE DOUBLE PRECISION BASE IS IN ACCUMULATORS A AND B. THE
;RESULT IS RETURNED IN ACCUMULATORS A AND B.

	ENTRY	DEXP.2
	EXTERN TYPER.
;	SEARCH DEF40

	A=	0
	B=	1
	C=	2
	D=	3
	E=	4
	G=	6

	Q=	16
	P=	17

	X=	G	;HIGHEST AC TO SAVE
MLON

	SIXBIT /DEXP.2/

DEXP.2:	SKIPN	(Q)		;IS EXPONENT 0?
	JRST	[MOVSI A,(1.0)	;YES, A**0 GIVES 1
		MOVEI B,0
		POPJ P,]
	JUMPE	A,[SKIPL (Q)	;IS BASE 0 WITH POSITIVE EXPONENT?
		POPJ P,		;YES, RETURN 0
		MOVEI 0,3	;BASE IS 0 WITH NEG. EXP- OVERFLOW
		PUSHJ P,TYPER.	;TYPE OVERFLOW MESSAGE
		HRLOI A,377777	;RETURN LARGEST POSITIVE NUMBER
		HRLOI B,344777
		POPJ P,]
	MOVEM X,XSAVE		;SAVE AC TO DO BLT
	MOVE X,XBLT		;SAVE OTHER AC'S
	BLT X,XSAVE-1		;...

	SKIPL G,(Q)		;GET EXPONENT. IS IT NEGATIVE?
	JRST	[DMOVE D,A	;NO, PUT ARG IN D,D+1
		JRST DEX2]	;START MAIN LOOP
	MOVMS G			;GET POSITIVE VALUE
	MOVSI D,(1.0)		;GET DOUB. PRECISION 1.0
	MOVEI E,0		;...
				;CALCULATE (1/X)**N, SINCE N .L. 0
	FDVL	3,0
	JFCL	1,OVER
	MOVN	5,3
	FMPR	5,1
	JFCL
	UFA	4,5
	FDVR	5,0
	JFCL
	FADL	3,5


DEX2:	MOVSI A,(1.0)		;GET DOUB. PREC. 1.0
	MOVEI B,0		;...
	JRST DEX4		;START CALCULATING POWERS OF X (OR 1/X)

DEX3:				;SQUARE X (OR 1/X) AGAIN
DMOVEM D,TEMP

	MOVEM D,D+2
	FMPR D+2,TEMP+1
	JFCL
	FMPR D+1,TEMP
	JFCL
	UFA D+1,D+2
	JFCL
	FMPL D,TEMP
	JOV OVR
	UFA D+1,D+2
	FADL D,D+2
	JOV OVR

	LSH G,-1		;LOOK AT NEXT BIT IN N
DEX4:	TRZN G,1		;IS LO BIT IN N A 1?
	JRST DEX5		;NO, DON'T MULTIPLY INTO ANSWER
				;MULTIPLY POWER OF X INTO ANSWER
	MOVEM A,A+2
	FMPR A+2,D+1
	JFCL
	FMPR A+1,D
	JFCL
	UFA A+1,A+2
	JFCL
	FMPL A,D
	JOV DEX6
	UFA A+1,A+2
	FADL A,A+2
	JOV DEX6

DEX5:	JUMPN G,DEX3		;IF G .N. 0, GET MORE POWERS OF X (OR 1/X)
DEX6:	MOVS X,XBLT		;RESTORE AC'S
	BLT X,X			;...
CPOPJ:	POPJ P,

OVR:				;ARITHMETIC FAULT, MOVE FIX UP TO A,B
	SKIPGE A		;SHOULD RESULT BE NEGATIVE?
	DFN D,E			;YES
OVR2:
DMOVE A,D

	JRST DEX6		;AND EXIT

OVER:	SKIPL	D		;IF THE ARG IS < 0
	JRST	OVR2		;AND THE EXPONENT
	TRNN	G,1		;IS ODD, THEN
	DFN	D,E		;THE ANSWER
	JRST	OVR2		;IS < 0.


XBLT:	XWD C,ACSAVE		;BLT POINTER TO RESTORE AC'S
ACSAVE:	BLOCK X-C
XSAVE:	BLOCK 1			;FOR AC X

TEMP:	BLOCK 2

END

