	TITLE	COSH	V.027
	SUBTTL	9-JUL-70	ED YOURDAN/KK/DMN
;FROM	V.021	26-AUG-69

;FLOATING POINT SINGLE PRECISION HYPERBOLIC COSINE FUNCTION.

;COSH(X) IS CALCULATED AS FOLLOWS:
;	IF ABS(X) <= 88.029,
;		COSH(X) = 1/2(EXP(X) + 1.0/EXP(X))
;	IF ABS(X) > 88.029 AND (ABS(X)-LN(2)) <= 88.029,
;		COSH(X) = EXP(ABS(X)-LN(2))
;	IF (ABS(X)-LN(2)) > 88.029,
;		COSH(X)=377777777777
;		AND AN ERROR MESSAGE IS RETURNED.

;THE ROUTINE IS CALLED IN THE FOLLOWING MANNER:
;	JSA	16,COSH
;	EXP	ARG
;THE ANSWER IS RETURNED IN AC 0.

	EXTERN	EXP,TYPER.
	ENTRY	COSH



	SIXBIT/COSH/
COSH:	0			;ENTRY TO HYPERBOLIC COSINE ROUTINE.
	MOVE	0,@(16)		;PICK UP THE ARGUMENT.
	MOVEM	2,SAVE2		;SAVE AC 2.
	MOVM	2,0		;PUT ABS(X) IN AC 2.
	CAMLE	2,EIGHT8	;IF ABS(X) > 88.029,
	JRST	OV88		;GO TO OV88.
	JSA	16,EXP		;O'E, CALC. EXP(ABS(X))
	EXP	2		;ARG FOR EXP ROUTINE IS IN AC 2.
	MOVSI	2,201400	;PUT 1.0 IN AC 2.
	FDVR	2,0		;CALC. 1.0/EXP(ABS(X)).
	FADR	0,2		;CALC. EXP(ABS(X)) + EXP(-ABS(X)).
	FDVRI	0,202400	;DIVIDE THIS BY 2.0.
	MOVE	2,SAVE2		;RESTORE AC 2.
	JRA	16,1(16)	;RETURN.

OV88:	FSBR	2,LN2BE		;FORM ABS(X)-LN(2).
	CAMG	2,EIGHT8	;OVERFLOW?
	JRST	EXPP		;NO,GO AHEAD.
	MOVEI	1,3		;YES, SET UP FOR ERROR MESSAGE.
	PUSHJ	17,TYPER.	;TYPE OUT ERROR MESSAGE.
	HRLOI	0,377777	;ANSWER = +INFINITY.
	MOVE	2,SAVE2		;RESTORE AC 2.
	JRA	16,1(16)	;RETURN.

EXPP:	JSA	16,EXP		;CALC. EXP(ABS(X)-LN(2)).
	EXP	2		;ARG FOR EXP ROUTINE IS IN AC 2.
	MOVE	2,SAVE2		;RESTORE “AC 2.
	JRA	16,1(16)	;RETURN.

SAVE2:	0
EIGHT8:	207540074636	;88.029
LN2BE:	200542710300	;LOG(2) BASE E.

	END

