TITLE DFMP	V.027J		1-OCT-1970
SUBTTL DOUBLE PRECISION FLOATING MULTIPLY- TOM EGGERS/TWE

Q=16	;POINTS TO MEMORY ARG
M=Q	;M IS MORE MNEMONIC
P=17	;PUSH DOWN POINTER

DEFINE FLMUL (X,%OV)<
	PUSH P,X+2		;SAVE X+2
	PUSH P,(M)		;GET HIGH PART OF MEM ARG OUT OF WAY OF X+2

	MOVEM X,X+2		;COPY HIGH PART OF AC ARG
	FMPR X+2,1(M)		;FORM 1 CROSS PRODUCT
	JFCL (2)		;IN CASE OF UNDERFLOW, UN-NORMALIZE
	FMPR X+1,(P)		;FORM OTHER CROSS PRODUCT
	JFCL (2)		;IN CASE OF UNDERFLOW, UN-NORMALIZE
	UFA X+1,X+2		;ADD CROSS PRODUCTS
	JFCL			;OVERFLOW ONLY IF NEXT INSTRUCTION ALSO
	FMPL X,(P)		;FORM PRODUCT OF HIGH ORDER PARTS
	JOV %OV			;FIX AND XFER ICASE OF OVER/UNDERFLOW
	UFA X+1,X+2		;ADD XROSS PRODUCT SUM INTO LOW WORD
	FADL X,X+2		;ADD HIGH AND LOW WORDS
%OV:	POP P,X+2		;REMOVE HIGH MEM ARG FROM LIST
	POP P,X+2		;RESTORE X+2
>

DEFINE DFM(X)<

ENTRY DFM.'X
	SIXBIT /DFM.'X/
DFM.'X:	FLMUL X
	POPJ P,
>


DEFINE DFMM(X)<

ENTRY DFMM.'X
	SIXBIT /DFMM.'X/
DFMM.'X:	FLMUL X
	MOVEM X,(M)		;PUT ANSWER IN MEMORY
	MOVEM X+1,1(M)
	POPJ P,
>

DEFINE EXPAND(X)<
IRP X,<
DFM X
DFMM X
>>

EXPAND <0,2,4,6>

END

