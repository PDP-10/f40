SUBTTL	HIGH-SEGMENT GLOBALS	V30.211	 11-JUN-71	D NIXON/DMN
TITLE	LOW40 IMPURE PART OF FORTRAN IV OPERATING SYSTEM

;THESE GLOBAL SYMBOLS CORESPOND TO PURE40
;SO THAT PURE40 NEED NOT BE LOADED ONCE FORTR.SHR IS ON SYS

;REENT==1 GIVES VERSION TO LOAD TO MAKE FORTR.SHR
;REENT==0 IS NORMAL VERSION ON SYS AS IMP40.REL
IFNDEF REENT,<REENT==0>

DEFINE	MAC2(A)
<IFE REENT,<	A=.%
ENTRY	A
.%==.%+1>>

DEFINE	MAC1(A)
<IFE REENT,<	A=.%
ENTRY	A>>

DEFINE MAC3(A)
<IFE REENT,<.%==.%+1>>

.%==400010		;START OF HISEG PROGRAM

INTERN	UUOH.,UUOL.,TYPER.

;THE FOLLOWING ARE CAUSED BY REENTRANCY
IFN REENT,<
INTERN	ALPHA.,OMEGA.	
INTERN	BUFSZ.,DMPIO.,RLINK.,DADDR.,INIFLG,HDRADD
INTERN	PAKFL.,INPDEV,INPDV.,FOBPDP,LASTLP
INTERN	SAVEAC,SAVFAC,BUFHD.,DNAME,RERDV.
INTERN	RPTR1,RPTR2,RCNT1,RCNT2,LINBUF,LINHDR
INTERN	ITMCNT,FRPTC.,FILNUM,DEVNAM,DIGPTR,GRPRPT
INTERN	DECHDR,EDERR,ENCHDR,END.,ERR.
INTERN	RERDN.,SAVSCN,SXBTNO,VADDR.,MTABF.,TABPT.,LBPTR
INTERN	WDPREC,CHARS.,WORDS.
>


;TO FORCE LOADING OF REST OF IMP40.REL
EXTERN	OVTRAP,EXIT.,JOBUUO

;ACCUMULATOR DEFINITIONS

A=1
LOW=16
SUBTTL	USER INTERFACE

;ENTRY FROM USER

UUOH.:	0
	EXCH	LOW,SAVEAC+LOW	;SAVE AC16 AND LOAD OFFSET
	MOVEM	0,SAVEAC+0	;SAVE AC 0
	EXCH	A,JOBUUO	;PICK UP UUO
	LDB	0,[POINT 9,A,8]	;PICK UP UUO OP CODE
	CAIE	0,15		;RESET. ?
UUOH1:	JRST	@UUOP.		;NO
	MOVEI	LOW,FORSE.-140	;MAKE SURE LOW IS CORRECT
UUOH3:	JRST	UUOH2		;PATCHED WHEN HISEG FOUND


;RETURN FROM USER

UUOL.:	EXCH	LOW,SAVEAC+LOW	;RESTORE AC16
	JRSTF	@UUOH.		;AND RETURN

;LOW SEG ENTRY TO TYPER. ROUTINE
IFN REENT,<	EXTERN	.TYPER>

TYPER.:	EXCH	LOW,SAVEAC+LOW
	JRST	@.TYPER		;GO TO HIGH-SEG NOW

SUBTTL	INITIALIZATION OF FORTR.SHR
IFN REENT,<	EXTERN	UUOP.>
	RELOC	ALPHA.


VBASE:	EXP	3		;VERSION OF DATA BASE
				;ONLY CHANGE IF WON'T RUN WITH CURRENT FORTR.SHR

UUOH2:	MOVEI	0,HISEG		;NO GET DATA FOR GETSEG UUO
	CALLI	0,40		;GETSEG UUO
	JRST	SEGERR		;CANNOT GET IT
	MOVE	0,.%		;GET HIGH SEG VERSION
	CAME	0,VBASE		;ARE VERSION NUMBERS THE SAME?
	JRST	VERERR		;NO PROBABLE ERROR
	MOVE	0,UUOP.		;ONLY DO IT ONCE
	MOVEM	0,UUOH1		;SAVES TIME EACH CALL
	ADDI	0,1		;FOR RESET.
	MOVEM	0,UUOH3
	MOVEI	LOW,FORSE.-140	;GET OFFSET
	MOVEI	0,15		;RESET UUO STILL
	JRST	@UUOH3		;GO TO PURE SEGMENT

HISEG:	SIXBIT	/SYS/
	SIXBIT	/FORTR/
	EXP	0,0,0,0

SEGMES:	ASCIZ	/?CANNOT ACCESS FORTR.SHR - GETSEG ERROR CODE /

SEGERR:	TTCALL	3,SEGMES
	IDIVI	0,8		;ALLOW TWO OCTAL DIGITS
	JUMPE	0,.+3		;ONLY ONE DIGIT
	ADDI	0,"0"		;MAKE ASCII
	TTCALL	1,0
	ADDI	1,"0"
	TTCALL	1,1
	CALLI	12		;EXIT

VERERR:	TTCALL	3,VERMES
	CALLI	12

VERMES:	ASCIZ	/?LIBRARY(FORTR.SHR) AND USER PROGRAM VERSION NUMBERS ARE DIFFERENT/

	RELOC
IF2,<	PURGE	VBASE,UUOH2,HISEG,SEGMES,SEGERR,VERERR,VERMES>

