	TITLE	ACOS	V.027
	SUBTTL	10-JULY-1970	KAREN KOLLING/DMN

;FROM V.022
;FLOATING POINT SINGLE PRECISION ARCCOSINE FUNCTION

;ACOS(X) IS CALCULATED IN THE FOLLOWING MANNER:
;	IF X > 0,	ACOS(X)=ATAN((SQRT(1-X?2))/X)
;	IF X < 0,	ACOS(X)=PI + ATAN((SQRT(1-X?2))/X)
;	IF X = 0,	ACOS(X)=PI/2

;THE RANGE OF DEFINITION FOR ACOS IS -1.0 TO +1.0.
;ARGUMENTS OUTSIDE OF THIS RANGE WILL CAUSE AN ERROR MESSAGE
;TO BE TYPED AND WILL RETURN AN ANSWER OF ZERO.

;THE CALLING SEQUENCE FOR ACOS IS:

;	JSA	16,ACOS
;	EXP	ARG


	ENTRY	ACOS
	EXTERN	ATAN,SQRT,TYPER.

	SIXBIT/ACOS/
ACOS:	0			;ENTRY TO ACOS ROUTINE.
	MOVM	0,@(16)		;GET /ARG./ IN AC 0.
	CAMLE	0,ONE		;IS MAGNITUDE OF ARG. GT 1.0?
	JRST	TOOLRG		;YES, GO TO ERROR RETURN.
	JUMPE	0,ZERARG	;IF ARG=0, GO TO ZERARG.
	FMPR	0,0		;X?2 IN AC 0.
	JFCL			;SUPPRESS ERROR MESSAGE FROM OVTRAP, IF NECESSARY.
	MOVNS	0		;-X?2 IN AC 0.
	FAD	0,ONE		;1.0-X?2 IN AC 0.
	MOVEM	0,TEMPSV	;ARG TO LOC NE 0 OR 1.
	JSA	16,SQRT		;CALC. SQRT(1.0-X?2).
	ARG	TEMPSV		;ADDRESS OF ARG FOR SQRT ROUTINE.
	FDVR	0,@(16)		;(SQRT(1.0-X?2))/X IS IN AC 0.
	JFCL			;SUPPRESS ERROR MESSAGE FROM OVTRAP, IF NECESSARY.
	MOVEM	0,TEMPSV	;ARG TO LOC NE 0 OR 1.
	JSA	16,ATAN		;FIND ATAN(SQRT(1.0-X?2)/X).
	EXP	TEMPSV		;ADDRESS OF ARG. FOR ATAN ROUTINE.
	SKIPL	@(16)		;SKIP IF ORIGINAL ARG < 0.
	JRA	16,1(16)	;RETURN.
	FAD	0,PII		;ANSWER IS PI - ORIGINAL ANSWER.
	JRA	16,1(16)	;RETURN.
ZERARG:	MOVE	0,PI2		;ANSWER IS PI/2.
	JRA	16,1(16)	;RETURN
TOOLRG:	MOVE	0,[XWD	[ASCIZ /ACOS OF ARG > 1.0 IN MAGNITUDE/],7]
	PUSHJ	17,TYPER.	;ERROR MESSAGE
	SETZ	0,		;SET ANSWER TO ZERO.
	JRA	16,1(16)	;RETURN.
ONE:	201400000000		;1.0
PI2:	201622077325
PII:	202622077325
TEMPSV:	0
	END
 
