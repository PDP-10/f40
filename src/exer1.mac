TITLE EXER1.  V.27
SUBTTL	FROM V.026B D NIXON/VAA/DMN 6/JUL/70
	;V.026B V ALUSIC	12/JUN/70
	;V.006 D NIXON		APRIL-3-69

;PROGRAM TO CATCH COMPILER-DETECTED ERRORS
;CALLED BY
;	PUSHJ	P, EXER1.
;EXER1. CALLS ERROR PRINT ROUTINE  WITH A MESSAGE
;	"SOURCE LEVEL ERROR AT USER LOC XXXXXX"
;	THEN IT GOES TO PAUSE
;EXER1. DESTROYS AC 1 BECAUSE PAUSE DOESNT SAVE IT.

	A=	0
	B=	1
	C=	2
	D=	3
	P=	17

%ARG==(7B2)
%A==0
%O==5
%Q==(1B4)

	ENTRY	EXER1.
	EXTERN	PAUSE.,ITYPM.

EXER1.:	SOS	(P)		;BACK UP POINTER
	PUSH	P,A		;SAVE SOME ACS
	PUSH	P,C
	MOVE	A,-2(P)		;ADDRESS OF ERROR
	PUSHJ	P,ITYPM.	;GENERAL PRINT ROUTINE IN ERROR.
	%A,	[ASCIZ /SOURCE LEVEL ERROR AT USER LOC /] (%ARG+%Q)
	%O,	A	(%ARG)
	MOVSI	A,(JFCL)	;NO-OP SO EXER1 ONLY CALLED ONCE
	MOVEM	A,@-2(P)	;PATCH PUSHJ P,EXER1
	POP	P,C
	POP	P,A		;RESTORE ACS
	MOVEI	B,0		;PAUSE ONLY
	JRST	PAUSE.		;RETURN TO USER PROG EVENTUALLY

	END

