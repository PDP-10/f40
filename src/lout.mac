TITLE LOUT.  V.27  	LOGICAL OUTPUT	FORTRAN IV
SUBTTL	15-JUNE-70	/DMN
;FROM		    /VAA      JULY-11-69      FROM: OCT. 28,1965
;REENT==1 FOR RE-ENTRANT OPERATING SYSTEM
IFNDEF REENT,<REENT==0>
IFN REENT,<	HISEG	>

;"LOUT." IS A ROUTINE WHICH OUTPUTS TO "DEPOT.", W-1 BLANKS
;FOLLOWED BY F OR T, DEPENDING ON WHETHER THE VALUE OF THE OUTPUT
;ITEM (IN AC0) IS POSITIVE (I.E. 0, OR FALSE) OR NEGATIVE (I.E. 1,
;OR TRUE).

;  CALLING SEQUENCE:
;	PUSHJ PDP,LOUT.
;  PUSHDOWN LIST CONTAINS:
;	1.  FIELD WIDTH W, INTEGER IN LEFT HALF
;	2.  PROGRAM COUNTER (RETURN ADDRESS)
;	3.-10.  FREE STORAGE

	EXTERN DEPOT.
	ENTRY LOUT.

LOUT.:	PUSH PDP,AC0	;STORE ACS
	PUSH PDP,ACA
	HLRZ ACA,-3(PDP)	;PICK UP FIELD WIDTH
	TRZE	ACA,200000	;G SPECIFICATION? CLEAR IT
	TRO	ACA,17		;SET FIELD WIDTH TO 15
	JUMPE ACA,LOR	;IF FIELD WIDTH = 0, RETURN
	SOJE ACA,NB	;IF FIELD WIDTH = 1, NO BLANKS
	MOVEI AC0," "
	PUSHJ PDP,DEPOT.	;OUTPUT A BLANK
	SOJG ACA,.-1	;ANY MORE?
NB:	MOVEI AC0,"F"	;NO, SET FALSE
	SKIPGE -1(PDP)	;SKIP IF OUTPUT ITEM IS FALSE
	MOVEI AC0,"T"	;OTHERWISE SET TRUE
	PUSHJ PDP,DEPOT.	;OUTPUT T OR F
LOR:	POP PDP,ACA	;RESTORE ACS
	POP PDP,AC0
	POPJ PDP,		;RETURN

	PDP=17		;PUSHDOWN POINTER
	ACA=1		;FIELD WIDTH
	AC0=0		;OUTPUT VALUE

	END

