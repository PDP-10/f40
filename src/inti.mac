TITLE INTI.  V30.0.143  DECIMAL INTEGER INPUT FORTRAN IV
SUBTTL	30-APR-71	/DMN
;V30 16-NOV-70	/DMN
;FROM 	V.027	9-JUL-70	/DMN	FROM	V.005  2-JUN-67

;REENT==1 GIVES RE-ENTRANT FORTRAN OP SYSTEM
IFNDEF REENT,<REENT==0>
IFN REENT,<	HISEG	>

;"INTI." IS A ROUTINE WHICH GETS DECIMAL ASCII
;CHARACTERS FROM "CHINN." (IN ACO),AND INPUTS THEM AS
;OCTAL INTEGERS (IN ACO). "IIB." INCREMENTS THE BYTE PTR.

;CALLING SEQUENCE:
;	PUSHJ P,INTI.
;2 RETURNS:
;	ILLEGAL CHARACTER
;	NORMAL
;PUSH DOWN LIST CONTAINS:
;	1.	W: FIELD WIDTH OF INPUT STRING;
;		  BITS 11-17
;		   W=0 MEANS VARIABLE FIELD

;	2.	PROGRAM COUNTER (RETURN ADDRESS)
;	3. THRU 6. FREE STORAGE

	ACO=0			;RECEIVES CHAR. FROM CHINN. ,
				;AND RETURNS THE INPUT ITEM
	ACTH=1			;FIELD WIDTH
	ACT=2			;FLAG
	D=3			;STORAGE
	N=4			;NO. OF ACS SAVED +1
	PDP=17			;PUSH DOWN POINTER

IFN REENT,<LOW=16>
IFE REENT,<LOW=0>
     

	ENTRY	INTI.
	EXTERN	IIB.,CHINN.,DELIM.,OVFLS.,TTYFL.

INTI.:	PUSH	PDP,ACT		;SAVE
	PUSH	PDP,D		;THE
	PUSH	PDP,ACTH	;ACS
	LDB	ACTH,[POINT 7,-N(PDP),17];PICK UP FIELD WIDTH
	SETZB	D,ACT		;SET TO ZEROS
	JUMPN	ACTH,.+2	;A VARIABLE FIELD?
	SETO	ACTH,		;YES, SET W TO -1
	JFCL	17,.+1		;CLEAR ALL OVERFLOW INDICATORS.
				;REG 10-10-72
GET:	JUMPE	ACTH,INTIN	;IF W=0, (NO MORE CHARS)
				;RETURN
	PUSHJ	PDP,CHINN.	;GOT A CHAR. (ACO)
	SUBI	ACTH,1		;DECREMENT FIELD WIDTH
	CAIG	ACO,71		;CHECK FOR A
	CAIGE	ACO,60		;DIGIT.
	JRST	ACH		;CHAR. NOT A DIGIT
	TLO	ACT,400000	;DIGIT, SET FLAG
	TLNE	ACT,100000	;INTERGER OVERFLOW OCCURED BEFORE?
	JRST	IIB		;YES, AND WILL HAPPEN AGAIN OTHERWISE
	SUBI	ACO,60		;GET ACTUAL NUMBER
FRMBLK:	IMULI	D,12		;FORM
	JFCL	11,OVRFLO	;TRAP INTEGER OVERFLOW
	ADD	D,ACO		;AN INTEGER
IIB:	SKIPN	OVFLS.(LOW)
	XCT	IIB.		;ADVANCE POINTER
	JRST	GET		;GET ANOTHER CHAR.

OVRFLO:	TLO	ACT,100000	;SIGNAL OVERFLOW OCCURED
	JRST	GET		;GET NEXT CHAR.
     

ACH:	CAIN	ACO,15		;CARRIAGE RETURN?
	JRST	CRIN		;YES
	CAIN	ACO,40		;BLANK?
	JRST	BLNK		;CHAR. IS BLANK
	CAIE	ACO,55		;TEST FOR-
	CAIN	ACO,53		;TEST FOR+
	JRST	PLSMNS		;= TO + OR-
	JUMPGE	ACTH,IIL	;ILLEGAL IF NOT A VARIABLE FLD
CKCR:	CAIE	ACO,15		;SKIP IF CARRIAGE RETURN
	XCT	IIB.		;VARIABLE FIELD, ADVANCE POINTER
	JRST	INTIN		;NORMAL RETURN

CRIN:	TLNE	16,TTYFL.	;INPUT FROM TTY?
	SETO	ACTH,		;YES, AS IF DELIMITER MODE
	JRST	.+2		;SKIP TEST FOR BLANK
BLNK:	JUMPGE	ACT,.+2		;IGNORE BLKS IF NO DIGITS YET
	JUMPL	ACTH,CKCR	;IF VAR. FLD. ,NORMAL RETURN
	MOVEI	ACO,0		;IGNORE BLKS -NO DIGITS
				;OR W NOT = 0.
	JRST	FRMBLK		;OUTPUT A 0
PLSMNS:	JUMPL	ACT,VARYT	;IS THE FIRST DIGIT IN
	TRNE	ACO,4		;TEST FOR -
	TLO	ACT,200000	;NEG, SET FLAG
	JRST	IIB		;GET NEXT CHAR.
VARYT:	JUMPGE	ACTH,IIL	;NO-ILLEGAL CHARACTER
	AOS	2(7)		;INCREMENT THE ITEM COUNT
INTIN:	TLZ	D,400000	;MAKE CHAR. POSITIVE
				;(FOR OVERFLOW)
	MOVEM	ACO,DELIM.(LOW)	;SAVE DELIMITER FOR NAMELIST
	TLNE	ACT,200000	;NEGATIVE
	MOVNS	D		;YES, NEGATE CHAR.
	MOVE	ACO,D		;SET NORMAL RETURN
	AOS	-3(PDP)
IIL:	POP	PDP,ACTH	;RESTORE
	POP	PDP,D		;ACS TO
	POP	PDP,ACT		;ORIGINAL VALUES
	POPJ	PDP,		;RETURN

	END
     


