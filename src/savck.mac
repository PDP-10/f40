TITLE	SAVCK. V30.0.141
SUBTTL	V30.0.141	29-APR-71	/DMN
;V.001  31-JAN-68 N PAPPAS


ENTRY		SAVCK.,SAVP1.,SAVPL.,INTVR.,UNSV.
EXTERNAL	DEVER.,DVTOT.,DEVTB.,DYNND.,DYNDV.,INIER.,UUOL.


A=1
B=2
C=3
D=4
E=5
F=6
Q=16
P=17


;USES A SEVEN WORD BLOCK TO SAVE AC'S 0-6
;VERIFIES THAT DEV NUM IS LEGAL
;ZEROES D AND E, DEV NUM IN A&0
;CALLED BY PUSHJ P,SAVCK.

SAVCK.:	MOVEM	0,SAVPL.		;SAVE
	MOVE	0,[XWD A,SAVP1.]	;AC'S
	BLT	0,SAVPL.+6		;0-6
	SETZB	D,E		;INITIALIZE STORAGE AREAS
	MOVE	0,@(Q)		;GET DEV NUM FOR DEVER.
	CAIGE	0,77		;IS DEV NUM BELOW LIMIT?
	SKIPG	A,0		;YES, BUT IS IT >0?
	PUSHJ	P,DIER		;NO, FATAL ERROR!
	POPJ	P,		;GOOD RETURN

DIER:	XCT	UUOL.		;SET UP AC 16
	JRST	DEVER.		;BEFORE GOING TO OP-SYSTEM

UNSV.:	MOVS	0,[XWD A,SAVP1.]	;
	BLT	0,F		;RESTORE
	MOVE	0,SAVPL.
	POPJ	P,
; GET DEVICE NAME AND SEE IF DEVICE ALREADY INIT
; CALLED BY :  PUSHJ P,INTVR.
; EXPECTS DEV NUM IN A
; USES: 0,E,F
; BAD RETURN: INIER., DEV NUM IN A, PC&FLAGS IN E
; GOOD RETURN: DEV NAM IN E, (F)=PTR TO DYNDV.,DEV NUM IN A,(0)=GARBAGE


INTVR.:	MOVE	C,A		;SAVE DEV NUM IN C
	IDIVI	A,12		;CONVERT DEVICE NUMBER TO 6BIT
	LSH	A,6
	OR	A,B
	IORI	A,2020
	TRNN	A,700
	LSH	A,6
	LSH	A,30
	MOVE	E,A
	CALLI	E,4
	TRNN	E,400000	;IS UNIT ASSIGNED BY CONSOL?
	JRST[	CAILE	C,DVTOT.;NOT ASSIGNED BY CONSOL.TOO BIG FOR DEVTB.?
		PUSHJ	P,DIER		;YES, FAIL
		MOVE	E,DEVTB.(C)	;NO, GET DEFAULT NAME
		JRST	INTVR1	]	;RETURN
	MOVE	E,A		;YES ASSIGNED BY CONSOL
INTVR1:	MOVEI	F,DYNDV.	;START-OF-DYNDV. IN F
NAMLOP:	MOVE	0,(F)		;PICK UP A NAME
	TRZ	0,77		;CLEAR CHAN. NUMBER
	CAMN	E,0		;THE ONE WE WANT?
	JSP	0,DIE		;YES,FAIL!
	CAIGE	F,DYNND.	;END OF SEARCH?
	AOJA	F,NAMLOP	;NO, UPDATE PTR
	POPJ	P,		;YES, NOT YET INIT

DIE:	XCT	UUOL.		;SET UP AC 16 FIRST
	MOVE	7,[PUSHJ P,INIER.]
	JRST	7		;DEV NAM AT PC-2


SAVPL.:	BLOCK	7	;AC'S SAVED HERE
SAVP1.=SAVPL.+1

	END

