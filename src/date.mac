	TITLE	DATE	V.022	TODAY'S DATE	FORTRAN IV
	SUBTTL	18-DECEMBER-1969	/KK

;FROM V.005
;EDITED (BY TWE) 11 DEC. 68 TO REMOVE GLITCH CHARACTERS

;THIS SUBROUTINE PUTS TODAY'S DATE INTO A
;DIMENSIONED TWO-WORD ARRAY.

;THE DATE WILL BE IN THE FORM:
;	17-AUG-66

;THE ROUTINE IS CALLED IN THE FOLLOWING MANNER:
;	JSA	16,DATE
;	ARG	A

;"DATE" OBTAINS THE DATE FROM THE MONITOR IN THE FORM:
;	( (YEAR-1964)*12 + (MONTH-1) )*31 + (DAY-1)

	ENTRY	DATE

	SIXBIT/DATE/
DATE:	0			;ENTRY TO DATE ROUTINE.
	MOVEM	2,S2		;SAVE THE CONTENTS OF AC 2.
	MOVEM	3,S3		;SAVE THE CONTENTS OF AC 3.
	MOVEM	4,S4		;SAVE THE CONTENTS OF AC 4.
	CALLI	1,14		;GET THE DATE FROM THE MONITOR.
	IDIVI	1,^D31		;DIV. BY 31 TO OBTAIN THE DAY-1.
	AOS	2		;TO OBTAIN THE DAY.
	IDIVI	2,^D10		;CONVERT INTO TWO DEC. DIGITS.
	SKIPN	2		;IS THE DAY < 10?
	MOVNI	2,20		;YES, OUTPUT BLANK.
	MOVE	0,0(16)		;CREATE BYTE POINTER.
	HRLI	0,440700	;TO DEPOSIT DATE
	JSP	4,SUB1		;DEPOSIT DAY
	IDIVI	1,^D12		;TO OBTAIN THE MONTH
	MOVE	3,[POINT 7,TABLE(2)] ;BYTE POINTER FOR MONTH
GETMON:	ILDB	4,3		;GET MONTH FROM THE TABLE
	IDPB	4,0		;DEPOSIT MONTH
	TLNE	3,760000	;ALL OF THE MONTH?
	JRST	GETMON		;NO, GET NEXT CHAR.
	MOVEI	2,^D64(1)	;YES, GET THE YEAR
	IDIVI	2,^D10		;CONVERT INTO TWO DEC. DIGITS
	JSP	4,SUB1		;DEPOSIT YEAR
	MOVE	2,S2		;RESTORE THE CONTENTS OF AC 2.
	MOVE	3,S3		;RESTORE THE CONTENTS OF AC 3.
	MOVE	4,S4		;RESTORE THE CONTENTS OF AC 4.
	JRA	16,1(16)	;RETURN.

;SUB1 CONVERTS THE DAY AND THE YEAR INTO ASCII CHARS, AND
;DEPOSITS THEM IN THE TWO WORD ARRAY.

SUB1:	ADDI	2,60		;CONVERT FIRST DIGIT TO ASCII
	IDPB	2,0		;DEPOSIT FIRST DIGIT
	ADDI	3,60		;CONVERT SECOND DIGIT TO ASCII
	IDPB	3,0		;DEPOSIT SECOND DIGIT
	JRST	(4)		;RETURN TO MAIN SEQ.

TABLE:	ASCII	/-JAN-/
	ASCII	/-FEB-/
	ASCII	/-MAR-/
	ASCII	/-APR-/
	ASCII	/-MAY-/
	ASCII	/-JUN-/
	ASCII	/-JUL-/
	ASCII	/-AUG-/
	ASCII	/-SEP-/
	ASCII	/-OCT-/
	ASCII	/-NOV-/
	ASCII	/-DEC-/

S2:	0
S3:	0
S4:	0

	END

