TITLE DSIGN	V.022	PDP-10
SUBTTL	29-DECEMBER-1969	/KK

;FROM V.006.
;DOUBLE PRECISION TRANSFER OF SIGN
;THIS ROUTINE RETURNS ABSF(ARG1)*SIGN(ARG2)
;THE ROUTINE MAKES USE OF THE FOLLOWING TABLE:
;ARG1	ARG2	RESULT	CHANGE OF SIGN?
;+	+	+	NO
;+	-	-	YES
;-	+	+	YES
;-	-	-	NO

;THE CALLING SEQUENCE FOR THIS ROUTINE IS AS FOLLOWS
;	JSA	Q,DSIGN
;	EXP	ARG1
;	EXP	ARG2
;ARG1 AND ARG2 ARE THE ADDRESSES OF THE HIGH ORDER WORDS OF
;THE DOUBLE PRECISION ARGUMENTS, THE DOUBLE PRECISION
;ANSWER IS RETURNED IN ACCUMULATORS A AND B.

	ENTRY DSIGN

	A=0
	B=1
	Q=16

	SIXBIT/DSIGN/
DSIGN:	0			;ENTRY TO DSIGN ROUTINE
	MOVEI	B,@(Q)		;GET ADDRESS OF FIRST ARGUMENT
	MOVE	A,(B)		;PICK UP HIGH ORDER OF ARG1
	MOVE	B,1(B)		;PICK UP LOW ORDER OF ARG1
	SKIPGE	@1(Q)		;THEN
	JUMPL	A,OUT		;CHOOSE
	SKIPL	@1(Q)		;THE
	JUMPGE	A,OUT		;CORRECT
	DFN	A,B		;SIGN.
OUT:	JRA	Q,2(Q)		;EXIT
	END

