TITLE DFM.. DOUBLE PRECISION FLOATING MULTIPLY- TOM EGGERS/DMN
SUBTTL	FOR DIRT/DOUBT ONLY	28-APR-71
;FROM DFMP	V.020 23-APR-69
;USES ACC 15 INSTEAD OF ACC 16

;REENT==1 GIVES RE-ENTRANT FORTRAN OP-SYSTEM
IFNDEF REENT,<REENT==0>
IFN REENT,<	HISEG	>

Q=15	;POINTS TO MEMORY ARG
M=Q	;M IS MORE MNEMONIC
P=17	;PUSH DOWN POINTER

ENTRY	DFM..

DFM..:	PUSH	P,2		;SAVE 2
	PUSH	P,(M)		;GET HIGH PART OF MEM ARG OUT OF WAY OF 2
	MOVE	2,0		;COPY HIGH PART OF AC ARG
	FMPR	2,1(M)		;FORM 1 CROSS PRODUCT
	JFCL			;IN CASE OF UNDERFLOW
	FMPR	1,(P)		;FORM OTHER CROSS PRODUCT
	JFCL			;IN CASE OF UNDERFLOW
	UFA	1,2		;ADD CROSS PRODUCTS
	JFCL			;OVERFLOW ONLY IF NEXT INSTRUCTION ALSO
	FMPL	0,(P)		;FORM PRODUCT OF HIGH ORDER PARTS
	JOV	%OV		;FIX AND XFER INCASE OF OVER/UNDERFLOW
	UFA	1,2		;ADD XROSS PRODUCT SUM INTO LOW WORD
	FADL	0,2		;ADD HIGH AND LOW WORDS
%OV:	POP	P,2		;REMOVE HIGH MEM ARG FROM LIST
	POP	P,2		;RESTORE 2

	POPJ P,

	END

