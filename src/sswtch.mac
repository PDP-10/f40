	TITLE	SSWTCH  V.005


; DATA SWITCH TESTING PROGRAM
;THIS PROGRAM IS CALLED IN THE FOLLOWING MANNER:
;	JSA	Q, SSWTCH
;	EXP	I
;	EXP	J
;I IS THE ADDRESS OF AN INTEGER ARGUMENT AND J IS THE ADDRESS
; OF THE ANSWER . IF DATA SWITCH I IS UP,THE ANSWER IS 2 , AND
; IF IT IS DOWN, THE ANSWER IS 1.

	ENTRY	SSWTCH

	A=	0
	B=	1
	Q=	16

SSWTCH:	0			;ENTRY TO SSWTCH PROGRAM
	MOVEM	A, ASAVE	;SAVE ACCUMULATOR A
	MOVEM	B, BSAVE	;SAVE ACCUMULATOR B
	MOVN	B, @(Q)		;PICK UP ARGUMENT
	MOVSI	A, 400000	;PUT A ONE IN BIT 0
	ROT	A,(B)	; ROTATE BIT INTO POSITION
	MOVEI	B,2		; SET ANSWER TO 2 FOR NOW
	MOVEM	B, @1(Q)	;...
	CALL	B, SWITCH	;GET DATA SWITCHES FROM MONITOR
	TDNN	B, A		;IS PROPER SWITCH ON?
	SOS	@1(Q)	; NO, CHANGE ANSWER TO ONE
	MOVE	A, ASAVE	;RESTORE ACCUMULATOR A
	MOVE	B, BSAVE	;RESTORE ACCUMULATOR B
	JRA	Q, 2(Q)		;EXIT

SWITCH:	SIXBIT	/SWITCH/
ASAVE:	0
BSAVE:	0

	END

