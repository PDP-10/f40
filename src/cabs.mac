TITLE CABS V.021
SUBTTL	23-SEPTEMBER-1969	TOM EGGERS/KK

;COMPLEX ABSOLUTE VALUE FUNCTION
;THIS ROUTINE CALCULATES THE ABSOLUTE VALUE OF A COMPLEX NUMBER
;Z = X +I*Y
;THE CALLING SEQUENCE IS AS FOLLOWS:
;	JSA Q,CABS
;	EXP ARG
;THE ANSWER IS LEFT IN ACCUMULATOR "A"

;CALCULATE SQRT(Y*Y+X*X) BX USING:
;	Y*SQRT[(X/Y)**2+1] WITH X<Y
;IF (X/Y)**2 UNDERFLOWS, THEN RETURN Y


EXTERNAL SQRT
ENTRY CABS

Q=16
A=0
B=1

MLON	;ALLOW MULTI LINE LITERALS

	SIXBIT/CABS/
CABS:	0
	MOVEI B,@(Q)		;GET ADR OF COMPLEX ARG
	MOVM A,(B)		;GET MAGNITUDE OF REAL PART
	MOVM B,1(B)		;GET MAGNITUDE OF IMAGINARY PART
	CAML A,B		;GET SMALLER ARG IN A AND
	EXCH A,B		; LARGER IN B
	FDVR A,B		;X/Y
	JFCL OUT		;IF UNDERFLOW, GO TO OUT VIA OVTRAP
	FMPR A,A		;(X/Y)**2
	JFCL OUT		;IF UNDERFLOW, GO TO OUT VIA OVTRAP
	FADRI A,201400		;(X/Y)**2+1.0
	MOVEM	A,T1		;STORE ARG. IN NE 0 OR 1.
	MOVEM	B,T2		;SAVE LARGER.
	JSA Q,SQRT		;TAKE SQUARE ROOT
	EXP	T1		;ARG. IS IN T1.
	FMPR A,T2		;Y*SQRT[(X/Y)**2+1.0]
RET:	JRA Q,1(Q)		;RETURN
OUT:	MOVE	A,B		;Y IS THE ANSWER
	JRA	Q,1(Q)		;RETURN

T1:	0
T2:	0
	END

