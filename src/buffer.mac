TITLE	BUFFER	IBUFF-OBUFF V30.101
SUBTTL	10-MAR-71	/DMN

ENTRY	BUFFER,IBUFF,OBUFF
EXTERN	SAVCK.,SETBFS
EXTERN	INTVR.,DATTB.,UNSV.,EXER1.

	;ARGUMENT PARAMETERS DEFINED
INARG==1		; IN ONLY
OUTARG==2	; OUT ONLY

	;AC'S
A=1
B=2
C=3
D=4
E=5
Q=16
P=17

; SAVE AC'S, INITIALIZE, CHECK ARGS, SAVE INFORMATION

BUFFER:	0
	PUSHJ	P,SAVCK.	;INITIALIZE
	PUSHJ	P,INTVR.	;PICK UP DEV NAME IN E
				;MAKE SURE DEVICE NOT INITED.
	MOVE	B,@1(Q)		;DEV NUM IN A, GET DIR
	CAIN	B,INARG		;COMPARE
	JRST	GETNUM		;ARGUMENT
	CAIN	B,OUTARG	;TO VERIFY
	JRST	GETNUM		;ARGUMENT
	CAIE	B,INARG+OUTARG	;REASONABLE
	PUSHJ	P,EXER1.	;IF UNREASONABLE,FAIL!
GETNUM:	MOVE	C,@2(Q)		;INOUT ARG IN B, GET NUM
	JUMPLE	C,ILLNUM	;FROM ONE
	CAIL	C,20		;TO FIFTEEN
ILLNUM:	PUSHJ	P,EXER1.	;BUFFS ALLOWED

; (0),(A)=DEV NUM, (B)=DIR, (C)=NUM



; NOW SET THE APPRPORAITE FIELDS IN THE DATTB. ENTRY

	TRNE	B,INARG		;IN SET?
	DPB	C,[POINT 4,DATTB.(A),31]
	TRNE	B,OUTARG	;OUT SET ?
	DPB	C,[POINT 4,DATTB.(A),35]
	PUSHJ	P,UNSV.		;RESTORE
	JRA	Q,3(Q)		;RETURN



;THESE TWO ENTRY POINTS ARE FOR THE BUFFER SIZE ROUTINES

IBUFF:	0
	LDB	A,[POINT 6,@(Q),35]  ;TRUNCATE IF DEVICE #>63
	MOVE	0,@1(Q)		;GET NUMBER BUFFERS DESIRED
	DPB	0,[POINT 4,DATTB.(A),31]	;TRUNCATE # BUFS TO 15 IF NEEDED
BUFR:	MOVE	0,@2(Q)		;GET SIZE
	HRLM	0,DATTB.(A)	;STOR IT
	JRA	16,3(16)	;RETURN

OBUFF:	0
	LDB	A,[POINT 6,@(Q),35]  ;DEVICE #
	MOVE	0,@1(Q)		;GET # BUFFERS DESIRED
	DPB	0,[POINT 4,DATTB.(A),35]  ;STORE IT
	JRST	BUFR

	END
