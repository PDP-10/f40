TITLE DIRT.  V.30.0.140   DOUBLE PRECISION INPUT ROUTINE PDP-10
SUBTTL	V30.0.140	28-APR-71	/DMN
;FROM	V30.0.46 7-JAN-71 FROM 30.0.42 /VAA
;V30.0.42 16-DEC-70 FROM V30 /VAA
;V30  16-NOV-70	/DMN
;FROM	V.027	9-JUL-70	/DMN
;FROM 	9-JUL-70	/DMN	FROM V.020	22 APRIL 1969	/TWE

;REENT==1 GIVES RE-ENTRANT FORTRAN OP SYSTEM
IFNDEF REENT,<REENT==0>
IFN REENT,<	HISEG	>

;DIRT IS CALLED BY:
;	PUSH	P,FMTWRD
;	PUSHJ	P,DIRT.
;	ILLEGAL CHARACTER RETURN
;	NORMAL RETURN
;THE DOUBLE PRECISION ANSWER IS RETURNED IN AC0 AND AC1.

;THE FORMAT WORD (FMTWRD) IS CONSTRUCTED AS FOLLOWS:
;	BITS 00-03 IGNORED
;	BITS 04-10 FORMAT SPECIFIED D
;	BITS 11-17 FORMAT SPECIFIED W
;	BITS 18-35 FORMAT SPECIFIED SCALE FACTOR
;IF W=0, THEN INPUT IS IN DELIMITER MODE

;THE CHARACTER LETTER O IS TREATED AS THE CHARACTER NUMBER 0

	ENTRY	DIRT.
	EXTERN	CHINN.,IIB.,ILLEG.,DELIM.,OVFLS.,TTYFL.
	EXTERN	DFM..,TAB.P1,TAB.M1

P=17
N=16
Q==15
X==4
D=15
AC=13	;AC+1 IS ALSO USED
T=11	;T+1 IS ALSO USED
W=10
WGS=7	;AC USED BY BILL SEGAL AND NOT SAVED AND RESTORED BY DIRT
F=6
DIG=5
B=2
XT=1
ANS=0
CH=0
ACH==15
ACL==4
NAC=ACH-ACL
IFN REENT,<LOW=16>
IFE REENT,<LOW=0>

ABLANK==40
APLUS==53
AMINUS==55
APER==56
A0==60
A9==71
AD==104
AE==105
AO==117

;FLAGS IN LEFT HALF OF AC F
FLDIG==1		;ON- DIGIT SEEN
FLPER==2		;ON- PERIOD SEEN
FLIGN==4		;ON- IGNORE SUBSEQUENT DIGITS
FLMSGN==10	;ON- MANTISSA IS NEGATIVE
FLXSGN==20	;ON- EXPONENT IS NEGATIVE
FLSGN==40	;ON- PLUS OR MINUS SEEN
DIRT.:	PUSH	P,ACL		;SAVE LOWEST AC
	MOVEI	ACL,1(P)	;SAVE ACL+1 THRU ACH INCLUSIVE
	HRLI	ACL,ACL+1	;...
	ADD	P,[XWD NAC,NAC]	;...
	BLT	ACL,(P)		;...
	MOVE	F,-2-NAC(P)	;GET FORMAT WORD
	SETZB	ANS,ANS+1	;INITIAL ANSWER OF ZERO
	LDB	W,[POINT 7,F,17];GET W= CHARACTER COUNT
	TLZ	F,740177	;SET ALL FLAGS TO ZERO
	SETZB	AC,AC+1		;CHAR BY CHAR ASSEMBLY
	JUMPN	W,.+2		;W=0?
	HRROI	W,-2		;YES, DELIMITER MODE INPUT
	SETZB	DIG,X		;DIGIT COUNTER, EXPONENT INDICATOR
	MOVEI	D,276		;PRESUMED EXPONENT
	AOJA	W,MMORE1	;MAKE W ENDCHECKABLE
MMORE:	SKIPN	OVFLS.(LOW)	;SKIP ON OVERFLOW
	XCT	IIB.		;LAST CHAR ACCEPTABLE
MMORE1:	PUSHJ	P,EATCH		;EAT ANOTHER CHAR
	JRST	MSPCHI		;SP CHAR BEFORE DIGIT
	JRST	MSPCHA		;SP CHAR AFTER DIGIT
	JRST	OVTZER		;W EXPIRED BEFORE DIGIT
	JRST	SPLASH		;W EXPIRED AFTER DIGIT

MSPCHI:	TLOE	F,FLSGN		;HAS A SIGN BEEN SEEN?
	JRST	ILC		;YES, COMPLAIN
	CAIN	CH,APLUS	;ASCII PLUS?
	JRST	MMORE		;IGNORE IT
	CAIN	CH,AMINUS	;ASCII MINUS?
	TLOE	F,FLMSGN	;SET SIGN INDICATOR
	JRST	ILC		;NOT +OR-
	JRST	MMORE		;EAT MORE MANTISSA

MSPCHA:	CAIE	CH,AD		;ASCII D OR E?
	CAIN	CH,AE		;...
	JRST	LFEL		;YES, LOOK FOR EXPONENT, LETS
	TLO	F,FLSGN		;INDICATE SIGN SEEN
	CAIN	CH,APLUS	;ASCII PLUS?
	JRST	LFE		;YES, LOOK FOR EXPONENT
	CAIE	CH,AMINUS	;ASCII MINUS?
	JRST	ILC		;NOPE, ILLEGAL CHAR
	TLOA	F,FLXSGN	;YES, INDICATE SIGN AND LFE
LFEL:	TLZ	F,FLSGN		;INDICATE SIGN NOT SEEN
LFE:	TLZ	F,FLIGN!FLDIG	;RESET EATCH FLAGS
	MOVEI	X,1		;INDICATE EXPONENT
	MOVEI	T+1,0		;INITIAL EXPONENT = 0
XMORE:	PUSHJ	P,GETCH		;GET THE NEXT CHAR
	JRST	XSPCHI		;SP CHAR BEFORE DIGIT
	JRST	XSPCHA		;SP CHAR AFTER DIGIT
	JFCL			;TO BE CONTINUED
SPLASH:	LDB	T,[POINT 7,F,10];GET D
	TLNN	F,FLPER		;EXPLICIT PERIOD?
	SUBI	DIG,(T)		;NO, USE IMPLICIT PERIOD OF D
	JUMPN	X,EXP		;WAS EXPONENT SEEN?
	HRRZ	T,F		;NO, USE SCALE FACTOR
	SUBI	DIG,(T)		;...
NORMAL:	JUMPN	AC,GOOF		;V006 HI MANTISSA =0?
	JUMPE	AC+1,SIGN	;V006 YES, REST=0?
GOOF:	TLNE	AC,400		;V006 N0, BIT 9=1?
	JRST	SIGN		;V006 YES, DONE
	ASHC	AC,1		;V006 LEFT MARCH
	SOJA	D,GOOF		;V006 MINUS ONE THE EXP

SIGN:	MOVE	ANS,AC		;MOVE HI ORDER FRACTION INTO ANS
	DPB	D,[POINT 9,ANS,8];DEPOSIT EXP. IN HI ORDER WORD
	SUBI	D,^D27		;TO OBTAIN LOW ORDER EXP.
	MOVE	B,AC+1		;GET LOW ORDER FRACTION IN B
	LSH	B,-10		;MAKE ROOM FOR LO ORDER EXP.
	DPB	D,[POINT 9,B,8]	;DEPOSIT EXP. IN LOW ORDER WORD
	FADL	ANS,B		;NORMALIZE RESULTS
	TLNE	F,FLMSGN	;SHOULD ANS BE NEGATIVE?
	DFN	ANS,ANS+1	;YES,NEGATE RESULTS
	JUMPE	DIG,OVT		;DEC EXP OF 0, NO MULT NEEDED
	JUMPG	DIG,POSMUL	;DEC EXP IS +
NEGMUL:	MOVNS	DIG		;DEC EXP IS -, MAKE IT +
	MOVEI	F,TAB.M1	;BASE ADR FOR NEG EXP TABLE
	JRST	.+2		;GO STORE IT
POSMUL:	MOVEI	F,TAB.P1	;BASE ADR FOR POS EXP TABLE
	TRNE	DIG,777700	;IS DEC EXP TOO BIG?
	JRST	OVTZER		;YES, RETURN ZERO
	TRNE	DIG,^D32		;E+-32 ?
	CAIE	F,TAB.M1	;E-32 ?
	JRST	MULOOP		;NO, ALL IS WELL
	TROE	DIG,^D16		;TRY *E-16
	JRST	OVTZER		;OUT OF RANGE IF E-48
	MOVEI	Q,TAB.M1+10	;ADDRESS OF 1.0E-16
	TRZ	DIG,40		;REMOVE ALL TRACES OF E-32
	PUSHJ	P,DFM..		;E-32=2*E-16
MULOOP:	MOVEI	Q,(F)		;TABLE PICKUP
	TRZE	DIG,1		;MULTIPLY THIS TIME?
	PUSHJ	P,DFM..		;YES, GO EAT TIME
	JUMPE	DIG,OVT		;JUMP IF NO MORE MULTIPLY
	ASH	DIG,-1		;LOOK AT NEXT BIT
	ADDI	F,2		;NEXT ENTRY IN TABLE
	JRST	MULOOP		;LOOK FOR NEXT MULTIPLY

XSPCHI:	TLOE	F,FLSGN		;HAS A SIGN BEEN ENCOUNTERED?
	JRST	ILC		;YES, ADDITIONAL SIGNS ILLEGAL
	CAIN	CH,APLUS	;ASCII PLUS?
	JRST	XMORE		;IGNORE
	CAIN	CH,AMINUS	;ASCII MINUS?
	TLOE	F,FLXSGN	;INDICATE FIRST AND ONLY MINUS
	JRST	ILC		;NOT SIGN
	JRST	XMORE		;GET MORE EXPONENT

XSPCHA:	JUMPGE	W,ILC		;ILLEGAL IF NOT DELIMITER MODE
	MOVEM	CH, DELIM.(LOW)	;SAVE TERMINATING CHAR FOR NAMELIST
	CAIE	CH,APLUS	;ASCII PLUS ?
	CAIN	CH,AMINUS	;ASCII MINUS?
	JRST	ADDCNT		;YES, DON'T ADVANCE POINTER.
	SKIPN	OVFLS.(LOW)
	XCT	IIB.		;NO, ADVANCE POINTER FOR BREAK CHAR
	JRST	SPLASH		;PROCESS PREVIOUS INPUT

EXP:	TLNE	F,FLXSGN	;SHOULD EXP BE NEGATIVE?
	MOVNS	T+1		;YES, MAKE IT SO
	ADD	DIG,T+1		;COMBINE W/ DIGIT COUNT
	JRST	NORMAL		;COMBINE W/ MANTISSA

ILC:	SKIPN	OVFLS.(LOW)
	XCT	IIB.		;INCREMENT POINTER
	MOVEM	CH, DELIM.(LOW)	;SAVE DELIMITER FOR NAMELIST
	JUMPL	W,SPLASH	;DELIMITER MODE
	JUMPE	W,ILLCK		;W EXPIRED ALREADY
ILC1:	PUSHJ	P,CHINN.	;IGNORE W CHARACTERS
	SKIPN	OVFLS.(LOW)
	XCT	IIB.		;...
	SOJG	W,ILC1		;...
ILLCK:	SKIPN	ILLEG.(LOW)	;IGNORE ILLEGALITY OF CHARS?
	JRST	OVTILC		;NO, RETURN W? ERROR RETURN
OVTZER:	SETZB	ANS,ANS+1	;RETURN ZERO ANSWER
OVT:	AOS	-1-NAC(P)	;NORMAL RETURN 2,4
OVTILC:	MOVEM	WGS,WGS-ACH(P)	;KEEP BILLS AC AS IS
	SUB	P,[XWD NAC,NAC]	;RESTORE ACL THRU ACH
	MOVEI	ACL,ACL+1	;...
	HRLI	ACL,1(P)	;...
	BLT	ACL,ACH		;...
	POP	P,ACL		;...
	POPJ	P,		;RETURN

ADDCNT:	AOS	2(WGS)		;ADD ONE TO THE ITEM COUNT
	JRST	SPLASH		;PROCESS NUMBER

GETCH:	SKIPN	OVFLS.(LOW)
	XCT	IIB.		;ACCEPT LAST CHARACTER
EATCH:	SOJN	W,MORECH	;W NOT EXPIRED, GET MORE CHAR
	MOVEI	T,2		;W EXPIRED, RETURN 2 LATER
	ADDM	T,(P)		;...
	POPJ	P,		;RETURN 3,4 OR 4,4

MORECH:	PUSHJ	P,CHINN.	;GET A CHAR IN CH
	CAIN	CH,ABLANK	;ASCII BLANK?
	JRST	BLNKIN		;YES, PROCESS IT
	CAIN	CH,15		;CCO 30-46 CHECK FOR <CR>
	JRST	CRIN		;CCO 30-46 YES <CR> RETURN
	CAIN	CH,APER		;ASCII PERIOD?
	JRST	PERIN		;YES, PROCESS IT
	CAIN	CH,AO		;ASCII LETTER O?
	MOVEI	CH,A0		;YOU MEANT ASCII NUMBER 0
	CAIL	CH,A0		;DIGIT?
	CAILE	CH,A9		;...
	POPJ	P,		;NO, RETURN 1,4 OR 2,4
BLNK0:	TLNE	F,FLIGN		;IGNORE FLAG ON?
	JRST	GETCH		;YES, IGNORE CHAR
	TLON	F,FLDIG		;DIGIT SEEN?
	AOS	(P)		;NO, FIRST DIGIT
	SUBI	CH,A0		;CAN'T DO INDEXING ON AC 0
	XCT	PJ(X)		;GO CONVERT DIGIT
	JRST	GETCH		;GET ANOTHER CHAR

BLNKIN:	TLNN	F,FLDIG		;DIGIT SEEN?
	JRST	GETCH		;NO, IGNORE BLANK
	JRST	CRIN+2		;BYPASS TEST FOR TTY INPUT
CRIN:	TLNE	N,TTYFL.	;IS INPUT FROM A TTY?
	SETO	W,		;YES, AS IF IN DELIMITER MODE
	JUMPL	W,CPOPJ		;DELIMITER MODE, BLANK DELIMITS
	MOVEI	CH,A0		;OTHERWISE, BLANK ACTS AS 0
	JRST	BLNK0		;...

PERIN:	XCT	TLO(X)		;FIRST PERIOD SEEN?
	JRST	GETCH		;YES, GET ANOTHER CHAR
CPOPJ:	POPJ	P,		;ILC

;THE INSTRUCTION PAIRS BELOW CONSIST OF:
;	AN INSTRUCTION EXECUTED WHILE COMPILING THE MANTISSA
;	AN INSTRUCTION EXECUTED WHILE COMPILED THE EXPONENT

TLO:	TLON	F,FLPER		;IS THIS FIRST PERIOD?
	POPJ	P,		;PERIOD ILLEGAL IN EXP

PJ:	PUSHJ	P,MADD		;MANTISSA ADD IN CHAR
	PUSHJ	P,XADD		;EXPONENT ADD IN CHAR

MADD:	TLNE	F,FLPER		;PERIOD SEEN?
	SUBI	DIG,1		;YES, COUNT DIGIT AFTER PERIOD
	PUSH	P,AC		;SAVE AC
	MOVE	T,AC+1		;...
	MULI	T,^D10		;MUL LOW HALF BY 10
	IMULI	AC,^D10		;MUL HIGH HALF BY 10.
	EXCH	T+1,AC+1	;PUT NEW LOW HALF IN AC+1,OLD IN T+1
	ADD	AC,T		;ADD HIGH HALF OF PROD. INTO HIGH*10.
	TLO	AC+1,(1B0)	;FLAG LOW ORDER WORD
	ADD	AC+1,CH		;(AC,AC+1)*10.+NEW DIGIT
	TLZN	AC+1,(1B0)	;HAS SIGN CHANGED?
	ADDI	AC,1		;YES,INCREMENT HIGH ORDER WORD
	POP	P,T		;SAVE OLD NUM IN T,T+1 IN CASE AC,AC+1 NEED RESTORING

	TLNN	AC,777000	;AC TOO BIG?
	POPJ	P,		;NO, RETURN
	TLO	F,FLIGN		;YES, IGNORE FURTHER DIGITS
	TLNE	F,FLPER		;PERIOD SEEN?
	ADDI	DIG,1		;YES, UNDO ADD TO COUNTER
	MOVE	AC,T		;AC=Y
	MOVE	AC+1,T+1	;...
	CAIGE	CH,5		;ATTEMPT CARRY?
	POPJ	P,		;NO, RETURN
	AOJN	AC+1,CPOPJ	;CARRY SUCCESSFUL
	ADDI	AC,1		;KEEP TRYING
	TLNN	AC,777000	;OVERFLO?
	POPJ	P,		;NO, SUCCESSFUL CARRY
	ASHC	AC,-1		;V7**AT ANY COST
	AOJA	D,CPOPJ		;NOTIFY EXP

XADD:	MOVE	XT,T+1		;SAVE JUST IN CASE
	MOVE	T,T+1		;MULTIPLY BY 10.
	MULI	T,^D10		;...
	JUMPN	T,XLOSE		;NO HI ORDER ALLOWED
	ADD	T+1,CH		;...
	POPJ	P,		;STILL W/IN BOUNDS

XLOSE:	TLO	F,FLIGN		;NO MORE DIGITS
	MOVE	T+1,XT		;RESTORE LAST EXP
	CAIGE	CH,5		;CARRY A GOODNESS?
	POPJ	P,		;NO, RETURN
	AOJN	T+1,CPOPJ	;CARRY IMPOSSIBLE?
	MOVE	T+1,XT		;YES, GIVE UP
	POPJ	P,		;...

	END

