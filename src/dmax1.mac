TITLE	DMAX1  V.020	DOUBLE PRECISION MAXIMUM VALUE FUNCTION
SUBTTL	3 MAY 1969	/TWE
		;FROM V.005,  21 JULY 1966

;THIS ROUTINE IS CALLED IN THE FOLLOWING MANNER
;	JSA	Q, DMAX1
;	EXP	ARG1
;	EXP	ARG2
;	.
;	.
;	.
;WHERE ARG1,ARG2,...ARE THE ADDRESSES OF THE HIGH ORDER
;PORTIONS OF DOUBLE PRECISION ARGUMENTS. THE MAXIMUM OF THE
;ENTIRE SET IS RETURNED AS A DOUBLE PRECISION NUMBER IN
;ACCUMULATOR A AND B.

	ENTRY	DMAX1

	A=	0
	B=	1
	C=	14
	Q=	16

	SIXBIT /DMAX1/
DMAX1:	0			;ENTRY TO DMAX1 ROUTINE
	MOVEI	B, @(Q)		;GET ADDRESS OF FIRST ARGUMENT
	MOVE	A, (B)		;GET HIGH ORDER PART OF ARG
	MOVE	B, 1(B)		;GET LOW ORDER PART OF ARG
	MOVEM	C,CSAVE		;SAVE AC C
	AOJA	Q, DMAX.2	;ADDRESS OF NEXT, START CHECKING

DMAX.1:	CAMLE	A, (C)		;IS HIGH ORDER > THAN THIS ONE?
	JRST	DMAX.2		;YES, GET ADDRESS OF NEXT IN LIST
	CAME	A, (C)		;ARE HIGH ORDER WORDS EQUAL?
	JRST	DMAX.3		;NO, REPLACEMENT NECESSARY
	CAML	B, 1(C)		;YES, CHECK LOW ORDER WORDS
	JRST	DMAX.2		;OK, GET ADDRESS OF NEXT ONE
DMAX.3:	MOVE	A, (C)		;PICK UP HI ORDER OF NEW ARG
	MOVE	B, 1(C)		;PICK UP LO ORDER OF NEW ARG
DMAX.2:	MOVE	C, (Q)		;GET ADDRESS OF NEXT ARGUMENT
	TLC	C,(<JUMP>)	;COMPLEMENT OP-CODE "JUMP" BITS
	TLNN	C,777000	;IS THE ARG "JUMP" ?
	AOJA	Q,DMAX.1	;YES,INCREMENT AND CHECK FURTHER
	MOVE	C,CSAVE		;NO, RESTORE AC C AND EXIT
	JRA	Q,(Q)

CSAVE:	BLOCK	1	;TEMP FOR AC C

	END

