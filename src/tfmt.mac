TITLE TFMT.  V.030.25    T FORMAT FORTRAN IV
SUBTTL	6-DEC-70	/DMN
;FROM	V.005	9-SEP-66

;REENT==1 GIVES RE-ENTRANT FORTRAN OP SYSTEM
IFNDEF REENT,<REENT==0>
IFN REENT,<	HISEG	>

;THIS SUBROUTINE INTERPRETS THE "T" FORMAT CONTROL
;IN FORTRAN IV

;THE SPACING COUNT IS CONTAINED IN AC A UPON ENTERING THE 
;ROUTINE.

;THE ROUTINE IS CALLED BY "FORSE."  .

ENTRY	TFMT.
EXTERN	XIO.,EOL.,RIN.,TPNTR.,TCNT2.,TCNT1.

;ACCUMULATORS
	A=1			;UTILITY
	B=2			;UTILITY
	C=3			;UTILITY
	D=4			;UTILITY
	M=7			;ADDR. OF BUFFER HEADER(RH)
	I=11			;FLAG REGISTER
	P=17			;PUSHDOWN POINTER
IFN REENT,<LOW=16>
IFE REENT,<LOW=0>

TFMT.:	POP	P,0		;DUMMY POP
	MOVEI	0,-1(A)		;GET COUNT
	TLNE	I,400000	;OUTPUT?
	SUBI	A,1		;YES, ACCOUNT FOR CARRIAGE CONTROL CHAR.
	IDIVI	A,5		;CALCULATE POINTER FOR T SETTING
	HRRZ	C,(M)		;BEGINNING OF BUFFER
	ADDI	C,1		;PLUS ONE
	HRLI	C,700		;BYTE SIZE
	ADD	C,A		;POINTER ADDRESS
	JUMPE	B,NOREM		;LOOK AT REMAINDER FOR INCREMENT
	IBP	C		;INCREMENT POINTER
	SOJG	B,.-1		;  UNTIL CORRECT
NOREM:	SKIPGE	EOL.(LOW)	;NEW LINE?
	JRST	NOTT		;YES, MUST BE FORWARDS
	MOVS	D,1(M)		;COMPARE WITH CURRENT POINTER
	TRC	D,-1		;BYTE POINTER COUNT BACKWARDS
	MOVS	B,C		;T POINTER
	TRC	B,-1		;CONVERT POINTER TO VALUE
	CAMG	B,D		;COMPARE POINTERS
	JRST	BACKUP		;BACK UP IN BUFFER
	TLZN	I,4		;HAS THERE ALREADY BEEN A T?
	JRST	NOTT		;NO
	MOVE	C,TPNTR.(LOW)	;YES,PICK UP PREVIOUS POINTER
	MOVEM	C,1(M)
	MOVE	A,TCNT2.(LOW)	;PICK UP PREVIOUS ITEM COUNT
	MOVEM	A,2(M)
	SOS	A,0		;ACCOUNT FOR EOL STARTING AT 0
	SUB	A,TCNT1.(LOW)	;SUBTRACT PREVIOUS COLUMN NO.
	JRST	XIO.		;GO TO X FORMAT WITH COUNT IN A

NOTT:	SOS	A,0		;ACCOUNT FOR EOL STARTING AT 0
	SUB	A,EOL.(LOW)	;SUBTRACT COLUMN NO.
	JRST	XIO.		;GO TO X FORMAT WITH COUNT IN A
BACKUP:	EXCH	C,1(M)		;NEW BACKUP POINTER IN 1(M)
	MOVEM	C,TPNTR.(LOW)	;PRESERVE PRESENT POINTER
	MOVE	B,2(M)		;PRESERVE ITEM COUNT
	MOVEM	B,TCNT2.(LOW)
	MOVE	B,EOL.(LOW)	;PRESERVE COLUMN COUNT
	MOVEM	B,TCNT1.(LOW)
	SUB	B,0		;COLUMN LESS T SETTING-1
	ADD	B,2(M)		;PLUS ITEM COUNT
	ADDI	B,1
	MOVEM	B,2(M)		;BACK UP ITEM COUNT
	SUBI	0,1		;T SETTING -2
	MOVEM	0,EOL.(LOW)	;BACKUP EOL
	TLO	I,4		;SET T FORMAT FLAG
	JRST	RIN.		;RETURN


	END

