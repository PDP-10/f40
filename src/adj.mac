	TITLE	ADJ.  V.006
	SUBTTL	29 JAN 68	TOM OSTEN

;ADJ. IS A PROGRAM CALLED AT RUN-TIME BY A FORTRAN PROGRAM
;TO CALCULATE THE MULTIPLIERS AND OFFSET FOR SUBSCRIPT CALCULATIONS
;FOR DIMENSIONS DECLARED AS SUBROUTINE ARGUMENTS. THE COMPILER
;GENERATES THE FOLLOWING SEQUENCE:
;	JSA	16, ADJ.
;	EXP	N		;DIMENSIONALITY OF ARRAY
;	ARG	X, TEMP+N+1	;ARG IS A NO-OP, X IS THE TYPE 
				;OF THE ARGUMENT,TEMP IS A PNTR
				;TYPE,TEMP+N+1 POINTS TO END OF
				;MULTIPLIER TABLE
;	EXP	U1		;ADDRESS OF NUMBER WHICH IS THE 
;				;UPPER BOUND FOR FIRST SUBSCRIPT
;	EXP	L1		;ADDRESS OF NUMBER WHICH IS THE
;				;LOWER BOUND FOR FIRST SUBSCRIPT
;	.
;	.
;	.
;	EXP	LN		;LAST LOWER BOUND ADDRESS
;THE TEMP BLOCK IS CONSTRUCTED AS FOLLOWS:

;TEMP:	SIZE OF ARRAY (EQUAL TO MULTIPLIER N)
;	OFFSET
;	MULTIPLIER N-1
;	.
;	.
;	.
;	MULTIPLIER 1
;	MULTIPLIER 0

;THE I-TH MULTIPLIER, P(I), IS DESCRIBED BY:
;	P(0) = 1
;	P(I) = P(I-1) * (U(I) - L(I) + 1)

;THE OFFSET IS DESCRIBED BY
;	OFFSET = SUM FROM 1 TO N OF P(I-1)*L(I)

	ENTRY	ADJ.

	A=	1
	B=	2
	C=	3
	D=	0
	E=	4

ADJ.:	0			;ENTRY TO ADJ. ROUTINE
	MOVE	E,16		;SAVE TYPE ARG PNTR
	MOVNI	A, @(16)	;GET MINUS COUNT OF DIMENSIONS
	MOVEI	B, @1(16)	;GET TOP ADDRESS OF TEMP BLOCK
	ADDI	B, -1(A)	;SET B BACK TO BEGINNING OF TEMP BLOCK
	HRL	B, A		;AOBJN WORD IS (-CNT)ADDR
	MOVEI	C, 1		;INITIALIZE P(0) = 1
	MOVEI	D, 0		;INITIALIZE OFFSET=0

ADJ.1:	MOVEM	C, (B)		;STORE P(N)
	ADDI	16, 2		;SET FOR NEXT PAIR OF DIMENSIONS
	MOVE	A, C		;COPY P(N)
	IMUL	A, @1(16)	;P(N-1)*L(N)
	ADD	D, A		;ADD INTO OFFSET
	MOVE	A, @(16)	;GET U(N)
	SUB	A, @1(16)	;U(N) - L(N)
	IMULI	C, 1(A)		;P(N-1)*(U(N) -L(N) +1)
	AOBJN	B, ADJ.1	;N=N+1, GO AROUND LOOP

	LDB	A, [POINT 3,1(E),11] ;GET HIGH ORDER AC BITS
	CAIN	A, 3		;WAS THE AC FIELD 6 OR 7?
	ASH	D, 1		;YES, MULTIPLY OFFSET BY 2 FOR
				;COMPLEX OR DOUBLE PRECISION ARG.
	MOVEM	D, (B)		;OFFSET TO NEXT TO LAST ENTRY
	MOVEM	C, 1(B)		;SIZE TO LAST ENTRY
	JRA	16, 2(16)	;RETURN

	END

