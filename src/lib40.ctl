;JOB%1(17) TO MAKE FORTRAN OPERATING SYSTEM AND LIBRARY
;SUBMIT WITH COMMAND  .QUEUE I:=LIB40/RESTART:1/PAGES:999
;
;
.GOTO OPREND	;SKIP OVER OLD OPR FILE

;
;
1.0	INTRODUCTION

	THIS DOCUMENT DESCRIBES THE SEQUENCE OF OPERATIONS THAT MUST
	BE DONE TO CREATE
			1).	 THE FORTRAN LIBRARY (LIB40.REL)
		OR	1A).	ALTERNATIVE FORTRAN LIBRARY (LIB40.REL)
			2).	THE REENTRANT OPERATING SYSTEM
				(FORTR.SHR)
			3).	THE IMPURE USER DATA AREA ASSOCIATED WITH
				THE SHAREABLE OPERATING SYSTEM (FORTR.SHR)
				THIS DATA AREA IS CALLED IMP40.REL

2.0	METHOD

	TO CREATE LIB40.REL
	-----------------
	LIB40.REL CONTAINS ALL THE FILES IN THE FORTRAN LIBRARY
	INCLUDING ALL THE MATH ROUTINES AND ALL THE SUBROUTINES
	NECESSARY TO SUPPORT FORTRAN I/O OPERATIONS. THE FORTRAN
	OPERATING SYSTEM IS ALSO PART OF LIB40.REL . IT IS
	CALLED FORSE AND IS A NON-REENTRANT, NON-SHAREABLE PROGRAM
	THE OPERATIONS NECESSARY TO CREATE LIB40.REL ARE AS FOLLOWS:
	LIB40.REL IS CREATED BY USING THE INDIRECT COMMAND FILE NAMES.CMD
	WHICH SPECIFIES ALL THE SOURCE FILES NECESSARY TO BUILD THE SYSTEM
	LIBRARY


	;DELETE OR RENAME ALL REL FILES ON THE DISK AREA
.DELETE *.REL
	;COMPILE ALL SOURCE FILES FOR LIB40.REL FROM
	;INDIRECT COMMAND FILE NAMES.CMD
.COMPILE @NAMES.CMD
	;USE PIP TO MAKE LIB40.REL
.R PIP
*DSK:LIB40.REL_*.REL
*^C
	;THE FOLLOWING STEP IS OPTIONAL
	;IT WAS NOT DONE ON THE RELEASED VERSION
	;RETURN TO MONITOR (^C) TO RUN FUDGE2
	;TO WRITE INDEX BLOCKS FOR FAST LOADING
.R FUDGE2
*LIB40.LIB_LIB40.REL/X$<ALTMODE>
*^C
.;LIB40.LIB MAY BE RENAMED TO LIB40.REL NOW IF THE FOLLOWING OPERATIONS
	;ARE NOT TO BE DONE, OTHERWISE IS SHOULD BE RENAMED AFTER
	; FOLLOWING OPERATIONS ARE DONE


	TO CREATE ALTERNATIVE LIB40.REL
	-------------------------------

	THIS IS OPTIONAL, IT PRODUCES A MODIFIED VERSION OF LIB40
	THIS IS THE VERSION CALLED XLIB40.REL ON THE RELEASED TAPE.

	THIS LIBRARY CONTAINS NONE OF THE I/O ROUTINES CONTAINED IN
	THE REENTRANT OPERATING SYSTEM, THUS IT IS MUCH SMALLER.
	HOWEVER IT CAN ONLY BE USED WITH THE REENTRANT SYSTEM.
	FOR INSTALLATIONS USING ONLY THE REENTRANT OPERATING SYSTEM
	THIS VERSION OF LIB40.REL WILL SAVE DSK SPACE AND LOADING TIME.
	NB. AND REQUIRES LOADER V52A OR LATER TO LOAD FORTR.SHR BY DEFAULT.
	THESE INSTALLATIONS SHOULD KEEP A VERSION WITH THE I/O ROUTINES
	IN SO THAT PROGRAMS CAN RUN NON-REENTRANTLY.

	USE THE SAME METHOD SHOWN ABOVE TO CREATE LIB40.REL
	BUT USE THE INDIRECT COMMAND FILE XNAMES.CMD RATHER THAN NAMES.CMD

	TO CREATE FORTR.SHR
	-----------------

	FORTR.SHR IS THE REENTRANT AND SHAREABLE VERSION OF THE
	FORTRAN OPERATING SYSTEM. IT IS CREATED USING THE INDIRECT
	COMMAND FILE HNAMES.CMD WHICH SPECIFIES ALL THE SOURCE FILES
	NECESSARY TO BUILD THE REENTRANT OPERATING SYSTEM.

	;DELETE OR RENAME ALL REL FILES ON DISK AREA
.DELETE *.REL
	;COMPILE AND LOAD THE SOURCE FILES FOR FORTR.SHR USING THE INDIRECT
	;COMMAND FILE HNAMES.CMD
.LOAD @HNAMES.CMD
	;AFTER LOADING THE LOADER RETURNS TO MONITOR MODE AND
	;NOW SAVE THE SHAREABLE OPERATING SYSTEM BY
.SSAVE DSK FORTR
	;DELETE ALL REL FILES AND FORTR.LOW SINCE THESE FILES ARE
	;NO LONGER NEEDED
.DELETE *.REL,*.LOW

	TO CREATE IMP40.REL
	-------------------
	IMP40.REL IS THE IMPURE DATA AREA OF THE FORTRAN
	OPERATING SYSTEM THAT IS LOADED WITH EACH USER PROGRAM 
	THAT IS REFERENCING THE REEENTRANT OPERATING SYSTEM.
	IT IS CREATED USING THE INDIRECT COMMMAND FILE LNAMES.CMD
	WHICH SPECIFIES ALL THE SOURCE FILES NECESSARY TO CREATE
	IMP40.REL.

	;COMPILE ALL SOURCE FILES FOR IMP40.REL USING THE INDIRECT
	;COMMAND FILE LNAMES.CMD
.COMPILE @LNAMES.CMD
	;AFTER RETURNING TO MONITOR MODE USE PIP TO COMBINE ALL REL
	;FILES INTO IMP40.REL
.R PIP
*DSK:IMP40.REL_*.REL
*^C
.	;AFTER RETURNING TO MONITOR MODE WITH ^C THEN RUN FUDGE2 TO
	;REMOVE EXTRA ZEROES FROM IMP40.REL
.R FUDGE2
*DSK:IMP40.LIB_DSK:IMP40.REL/A$<ALTMODE>
*^C
.	;AFTER RETURNING TO MONITOR MODE WITH ^C THEN DELETE
	;ALL REL FILES
.DELETE *.REL
	;NOW RENAME ALL LIB FILES
.RENAME *.REL = *.LIB
	;DISK AREA SHOULD NOW CONTAIN
	;LIB40.REL,IMP40.REL,FORTR.SHR AND ALL SOURCE FILES
	;COPY THE LIBRARY AND OPERATING SYSTEM ONTO THE SYSTEM AREA
.R PIP
*SYS:/X_IMP40.REL,LIB40.REL,FORTR.SHR

	TO COPY THE FORTRAN SYSTEM (COMPILER AND LIBRARY) ONTO DEC TAPES
	USE THE PIP CCL FILES 1.CCL THROUGH 9.CCL AS FOLLOWS:-
.AS DTA 1
R PIP
*1.CCL@
^C
.AS DTA 2
R PIP
*2.CCL@
^C
	ETC FOR 1.CCL THROUGH 9.CCL

[END OF LIB40.OPR]

OPREND:	;START CTL FILE HERE
;
;REQUIRED FILES:  (LATEST RELEASED VERSIONS)
;[10,7]	PIP.SHR
;	DIRECT.SHR
;	COMPIL.SHR
;	MACRO.SHR
;	F40.SHR
;	LOADER.SHR
;	JOBDAT.REL
;	CREF.SHR
;[SELF]	NAMES.CMD
;	LNAMES.CMD
;	HNAMES.CMD
;	XNAMES.CMD
;PLUS ALL OF LIB40 SOURCES

;
;OUTPUT FILE:
;	FORTR.SHR
;	IMP40.REL
;	LIB40.REL
;	XLIB40.REL
;OUTPUT LISTINGS:
;	FORTR  MAP
;	LIB40  CREF LISTING
;	LIB40.LOG
;
;
;COPY FILES FROM [10,7] AND USE PRIVATE "SYS:"
.RUN DSK:PIP[10,7]
*/X_DSK:PIP.SHR[10,7],COMPIL.SHR[10,7],MACRO.SHR[10,7],LOADER.SHR[10,7]
*/X_DSK:F40.SHR[10,7],JOBDAT.REL[10,7],CREF.SHR[10,7]
;
;MAKE A RECORD OF WHAT IS BEING USED
.SET WATCH VERSION
.IF (NOERROR) .GOTO A
.RUN DSK:DIRECT[10,7]
*TTY:/CHECKSUM=*.SHR
.GOTO A
A:.RUN DSK: DIRECT[10,7]
*TTY:/CHECKSUM=*.MAC+*.REL
.ASSIGN DSK: SYS:
;
;TO MAKE SHARABLE OPERATING SYSTEM FORTR.SHR
;COMPILE, LOAD, AND SAVE; PRODUCING MAP AND CREF FILE
.LOAD %A /MAP:LPT:FORTR /CREF /COMPILE @HNAMES
.SSAVE DSK:FORTR
.VERSION
.IF (ERROR) .E 137
;
.RUN DSK:DIRECT[10,7]
*TTY:/CHECKSUM=FORTR.SHR
;
;DELETE LOW SEGMENT AND ALL REL FILES
.SILENCE	;DON'T SEE ALL OF THIS
.DELETE FORTR.LOW,*.REL
.REVIVE	;LOOK AGAIN
;
;PRODUCE SOURCE LISTING
.CREF
;
;MAKE DATA BASE FOR SHARABLE OPERATING SYSTEM
;
.COMPIL /CREF /COMPIL @LNAMES
.R PIP
IMP40.LIB/B_*.REL
;
;PRODUCE SOURCE LISTINGS
.CREF
;
;REMOVE ALL TEMPORARY FILES
.DELETE *.REL
;
; TO MAKE LIB40 - COMPLETE FORTRAN LIBRARY
;
.COMPIL /CREF /COMPIL @NAMES
;
.R PIP 7
LIB40.LIB/B_*.REL
;
;GET RID OF ALL TEMPORARY FILES
;
.SILENCE	;DON'T SEE ALL OF THIS
.DELETE *.REL
.REVIVE	;LOOK AGAIN
;
;PRODUCE SOURCE LISTINGS
.CREF
;
;MAKE SHORT LIBRARY NO I/O ROUTINES
;
.COMPIL /COMPIL @XNAMES
.R PIP 7
XLIB40.LIB/B_*.REL
;
;GET RID OF ALL TEMPORARY FILES
.SILENCE	;DON'T SEE ALL OF THIS
.DELETE *.REL
.REVIVE	;LOOK AGAIN
;
;RENAME ALL LIB FILES
.RENAME *.REL=*.LIB
.IF (ERROR) ;DON'T CARE
;
;TELL OPERATOR
.PLEASE LIB40 SUCCESSFUM	 
;
;REMOVE ALL TEMPORARY FILES
%FIN: .DELETE MACRO.SHR,LOADER.SHR,CREF.SHR
.IF (ERROR) ;DON'T CARE IF FAILED
.DELETE COMPIL.SHR,PIP.SHR,F40.SHR
.IF (ERROR)	;DON'T CARE IF FAILED
