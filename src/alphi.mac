TITLE ALPHI.  V.30.0.40  	ALPHANUMERIC INPUT	FORTRAN IV
SUBTTL	V30.0.40 FROM V 30 16-DEC-70 /VAA
;V.30 16-NOV-70	/DMN

;REENT==1 FOR RE-ENTRANT OPERATING SYSTEM
IFNDEF REENT,<REENT==0>
IFN REENT,<	HISEG	>

;FROM V.26 26/MAR/70 FROM V.006 18-JUL-68 /VAA
;VERSION 006 CREATED BY TIM MCGETTIGAN

;"ALPHI." IS A ROUTINE WHICH RECEIVES UP TO 5 7-BIT ASCII CHARACTERS
;IN ACO FROM "CHINN.", AND WHICH RETURNS THE INPUT ITEM IN THE
;SAME AC.  "IIB." INCREMENTS THE BYTE POINTER.
;IF THE FIELD WIDTH, W, <5, THE CHARACTERS ARE LEFT ADJUSTED AND
;ACZ IS FILLED WITH W-5 BLANKS.
;IF W > 5, W-5 CHARACTERS ARE IGNORED AND THE 5 RIGHTMOST CHARACTERS
;ARE USED.

;  CALLING SEQUENCE:
;	PUSHJ P,ALPHI.
;  2 RETURNS:
;       ILLEGAL CHARACTER...NOT USED
;	NORMAL
;  PUSHDOWN LIST CONTAINS:
;	1.  FIELD WIDTH W IN LEFT HALF
;	2.  PROGRAM COUNTER (RETURN ADDRESS)
;	3.-8.  FREE STORAGE


;THIS ROUTINE HAS BEEN MODIFIED TO HANDLE DOUBLE PRECISION
;VARIABLES. IF INPUT IS TO ONE OF THESE VARIABLES, THE
;MAXIMUN FIELD WIDTH IS 10. THE TYPE OF THE VARIABLE IS STORED
;IN " TYPE." BY "FORSE." IF THE VARIABLE IS D.P., THE SECOND WORD IS 
;PLACED DIRECTLY IN THE SECOND WORD OF THE VARIABLE IN THE USERS'
;PROGRAM.  IF INPUT WAS DONE THROUGH A "A" FORMAT, "FORSE." WILL
;ONLY LOAD THE FIRST WORD; THEREFORE THE SECOND WORD MUST BE LOADED HERE.

	ACO=0		;RECEIVES CHAR FROM CHINN. AND
			;RETURNS THE OUTPUT ITEM
	ACT=2		;FIELD WIDTH
	ACTH=1		;BYTE PNTR
	ACF=4		;COUNTER
	E=5		;STORAGE
	AC6=6
	AC8=8	;CONTAINS MAXIMUM NUMBER OF INPUT CHARACTERS ALLOWABLE
	PDP=17		;PUSHDOWN POINTER
IFE REENT,<LOW=0	;NO INDEXING>
IFN REENT,<LOW=16	;OFFSET OF LOW SEGMENT DATA>
	ENTRY	ALPHI.
	EXTERN CHINN.,IIB.,TYPE.,OVFLS.

ALPHI.:	PUSH PDP,E	;SAVE ACS
	PUSH PDP,ACT
	PUSH	PDP,AC6		;SECOND WORD FOR D.P. 
	PUSH PDP,ACF
	PUSH	PDP,AC8
	PUSH PDP,ACTH
	MOVE	AC8,TYPE.(LOW)	;GET TYPE OF VARIABLE TO WHICH INPUT IS BEING DONE
	CAIE	AC8,7	;COMPLEX
	CAIN	AC8,6	;"6" MEANS DOUBLE PRECISION
	MOVEI	AC8,12	;THEREFORE POSSIBLE TEN CHARACTERS OF INPUT
	CAIE	AC8,12	;OTHERWISE ONLY 5 CHAR
	MOVEI	AC8,5
	SETZB	AC6,E		;CLEAR AC6 FOR LOW ORDER WD OF DP
	HLRZ ACT,-7(PDP)	;PICK UP FIELD WIDTH
	JUMPLE ACT,AIRTN	;IF W <,= 0, RETURN
	MOVE ACTH, CPTR	;STORE BYTE POINTER
	MOVE ACF,ACT	;SAVE WIDTH
	SUBI	ACF,(AC8)	;GET DEVIATION FROM MAX. NUMBER OF CHAR. ALLOWED
	JUMPG ACF, SKA	;IS WIDTH > 5?

ALOAD:	PUSH	PDP,AC6
	PUSH	PDP,AC8	;SAVE AC6 AND AC8 AND RESTORE THESE
	MOVE	AC6,-3(PDP)	;ACCUMLATORS IN CASE CHINN. NEEDS THEM
	MOVE	AC8,-2(PDP)
	PUSHJ PDP,CHINN.	;NO, GET A CHAR. (ACO)
	POP	PDP,AC8
	POP	PDP,AC6
	CAIN	ACO,15		;IS THE CHAR. A CR. ?
	TRCA	ACO,40!15	;YES, CONVERT TO SPACE AND SKIP
	XCT	IIB.		;NO,INCREMENT THE POINTER
	IDPB ACO,ACTH	;DEPOSIT CHAR
	SOJG ACT,ALOAD	;ANOTHER CHAR?
	JUMPE ACF,AIRTN	;NO, ANY BLANKS?
	MOVEI ACO,40	;YES
	IDPB ACO,ACTH	;DEPOSIT BLANK
	AOJL ACF,.-1	;ANY MORE?
AIRTN:	MOVE ACO,E	;NO, SET NORMAL RETURN
	MOVE	ACTH,AC6
	SUBI	AC8,12
	JUMPN	AC8,NORM
	POP	PDP,(PDP)	;DECREMENT STACK SAFELY
	JRST	.+2
NORM:	POP PDP, ACTH
	POP	PDP,AC8
	AOS -4(PDP)
	POP PDP,ACF	;RESTORE AC'S
	POP	PDP,AC6
	POP PDP,ACT
	POP PDP,E
	PUSH	PDP,ACF
	MOVE	ACF,7(PDP)
	CAIN	ACF,12
	MOVEM	1,1(3)	;THIS PLACES THE LOWER PART IN THE
			;CORRECT LOACTION IN THE FORTRAN PROGRAM
			;FORSE. WILL NOT DO IT FOR "A" FIELDS, SO IT MUST 
			;BE DONE HERE
	POP	PDP,ACF
	POPJ PDP,		;RETURN

SKA:	PUSH	PDP,AC6
	PUSH	PDP,AC8
	MOVE	AC6,-3(PDP)
	MOVE	AC8,-2(PDP)
	PUSHJ PDP,CHINN.	;GET CHAR
	POP	PDP,AC8
	POP	PDP,AC6
	SKIPN	OVFLS.(LOW)	;IF NOT AT END OF LINE
	XCT IIB.		;ADVANCE POINTER
	SOJG ACF,SKA	;W STILL > 5?
	MOVE ACT,AC8	;NO, SET W = 5
	JRST ALOAD	;BEGIN INPUT
CPTR:	POINT 7,E

	END

