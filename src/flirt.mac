TITLE FLIRT.   	FLOATING POINT INPUT	FORTRAN IV
SUBTTL	V31(217)	24-JUN-71	/DMN

;REENT==1 FOR RE-ENTRANT OPERATING SYSTEM
IFNDEF REENT,<REENT==0>
IFN REENT,<	HISEG	>

;"FLIRT." IS A ROUTINE WHICH INPUTS A STRING OF ASCII CHARACTERS.
;THE CHARS. ARE RECEIVED IN ACO FROM "CHINN."; THE INPUT ITEM IS
;RETURNED IN THE SAME AC.  "IIB." IS AN EXTERNAL ROUTINE WHICH
;ADVANCES THE POINTER; "TBLP." IS AN EXTERNAL TABLE
;OF FLOATING POINT POWERS OF TEN.

;IF THE FLAG ILLEG. HAS BEEN SET (BY A CALL TO ILL), THE
;INPUT WORD WILL BE SET TO 0 IF ANY ILLEGAL CHARACTERS
;ARE SCANNED FOR THAT WORD.

;  CALLING SEQUENCE:
;	PUSHJ P,FLIRT.
;  2 RETURNS:
;	ILLEGAL CHARACTER
;	NORMAL
;  PUSHDOWN LIST CONTAINS:
;	1.  FORMAT WORD CONSTRUCTED AS FOLLOWS:
;	    BIT 0:  0=F TYPE CONVERSION
;	            1=E TYPE CONVERSION
;	    BIT 1:  1=G TYPE CONVERSION
;	    BITS 4-10:  D -- NO. OF DIGITS FOLLOWING THE DECIMAL POINT
;	    BITS 11-17:  W -- FIELD WIDTH; W=0, VARIABLE FIELD
;	    BITS 18-35:  N -- SCALE FACTOR
;	2.  PROGRAM COUNTER (RETURN ADDRESS)

	H=6		;INPUT WORD
	ACO=0		;RECEIVES CHAR. FROM CHINN., AND
			;RETURNS THE INPUT ITEM
	ACT=2		;CNTR FOR MULTIPLICATION FACTOR
	FL=1		;FLAG
	FMT=4		;FORMAT WORD
	ACNO=3		;FRACTION,EXPONENT
	W=5		;FIELD WIDTH
	N=16		;FLAGS IN LEFT HALF
	PDP=17		;PUSHDOWN POINTER
	NB=6		;NO. AC'S TO BE STORED
IFE REENT,<LOW=0	;NO INDEXING>
IFN REENT,<LOW=16	;OFFSET OF LOW SEGMENT DATA>


	ENTRY	FLIRT.
	EXTERN CHINN.,IIB.,TBLP.,ILLEG.,DELIM.,OVFLS.,TTYFL.

FLIRT.:	MOVEI	ACO,1(PDP)	;TO ADDRESS FOR BLT
	HRLI ACO,ACO+1
	ADD PDP,XNB	;COMPENSATE FOR BLT
	BLT ACO,(PDP)
	CLEARB	ACNO,FL
	CLEARB H,ACT
	MOVE	FMT,-NB-1(PDP)	;GET FORMAT WORD
	TLNE FMT,200000	;IF G TYPE,
	TLZ FMT,400000	;SET TO F-TYPE
	LDB W,PTR1	;LOAD FIELD WIDTH
	JUMPN W,.+3
	SOJA W,.+2	;IF VARIABLE FIELD ,SET TO -1
CL:	JUMPE W,ENDF1	;END OF FIELD
	PUSHJ PDP,CHINN.	;GET A CHARACTER
	SUBI W,1	;DECREMENT FILED WIDTH
	CAIG ACO,"9"	;TEST FOR DIGIT
	CAIGE ACO,"0"
	JRST NODIG	;NOT A DIGIT
	TLO FL,FIRDIG	;SET FIRST-DIGIT FLAG
	SUBI ACO,60	;REMOVE ASCII CODE
IM:	TLNN FL,OFLO	;OVERFLOW FLAG SET?
	JRST IM1		;NO, FORM AN INTEGER
	TLNN FL,FRAC	;IS THIS THE FRACTION?
	ADD ACT,L1	;NO, INCREMENT OVERFLOW COUNTER
	JRST IB		;GET NEXT CHARACTER
IM1:	TLNE	FL,FRAC	;ON FRACTION?
	TLNN	FL,DECF	;AND DECIMAL POINT SEEN?
	JRST	IM2	;NO
	JUMPE	ACO,[TLO FL,ZFL	;SET ZERO SEEN
		AOJA	FL,IB]	;ADD ONE TO COUNT
	TLZE	FL,ZFL	;ANY ZERO SEEN?
	PUSHJ	PDP,ZFILL	;YES, MUST MULTIPLY
IM2:	IMULI ACNO,12	;FORM AND SAVE INTEGER
	ADD ACNO,ACO
	TLNE	FL,FRAC		;FRACTION?
	ADDI	ACT,1	;YES
	TLNN ACNO,377000	;OVERFLOW IN CHARACTERISTIC?
	JRST IB		;NO, GET NEXT CHAR.
	PUSH PDP,ACNO+1	;SAVE ACNO+1
	IDIVI ACNO,12	;OTHERWISE, COMPENSATE:
	CAIL ACNO+1,5	;IF DROPPED DIGIT >,= 5,
	ADDI ACNO,1	;ADD ONE TO LAST CHAR
	ADD ACT,L1	;INCREMENT OVERFLOW CNTR
	TLO FL,OFLO	;SET OVERFLOW FLAG
	POP PDP,ACNO+1	;RESTORE ACNO+1
	JRST IB		;GET NEXT CHARACTER

EXPCH:	TLOE FL,EXP	;YES, EXPONENT FLAG ALREADY SET?
	JRST DELCK
	EXCH H,ACNO	;NO, DEPOSIT FRACTION
	TLO FMT,400000	;SET E-TYPE
	TLZ	FL,OFLO+FRAC	;CLEAR OVERFLOW AND FRAC FLAGS
	JRST IB		;GET NEXT CHARACTER
DELCK:	JUMPGE W,ERROR		;VARIABLE FIELD?
ENDF:	MOVEM	ACO,DELIM.(LOW)	;SAVE DELIMITER FOR NAMELIST
	CAIN	ACO,15		;IS CHAR. A CARRIAGE RETURN ?
	JRST	ENDF1		;YES,DONT ADVANCE POINTER
	CAIN	ACO,56		;IS CHAR A DECPT? (V17.3)
	JRST	ADDCNT		;(V17.3)
	CAIE	ACO,55		;IS CHAR. A -
	CAIN	ACO,53		;IS CHAR. A + ?
ADDCNT:	AOSA	2(7)		;ADD ONE TO THE ITEM COUNT
	XCT IIB.		;OTHERWISE, ADVANCE PNTR.
ENDF1:	TLNN FL,EXP	;EXPONENT?
	EXCH H,ACNO	;NO, DEPOSIT FRACTIONS
	JUMPE H,RETURN	;IF NUMBER IS 0, NORMAL RETURN
	TLNE FL,NEGEXP	;IS EXP NEGATIVE?
	MOVNS ACNO	;YES, COMPLEMENT IT
	HLRZ ACO,ACT	;SET UP REDUCTION CNTR
	TLNN FL,DECF	;DECIMAL POINT FLAG SET?
	LDB ACT,PTR2	;NO - GET D FROM FORMAT WORD
	ADD ACNO,ACO
	SUBI ACNO,(ACT)	;SET MULTIPLICATION FACTOR
	TLNE FL,EXP	;EXPONENT?
	JRST .+3
	HRRE ACO,FMT	;YES, SUBTRACT SCALE FACTOR FROM
	SUB ACNO,ACO	;MULTIPLICATION FACTOR
	JOV .+1		;CLEAR OV
	TLCE	H,233000	;SET CHARACTERISTIC, CHECK FOR OVERFLOW
	MOVSI	H,234400	;YES, OVERFLOWED DURING ADDI 1

	FAD H,ZE		;RANGE OK, NORMALIZE FRACTION
	JUMPGE ACNO,FMR	;IS EXPONENT .GE. 0?
	MOVMS ACNO	;NO, THEN COMPLEMENT IT
	MOVEI ACT,-6	;AND SET CNTR = -6
	JRST FMR+1
FMR:	MOVEI ACT,1	;OTHERWISE, SET CNTR = 1
	CAIL	ACNO,2*^D32
	JRST	BADEXP	;WELL OUT OF RANGE
TR:	TRNE ACNO,1	;IF LOW ORDER BIT OF EXPONENT
	FMPR H,TBLP.(ACT)	;IS ONE,MULTIPLY BY POWER OF TEN
	ASH ACNO,-1
	ADDI ACT,1
	JUMPN ACNO,TR	;ANY MORE CHARS?
	JOV BADEXP	;NO, CHECK OV
TLF:	TLNE FL,NEGFRA	;IF NEGATIVE FRACTION,
	MOVNS H		;COMPLEMENT IT
	JRST RETURN	;NORMAL RETURN

BADEXP:	HRLOI H,377777	;SET NUMBER TO LARGEST POSSIBLE
	TLNE FL,NEGEXP	;IF EXPONENT IS NEGATIVE
	SETZ H,		;SET TO ZERO FOR UNDERFLOW
	JRST TLF

NODIG:	TLZE	FL,ZFL	;ANY ZEROS SEEN
	TRZ	FL,-1	;CLEAR COUNT
	CAIE ACO,105	;E?
	CAIN ACO,104	;D?
	JRST EXPCH	;EXPONENT
NOTE:	CAIN	ACO,40	;BLANK?
	JRST	BLK	;YES
	CAIN	ACO,15	;CR ?
	JRST	TSTVAR	;YES
NBL:	CAIN ACO,56	;NOT BLANK, .
	JRST DECPT
	CAIN ACO,55	;-
	JRST MINUS
	CAIN ACO,53	;+
	JRST PLUS
	JUMPL W,ENDF	;DELIMITER?
ERROR:	SKIPN ILLEG.(LOW);ILLEGAL CHAR. FLAG SET?
	JRST RETURN+1	;ILLEGAL CHAR. RETURN
ERROR1:	SKIPN	OVFLS.(LOW)
	XCT IIB.	;YES,SKIP REMAINDER OF FIELD
	PUSHJ PDP,CHINN.;GET NEXT CHARACTER
	SOJGE W,ERROR1	;SKIP IT
	MOVEI H,0	;RETURN RESULT OF 0
RETURN:	AOS -NB(PDP)	;SET NORMAL RETURN
	MOVE ACO,H	;PUT RESULT IN AC0
	HRLI H,1-NB(PDP)	;RESTORE AC'S
	HRRI H,1
	BLT H,NB
	SUB PDP,XNB
	POPJ PDP,		;RETURN
BLK:	TLNN FL,4	;BLANK,FIRST DIGIT IN?
	JRST IB		;NO,GET NEXT CHAR.
TSTVAR:	JUMPL W,ENDF	;DELIMITER?
	TLNE	N,TTYFL.	;IS INPUT FROM A TTY?
	JRST	ENDF		;YES, AS IF IN DELIMITER MODE
	MOVEI ACO,0	;NO,INPUT A 0
	JRST IM		;ACCUMULATE RESULT



DECPT:	TLNE	FL,EXP		;IN EXPONENT?
	JRST	DELCK		;YES, D. OR E. IS ILLEGAL
	TLON FL,FRAC+DECF	;SET FRACTION AND DEC. PT. FLAGS
	JRST IB		;NO, GET NEXT CHAR
ILLCH:	JUMPL W,ENDF	;YES, DELIMITER?
	JRST ERROR	;NO, ILLEGAL
MINUS:	TROA FL,NEGFRA	;-, SET RT. HALF TO 10
PLUS:	HRRI FL,0		;+, SET RT. HALF TO 0
	TLNE FL,FIRDIG	;FIRST DIGIT IN?
	JRST .+3
	TLO FL,(FL)	;NO - SET RESULT SIGN
	JRST	IBB	;CLEAR FL RIGHT AND GET NEXT CHAR. (REG&TVR 10/72)
	JUMPL FMT,.+2	;EXPONENT EXPECTED?
	JUMPL W,ENDF	;NO, VARIABLE FIELD?
	TLOE FL,ESIGN	;EXPONENT SIGN ALREADY SET?
	JRST ILLCH
	TLON FL,EXP	;NO, EXPONENT FLAG SET?
	EXCH H,ACNO	;NO, DEPOSIT FRACTION
	ADDI FL,(FL)	;SET EXPONENT SIGN
	TLO FL,(FL)
	TLZ	FL,OFLO+FRAC	;CLEAR OVERFLOW AND FRAC FLAGS
IBB:	HLLZ	FL,FL		;FLUSH FLAG FROM FL RIGHT (REG&TVR 10/72)
IB:	SKIPN	OVFLS.(LOW)	;DONT MOVE PTR IF AT END OF LINE(V17.3)
	XCT IIB.		;ADVANCE POINTER
	JRST CL		;GET NEXT CHAR

ZFILL:	IMULI	ACNO,^D10	;MULTIPLY BY 10
	ADDI	ACT,1		;INCREMENT COUNTER
	TLNE	ACNO,377000	;OVERFLOW INTO CHARACTERISTIC?
	SOJA	ACT,ZFILLO	;YES, UNDO ADD
	TRNE	FL,-2		;FINISHED
	SOJA	FL,ZFILL	;NO
	JRST	ZFILL1		;CLEAR FLAG AND RETURN

ZFILLO:	PUSH	PDP,ACNO+1	;SAVE ACNO+1 FROM DIVIDE
	IDIVI	ACNO,^D10	;UNDO LAST MULTIPLY
	TLO	FL,OFLO		;SET OVERFLOW
	POP	PDP,ACNO+1	;RESTORE ACNO+1
	AOBJP	ACT,.+1		;INCREMENT OVERFLOW COUNTER
	TRNE	FL,-2		;FINISHED
	SOJA	FL,.-2		;NO
ZFILL1:	TLZ	FL,ZFL		;YES, CLEAR FLAG
	TRZ	FL,-1		;CLEAR COUNT
	POPJ	PDP,		;RETURN


;FLAGS
	EXP==1			;EXPONENT
	FRAC==2			;FRACTION
	FIRDIG==4		;FIRST DIGIT
	NEGFRA==10		;NEGATIVE FRACTION
	NEGEXP==20		;NEGATIVE EXPONENT
	ESIGN==40		;EXPONENT SIGN
	OFLO==100		;OVERFLOW INTO CHARACTERISTIC
	DECF==1000		;DECIMAL POINT FLAG
	ZFL==2000		;ZERO SEEN
L1:	XWD 1,0
PTR1:	POINT 7,FMT,17
PTR2:	POINT 7,FMT,10
ZE:	Z
XNB:	XWD NB,NB

END


