	TITLE	SLITE  V.005
	SUBTTL  28-SEP-66

;SENSE LIGHT SETTING AND TESTING PROGRAM
;THIS PROGRAM CAN BE ENTERED AT TWO PLACES. THE SENSE LIGHT
;TESTING PROGRAM IS CALLED IN THE FOLLOWING MANNER:
;	JSA	Q, SLITET
;	EXP	I
;	EXP	J
;I IS THE ADDRESS OF AN INTEGER ARGUMENT, AND J IS THE ADDRESS
;OF THE ANSWER. IF SENSE LIGHT I IS ON, THE ANSWER IS ONE, AND
;IF IT IS OFF, THE ANSWER IS 2.

;THE SENSE LIGHT SETTING PROGRAM IS CALLED IN THE FOLLOWING
;MANNER:
;	JSA	Q, SLITE
;	EXP	I
;I IS THE ADDRESS OF AN INTEGER ARGUMENT WHOSE VALUE IS
;BETWEEN 0 AND 36. IF I=0, ALL SENSE LIGHTS ARE TURNED OFF.
;OTHERWISE, SENSE LIGHT I IS TURNED ON.

	ENTRY	SLITE, SLITET

	A=	0
	B=	1
	Q=	16


SLITE:	0			;ENTRY TO SLITE PROGRAM
	MOVEM	A, ASAVE	;SAVE ACCUMULATOR A
	MOVEM	B, BSAVE	;SAVE ACCUMULATOR B
	MOVN	B, @(Q)		;GET ARGUMENT
	JUMPE	B, SLITE1	;IS IT ZERO?
	MOVSI	A, 400000	;NO, PUT A ONE IN BIT 0
	ROT	A, 1(B)		;ROTATE IT INTO POSITION
	MOVE	B, LITES	;GET THE SENSE LIGHTS
	TDO	B, A		;TURN ON PROPER LIGHT
	MOVEM	B, LITES	;SAVE NEW SENSE LIGHTS
SLITE2:	MOVE	A, ASAVE	;RESTORE ACS
	MOVE	B, BSAVE	;...
	JRA	Q, 1(Q)		;EXIT
SLITE1:	SETZM	LITES		;ZERO ARGUMENT,CLEAR LITES
	JRST	SLITE2		;RESTORE ACS AND EXIT

SLITET:	0			;ENTRY TO SENSE TESTING PROGRAM
	MOVEM	A, ASAVE	;SAVE ACCUMULATOR A
	MOVEM	B, BSAVE	;SAVE ACCUMULATOR B
	MOVN	B, @(Q)		;PICK UP ARGUMENT
	MOVSI	A, 400000	;PUT A ONE IN BIT 0
	ROT	A, 1(B)		;ROTATE IT INTO POSITION
	MOVEI	B, 1		;SET ANSWER TO ONE FOR NOW
	MOVEM	B, @1(Q)	;...
	MOVE	B, LITES	;PICK UP SENSE LIGHTS
	TDZN	B,A		;IS THE PROPER LIGHT ON?
	AOS	@1(Q)		;NO, CHANGE ANSWER TO 2
	MOVEM	B,LITES		;RESTORE WITH TESTED LIGHT OFF
	MOVE	A, ASAVE	;RESTORE ACCUMULATOR A
	MOVE	B, BSAVE	;RESTORE ACCUMULATOR B
	JRA	Q, 2(Q)		;EXIT

ASAVE:	0
BSAVE:	0
LITES:	0

	END

