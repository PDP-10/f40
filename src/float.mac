	TITLE	FLOAT  V.27Q
	SUBTTL	7/OCT/70 FROM V.022 /VAA
;V.022 5-DECEMBER-1969	ED YOURDON/KK
;FROM V.005
;36 BIT FLOAT FUNCTION
;CONVERTS A SIGNED FIXED POINT INTEGER TO FLOATING POINT
;BY BREAKING THE INTEGER INTO HIGH ORDER AND LOW ORDER
;FRACTIONS, CALCULATING AN EXPONENT, THEN ADDING THE TWO
;TOGETHER. SINGLE AND DOUBLEPRECISION CONVERSION.

;THE ROUTINE IS CALLED AS FOLLOWS:
;	JSA	Q, FLOAT
;	EXP	ARG
;THE ANSWER IS RETURNED IN ACCUMULATOR A

	ENTRY	FLOAT

	A=	0
	B=	1
	Q=	16

	SIXBIT/FLOAT/
FLOAT:	0			;ENTRY TO FLOAT ROUTINE
	MOVE	A, @(Q)		;PICK UP THE ARGUMENT
	IDIVI	A, 400		;DIVIDE INTO 2 PEICES
	SKIPE	A		;IMPLIES INTEGER LESS THAN 18 BITS
	TLC	A, 243000	;SET EXP TO 254 (27+17 DECIMAL)
	TLC	B, 233000	;SET EXP OF B TO 233 (27 DECIMAL)
	FADL	A, B		;NORMALIZE AND ADD
	JRA	Q, 1(Q)		;EXIT

	END

