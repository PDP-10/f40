	TITLE	TYPER.	DUMMY TYPER FOR LIB40
; REG 24 JULY 72

	ENTRY	TYPER.

TYPER.:	JUMP	0		;A NICE NO-OP
	TTCALL	3,[ASCIZ/YOU HAVE JUST INDUCED A SUBROUTINE
FROM THE FORTRAN LIBRARY TO GIVE YOU AN ERROR MESSAGE.
CALLERS ADDRESS = /]
	MOVEM	17,SAVEAC+17
	MOVEI	17,SAVEAC
	BLT	17,SAVEAC+16
	MOVE	17,[IOWD 10,PDLIST]
	SETOM	PUSHY
	MOVE	1,TYPER.
	CAME	1,[JUMP	]
	SETZM	PUSHY		;MUST HAVE BEEN A JSA
	HRRZ	1,SAVEAC+17	;GET PDL POINTER
	HRRZ	1,(1)
	SKIPN	PUSHY
	HRRZ	1,SAVEAC+16
	SUBI	1,1
	PUSHJ	17,OCTOUT
	TTCALL	3,[ASCIZ/    AC0 = /]
	MOVE	1,SAVEAC
	PUSHJ	17,OCTOUT
	TTCALL	3,[ASCIZ/
/]
	MOVSI	17,SAVEAC
	BLT	17,17
	SKIPE	PUSHY
	POPJ	17,
	JRA	16,0(16)

PDLIST:	BLOCK	10
PUSHY:	0
SAVEAC:	BLOCK	20

OCTOUT:	PUSH	17,1
	HLRZ	1,1		;GET LEFT SIDE
	JUMPE	1,OCTOU1
	PUSHJ	17,OCTOU2
	TTCALL	3,[ASCIZ/,,/]
OCTOU1:	POP	17,1
	HRRZ	1,1
OCTOU2:	IDIVI	1,10
	HRLM	2,(17)
	JUMPE	1,.+2
	PUSHJ	17,OCTOU2
	HLRZ	1,(17)
	ADDI	1,"0"
	TTCALL	1,1
	POPJ	17,
	END
