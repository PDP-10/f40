	TITLE	MIN	V.022
	SUBTTL	5-DECEMBER-1969	ED YOURDON/TWE/KK

;FROM V.005.
;AMIN1, MIN1, AMIN0, AND MIN0 CALCULATE THE MINIMUM OF A
;STRING OF ARGUMENTS, THE FIRST ADDRESS OF WHICH IS IN
;ACCUMULATOR Q.

;THE ROUTINES ARE CALLED IN THE FOLLOWING MANNER:
;	JSA	Q,AMIN1 OR MIN1 OR AMIN0 OR MIN0
;	EXP	A
;	EXP	B
;	ETC
;FOR AMIN1 AND MIN1, A,B,... ARE REAL.  THE ANSWER
;RETURNED IS REAL FOR AMIN1 AND INTEGER FOR MIN1.
;FOR AMIN0 AND MIN0, A,B,... ARE INTEGER.  THE ANSWER 
;RETURNED IS REAL FOR AMIN0 AND INTEGER FOR MIN0.

;THE ANSWER IS RETURNED IN ACCUMULATOR A.

	ENTRY	AMIN1,MIN1,AMIN0,MIN0
	EXTERNAL	IFIX,FLOAT

A=0
B=1
Q=16
P=17

	SIXBIT/AMIN1/
AMIN1:	0			;ENTRY TO AMIN1 ROUTINE.
	PUSHJ	P,MIN.		;FIND THE MINIMUM AND
	JRA	Q,(Q)		;RETURN.

	SIXBIT/MIN0/
MIN0:	0			;ENTRY TO MIN0 ROUTINE.
	PUSHJ	P,MIN.		;FIND THE MINIMUM AND
	JRA	Q,(Q)		;RETURN.

	SIXBIT/MIN1/
MIN1:	0			;ENTRY TO MIN1 ROUTINE.
	PUSHJ	P,MIN.		;FIND THE MINIMUM AND
	JSA	Q,IFIX		;FIX THE
	EXP	SAVARG		;RESULT AND
	JRA	Q,(Q)		;RETURN.

	SIXBIT/AMIN0/
AMIN0:	0			;ENTRY TO AMIN0 ROUTINE.
	PUSHJ	P,MIN.		;FIND THE MINIMUM AND
	JSA	Q,FLOAT		;FLOAT THE
	EXP	SAVARG		;RESULT AND
	JRA	Q,(Q)		;RETURN.

MIN.:	MOVE	A,@(Q)		;PICK UP FIRST ARG.
	AOJA	Q,MIN.2		;INCREMENT POINTER TO NEXT ADDRESS.

MIN.1:	CAMLE	A,@B		;IS CURRENT ARG < NEXT ARG?
	MOVE	A,@B		;NO, PUT ARG IN A.

MIN.2:	MOVE	B,(Q)		;NEXT ARG ADDR TO B.
	TLC	B,(<JUMP>)	;IS THE OPCODE OF THE
	TLNN	B,777000	;NEXT ARG A JUMP?
	AOJA	Q,MIN.1		;YES, LOOP.
	MOVEM	A,SAVARG	;ANSWER IN A AND SAVARG.
	POPJ	P,		;EXIT.

SAVARG:	0
	END


