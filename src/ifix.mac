	TITLE	IFIX  V.022
	SUBTTL	5-DECEMBER-1969	ED YOURDON/TWE/KK

;FROM V.005.
;36 BIT FIX FUNCTION
;AN INTEGER RESULT IS OBTAINED BY SEPARATING FRACTION AND
;EXPONENT. THE FRACTION IS SHIFTED N PLACES RIGHT, WHERE
;N = 43 - (EXPONENT-200) (OCTAL)

;THE CALLING SEQUENCE FOR THE ROUTINE IS AS FOLLOWS:
;	JSA	Q, IFIX
;	EXP	ARG
;OR
;	JSA	Q,INT
;	EXP	ARG

;THE ANSWER IS RETURNED IN ACCUMULATOR A

	ENTRY	IFIX,INT

	A=	0
	B=	1
	Q=	16

	SIXBIT/INT/
INT:	0			;ENTRY TO INT ROUTINE.
	JRST	IFIX+1		;GO TO MAIN ROUTINE.

	SIXBIT/IFIX/
IFIX:	0			;ENTRY TO IFIX ROUTINE
	MOVM	A, @(Q)		;PICK UP THE ARGUMENT
	MULI	A, 400		;SEPARATE FRACTION AND EXPONENT
	EXCH	A, B		;PUT PARTIAL RESULTS IN A
	ASH	A, -243(B)	;USE EXPONENT AS INDEX REGISTER
	SKIPGE	@(Q)		;SET THE
	MOVNS	A,A		;CORRECT SIGN.
	JRA	Q, 1(Q)		;EXIT

	END

