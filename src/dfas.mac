TITLE DFAS	19 APRIL 1969
SUBTTL V.020 DOUBLE PRECISION ADD AND SUBTRACT- TOM EGGERS
Q=16	;ADDRESS FOR MEMORY ARGUMENT
M=Q	;DITTO, BUT M IS MNEMONIC
P=17	;PUSH DOWN POINTER

MLON


DEFINE FLADD(X,%OV)<
	PUSH P,X+2		;SAVE 3RD AC
	PUSH P,(M)		;GET HIGH PART OF MEM ARG AWAY FROM X+2
	UFA X+1,1(M)		;ADD LOW ORDER PARTS
	FADL X,(P)		;ADD HIGH ORDER PARTS
	JOV %OV			;IF OVER/UNDERFLOW, FIX AND EXIT
	UFA X+1,X+2		;ADD LOW PART TO SUM OF LOWER PARTS
	FADL X,X+2		;ADD HIGH PART OF SUM TO LOW PART
%OV:	POP P,X+2		;GET HIGH MEM ARG OFF LIST
	POP P,X+2		;RESTORE X+2
>

DEFINE DFAD(X)<

ENTRY DFA.'X
	SIXBIT /DFA.'X/
DFA.'X:	FLADD X
	POPJ P,

>

DEFINE DFAM(X)<

ENTRY DFAM.'X
	SIXBIT /DFAM.'X/
DFAM.'X:	FLADD X
	MOVEM X,(M)		;SAVE HIGH PART IN MEMORY
	MOVEM X+1,1(M)		;DITTO LOW PART
	POPJ P,

>

DEFINE DFSB(X)<

ENTRY DFS.'X
	SIXBIT /DFS.'X/
DFS.'X:	DFN X,X+1		;NEGATE AC
	PUSHJ P,DFA.'X		;DOUBLE PRECISION ADD
	DFN X,X+1		;NEGATE AC AGAIN
	POPJ P,
>

DEFINE DFSM(X)<

ENTRY DFSM.'X
	SIXBIT /DFSM.'X/
DFSM.'X:	DFN X,X+1	;NEGATE AC
	PUSHJ P,DFA.'X		;ADD
	DFN X,X+1		;NEGATE AC AGAIN
	MOVEM X,(M)		;STORE HIGH PART
	MOVEM X+1,1(M)		;STORE LOW PART
	POPJ P,
>

DEFINE EXPAND(X)<
IRP X,<
DFAD X
DFAM X
DFSB X
DFSM X
>>

EXPAND <0,2,4,6>

END

