	TITLE	SIGN.  V.022
	SUBTTL	29-NOVEMBER-1969 	ED YOURDON/KK

;SIGN(A,B) AND ISIGN(A,B) ARE CALLED IN THE FOLLOWING MANNER:
;	JSA	Q, SIGN
;	EXP	A
;	EXP	B
;IF B .GE. 0, THEN ABSF(A) IS RETURNED IN ACCUMULATOR A
;IF B .L. 0, THEN -ABSF(A) IS RETURNED IN ACCUMULATOR A

	ENTRY	SIGN, ISIGN

	A=	0
	Q=	16

	SIXBIT/SIGN/
SIGN:	0			;ENTRY TO SIGN
	MOVM	A, @(Q)		;GET MAGNITUDE OF FIRST ARGUMENT
	SKIPGE	@1(Q)		;IS SECOND ARGUMENT POSITIVE?
	MOVNS	A		;NO, NEGATE RESULT
	JRA	Q, 2(Q)		;EXIT

	SIXBIT/ISIGN/
ISIGN:	0			;ENTRY TO ISIGN
	MOVE	A,@(Q)		;FIRST ARG TO A.
	XOR	A,@1(Q)		;IF FIRST AND SECOND HAVE
	JUMPL	A,.+3		;DIFFERENT SIGNS, GO TO .+3.
	MOVE	A,@(Q)		;O'E, THE ANSWER = FIRST ARG.
	JRA	Q,2(Q)		;EXIT.
	MOVN	A,@(Q)		;ANSWER=-FIRST ARG.
	JFCL	1,.+2		;IF OVERFLOW, GO TO .+2.
	JRA	Q,2(Q)		;O'E, EXIT.
	HRLOI	A,377777	;ANSWER IS SET TO +INFINITY.
	JRA	Q,2(Q)		;EXIT.

	END

