TITLE	DMIN1  V.020	DOUBLE PRECISION MINIMUM VALUE FUNCTION
SUBTTL	3 MAY 1969	/TWE
		;FROM V.005, 	21 JULY 1966

;THIS ROUTINE IS CALLED IN THE FOLLOWING MANNER:
;	JSA	Q, DMIN1
;	EXP	ARG1
;	EXP	ARG2
;	.
;	.
;	.
;WHERE ARG1,ARG2,... ARE THE ADDRESSES OF THE HIGH ORDER
;PORTIONS OF DOUBLE PRECISION ARGUMENTS. THE MINIMUM OF
;THE ENTIRE SET IS RETURNED AS A DOUBLE PRECISION NUMBER
;IN ACCUMULATOR A AND B

	ENTRY	DMIN1

	A=	0
	B=	1
	C=	14
	Q=	16


	SIXBIT /DMIN1/
DMIN1:	0			;ENTRY TO DMIN1 ROUTINE
	MOVEI	B, @(Q)		;GET ADDRESS OF FIRST ARGUMENT
	MOVE	A, (B)		;GET HIGH ORDER PART OF ARGUMENT
	MOVE	B, 1(B)		;GET LOW ORDER PART OF ARGUMENT
	MOVEM	C,CSAVE		;SAVE AC C
	AOJA	Q, DMIN.2	;ADDRESS OF NEXT,START CHECKING

DMIN.1:	CAMGE	A, (C)		;IS HIGH ORDER LESS THAN NEXT ARG?
	JRST	DMIN.2		;YES, GET ADDRESS OF NEXT
	CAME	A, (C)		;NO, ARE HIGH ORDER WORDS EQUAL?
	JRST	DMIN.3		;NO, REPLACEMENT IS NECESSARY
	CAMG	B, 1(C)		;YES, CHECK LOW ORDER WORDS
	JRST	DMIN.2		;PRESENT ARGUMENT IS SMALLER
DMIN.3:	MOVE	A, (C)		;PICK UP HIGH ORDER OF NEW ARG
	MOVE	B, 1(C)		;PICK UP LOW  ORDER OF NEW ARG
DMIN.2:	MOVE	C, (Q)		;GET ADDRESS OF NEXT ARG
	TLC	C,(<JUMP>)	;COMPLEMENT OP CODE "JUMP" BITS
	TLNN	C,777000	;IS THE ARG "JUMP"?
	AOJA	Q,DMIN.1	;YES , INCREMENT AND CHECK FURTHER
	MOVE	C,CSAVE		;NO, RESTORE AC C AND EXIT
	JRA	Q,(Q)

CSAVE:	BLOCK	1	;TEMP FOR AC C

	END

