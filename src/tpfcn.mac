TITLE TPFCN.      SPECIAL TAPE FUNCTIONS FORTRAN IV
SUBTTL	V.30.0.211	09-JUN-71	/DMN

;REENT==1 FOR RE-ENTRANT OPERATING SYSTEM
IFNDEF REENT,<REENT==0>
IFN REENT,<	HISEG	>

;FROM V.005 19-SEP-66
;THIS SUBROUTINE DETERMINES WHICH SPECIAL TAPE FUNCTION IS CALLED
;FOR AND THEN JUMPS TO THE APPROPRIATE ROUTINE.

;THE ROUTINE IS CALLED BY "FORSE."  .

ENTRY	TPFCN.
EXTERN	CLOS.,FI.,FNCTN.,CLRSY.,CLROU.,CLOSI.
EXTERN	SETOU.,MSPR.,MBSR.,TNAME.

;ACCUMULATORS
	B=2			;UTILITY
	C=3			;UTILITY
	D=4			;UTILITY
	E=5			;FORMAT DESCRIPTOR
	G=15			;DEVICE NAME ADDRS.
	I=11			;FLAG REGISTER
	P=17			;PUSHDOWN POINTER
	SFCN=10			;NUMBER OF SPECIAL TAPE FUNCTIONS
IFE REENT,<LOW=0		;SO INDEXING WILL NOT CAUSE PROBLEMS>
IFN REENT,<LOW=16		;OFSET OF LOW SEGMENT DATA>

TPFCN.:	LDB	D,[POINT 4,I,17];GET SPECIAL FCN. CODE
	TLNE	E,24		;CHECK FOR DIRECTORY DEVICE OR MAG-TAPE
	CAILE	D,SFCN-1	;CHECK FOR ILLEGAL FCN
	JRST	FI.		;ILLEGAL FUNCTION
	HRRZ	B,D		;SAVE FUNC. CODE
	TLNE	E,4		;DECTAPE OR DSK?
	ADDI	B,SFCN		;NO. OF FUNCTIONS
	HLRZ	D,FCNTAB(B)	;MTAPE CODE
	HRRZ	B,FCNTAB(B)	;SPECIAL FUNCTION DISPATCH
	JRST	@B		;DO FUNCTION

FCNTAB:	XWD 1,MREW		;REWIND
	XWD 11,MREWUN		;REWIND AND UNLOAD
	XWD 7,MBSR.		;BACKSPACE RECORD
	XWD 17,MBSF		;BACKSPACE FILE
	XWD 3,MWEF		;WRITE END OF FILE
	XWD 6,MSPR.		;SKIP RECORD
	XWD 13,MWBR		;WRITE BLANK RECORD (3")
	XWD 16,MSPF		;SKIP FILE
	XWD 0,DREW
	XWD 0,DREWUN
	XWD 7,DBSR
	XWD 0,DBSF
	XWD 0,DWEF
	XWD 0,DSPR
	XWD 0,DWBR
	XWD 0,DSPF

;DECTAPE REWIND DOES CLOSE,DUMMY OUTPUT AND CLEARS FILE NAMES

DREWUN:
DREW:	PUSHJ	P,CLOS.		;DO CLOSE
	ASH	C,1		;INDEX FOR NAME
IFN REENT,<ADDI	C,(LOW)		;ADD IN OFFSET>
	SETZM	TNAME.(C)	;INPUT FILE NAME
DREW1:	SETZM	TNAME.+1(C)	;OUTPUT FILE NAME
IFN REENT,<SUBI C,(LOW)		;REMOVE OFFSET>
				;RETURN

MBSF:
MSPF:	JRST	FI.
DBSR:	TLNE	E,200000	;DSK?
	CAIE	D,7		;BACKSPACE?
	JRST	FI.		;NOT BACKSPACE DSK.
	JRST	MBSR.		;BACKSPACE DSK.
DBSF:
DSPR:
DSPF:
DWBR:	JRST	FI.		;THESE OPERATIONS ARE UNDEFINED

;DECTAPE END FILE

DWEF:	PUSHJ	P,CLOS.		;DO CLOSE
	TLNN	I,40		;DECTAPE?
	JRST	FI.		;RETURN
	ASH	C,1		;INDEX FOR FILE NAME
IFN REENT,<ADDI C,(LOW)>
	JRST	DREW1

;WRITE 3" BLANK TAPE

MWBR:	PUSHJ	P,FNCTN.	;DO FUNCTION
	PUSHJ	P,CLRSY.	;CLEAR USER-SYS SYNCH FLAG
	PUSHJ	P,CLROU.	;CLEAR OUTPUT-LAST FLAG
	JRST 	FI.		;RETURN

;MAG TAPE REWIND,UNLOAD

MREW:
MREWUN:	MOVE	B,(G)		;PICK UP DEVICE NAME
	TRZN	B,2		;TEST FOR OUTPUT LAST
	JRST	MR1		;NOT OUTPUT LAST
	PUSHJ	P,CLOS.		;CLOSE OUTPUT
	MOVEM	B,(G)		;CLEAR OUTPUT LAST
	JRST	.+2
MR1:	PUSHJ	P,CLOSI.	;INPUT CLOSE
	PUSHJ	P,FNCTN.	;DO REWIND
	JRST	FI.		;RETURN

;MAG TAPE END OF FILE

MWEF:	PUSHJ	P,CLOS.		;CLOSE BUFFERS,CLEAR SYNCH FLAG
	PUSHJ	P,SETOU.	;SET OUTPUT LAST FLAG
	JRST	FI.		;RETURN

	END

